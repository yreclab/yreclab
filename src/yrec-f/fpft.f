C $$$$$$
      SUBROUTINE FPFT(HD,HR,HS,M,OMEGA,ETA2,FP,FT,HG,R0)

      PARAMETER (JSON=5000)
      IMPLICIT LOGICAL*4(L)
      IMPLICIT REAL*8(A-H,O-Z)
      COMMON/CCOUT/LSTORE,LSTATM,LSTENV,LSTMOD,LSTPHYS,LSTROT,LSCRIB,LSTCH,LPHHD
      COMMON/CONST1/ CLN,CLNI,C4PI,C4PIL,C4PI3L,CC13,CC23,CPI
      COMMON/CONST2/CGAS,CA3,CA3L,CSIG,CSIGL,CGL,CMKH,CMKHN
      COMMON/LUOUT/ILAST,IDEBUG,ITRACK,ISHORT,IMILNE,IMODPT,ISTOR,IOWR
      COMMON/ROT/WNEW,WALPCZ,ACFPFT,ITFP1,ITFP2,LROT,LINSTB,LWNEW
      COMMON/QUADD/PHISP(JSON),PHIROT(JSON),PHIDIS(JSON),RAT(JSON)
      DIMENSION HD(JSON),HS(JSON),HR(JSON),OMEGA(JSON),FP(JSON),
     * FT(JSON),ETA2(JSON),R0(JSON),H(20),XA(20),YA(20),AINT0(10),
     * HG(JSON)
      REAL*8 MP
      SAVE

C  FIND THE RUN OF R0 AND ETA2 FOR THE MODEL
      CALL SHAPE(HD,HR,HS,1,M,OMEGA,ETA2,R0)
      B = 0.125D0*C4PI
      CG = DEXP(CLN*CGL)
      EPS = 1.0D-6
      JMAX = 2
      K = 2
      KM = K-1
C THE VALUES OF THE PREVIOUS SHELL MPHI,ETA2+2,OMEGA**2,RHO, AND R0 ARE
C NEEDED FOR THE EVALUATION OF THE DISTORTED POTENTIAL.
C THE BELOW VALUES ARE THE CENTRAL VALUES OF THE ABOVE IN ORDER.
      MP = 0.0D0
      ETA22P = 2.0D0
      W2P = OMEGA(1)**2
      RHOP = EXP(CLN*HD(1))
      R0P = 0.0D0
      AINTP = 0.0D0
      AINTT = 0.0D0
      QP = 0.0D0
C NOW CALCULATE FP AND FT USING THE ETA2 AND R0 VALUES
      DO 100 I = 1,M
         RHO = EXP(CLN*HD(I))
         GM = CG*DEXP(CLN*HS(I))
         FACT = 5.0D0*CC13*OMEGA(I)**2/(GM*(2.0D0+ETA2(I)))
         R03 = R0(I)**3
         A = FACT*R03
         SM = DEXP(CLN*HS(I))
         ETA22 = ETA2(I)+2.0D0
         W2 = OMEGA(I)**2
         H(1) = 1.0D0
         DO 40 J = 1,JMAX
C  EVALUATE THE INTEGRAL AINT FROM 0 TO R0 USING THE TRAPEZOIDAL RULE
            CALL TRAPZD(R0P,R0(I),AINT0(J),J,RHO,RHOP,SM,MP,
C      *      W2,W2P,ETA22,ETA22P,Q,QP,I)  ! KC 2025-05-31
     *      W2,W2P,ETA22,ETA22P,Q,QP)
            IF(J.GE.K) THEN
               N1 = J - KM
               DO 35 J1 = 1,K
                  XA(J1) = H(N1)
                  YA(J1) = AINT0(N1)
                  N1 = N1 + 1
   35          CONTINUE
               CALL POLINT(XA,YA,K,0.0D0,AINT1,DINT)
               IF(DABS(DINT).LT.EPS*DABS(AINT1)) GOTO 50
            ENDIF
            AINT0(J+1) = AINT0(J)
            H(J+1) = 0.25D0*H(J)
   40    CONTINUE
   50    AINT = AINTP + AINT1
C  FIND <G> AND <G-1> ACROSS THE SHELL BY GAUSSIAN QUADRATURE
         CALL QGAUSS(G0,GINV0,SPHI,B,R0,HS,AINT,Q,W2,A,I)
         HG(I) = G0/SPHI
         GINV = DLOG10(GINV0)
         F0 = C4PIL + 4.0D0*HR(I) - GINV
         FP(I) = DEXP(CLN*(F0 - CGL - HS(I)))
         FT(I) = DEXP(CLN*(F0 + C4PIL))/G0
C  OUTPUT DATA
         RPHI = DEXP(CLN*HR(I))
         RPHI3 = RPHI**3
         PHISP(I) = GM/RPHI
         PHIROT(I) =  W2*RPHI**2
         PHIDIS(I) = C4PI*CC13*AINT/RPHI3
         AINTP = AINT
         QP = Q
         MP = SM
         ETA22P = ETA22
         W2P = W2
         RHOP = RHO
         R0P = R0(I)
  100 CONTINUE

      RETURN
      END
