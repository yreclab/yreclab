C
C
C$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C ENTIME
C$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
      SUBROUTINE ENTIME(DELTS,HL,TEFFL,M,EDELT)

      PARAMETER(JSON=5000)
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT LOGICAL*4(L)
      COMMON/CONST/CLSUN,CLSUNL,CLNSUN,CMSUN,CMSUNL,CRSUN,CRSUNL,CMBOL
      COMMON/CONST1/ CLN,CLNI,C4PI,C4PIL,C4PI3L,CC13,CC23,CPI
      COMMON/CONST2/CGAS,CA3,CA3L,CSIG,CSIGL,CGL,CMKH,CMKHN
      COMMON/CONST3/CDELRL,CMIXL,CMIXL2,CMIXL3,CLNDP,CSECYR
      COMMON/CTLIM/ATIME(14),TCUT(5),TSCUT,TENV0,TENV1,TENV,TGCUT
      COMMON/OLDMOD/HPO(JSON),HTO(JSON),HRO(JSON),HLO(JSON),HDO(JSON),
     *              HCOMPP(15,JSON),HSS(JSON),LCO(JSON),LCZO(JSON),TEFFLO,MO
      COMMON/CENV/TRIDT,TRIDL,SENV0,LSENV0,LNEW0

      DIMENSION HL(JSON)
      DIMENSION HMAX(4)  ! NHMAX(4)
      SAVE

C  THIS SR LIMITS THE TIMESTEP BASED ON THE REQUIREMENT THAT THE CHANGES FROM
C  ONE MODEL TO THE NEXT IN T,AND L BE LESS THAN THE SIZE OF THE ENVELOPE TRIANGLE
C  (TRIL AND TRIT). THIS PREVENTS EXTRAPOLATION OUTSIDE OF THE TRIANGLE
C  FOR SMALL ENVELOPE TRIANGLES.
C
C INPUT VARIABLES
C DECLARED VARIABLES :
C   DELTS : PREVIOUS MODEL TIMESTEP.
C   HL : RUN OF LUMINOSITY(AS A FUNCTION OF MASS) IN THE CURRENT MODEL.
C   TEFFL: EFFECTIVE TENPERATURE OF CURRENT MODEL
C   M : NUMBER OF POINTS IN THE MODEL.
C VARIABLES IN COMMON BLOCKS :
C   TRIL: DELT L OF ENVELOPE TRIANGLE
C   TRIT: DELT T OF ENVELOPE TRIANGLE
C   ATIME(13): maximum percentage change in the timestep from the previous one
C   HLO : RUN OF LUMINOSITY(AS A FUNCTION OF MASS) IN THE PREVIOUS MODEL.
C   TEFFLO : EFFECTIVE TEMPERATURE OF PREVIOUS MODEL
C OUTPUT VARIABLES :
C   EDELT : TIME STEP FOR REQUESTED CHANGE IN P,T,R,L.
C
C FIND MAXIMUM ABSOLUTE TIME DIFFERENCES FOR EACH QUANTITY
C
C  TEMPERATURE
      HMAX(1)=TRIDT
C  LUMINSOITY
        HMAX(2)=TRIDL

        TESTT = ABS(TEFFLO-TEFFL)
      TESTL = ABS(DLOG10(HLO(MO))-DLOG10(HL(M)))

C NOW ACTUALLY LIMIT THE TIMESTEP BY A FACTOR THAT REDUCES THE
C TIME CHANGES IN ALL QUANTITIES TO THE TRIANGLE VALUES OR LESS
      FAC = TESTT/TRIDT
      IF (TESTL/TRIDL .GT.FAC) FAC=TESTL/TRIDL
C  IF NO CHANGE FROM PREVIOUS MODEL,SET EDELT TO TIMESTEP
C  STORED IN THE PREVIOUS MODEL.
      IF (FAC .EQ. 0.0D0) FAC=1.0D0
C USE ATIME(13) AS THE GLOBAL FACTOR FOR LIMITING TIMESTEP CHANGES
      TEMP = ATIME(13)
      IF (FAC.GT.TEMP) FAC=TEMP
      IF (FAC.LT.1.0D0/TEMP) FAC=1.0D0/TEMP
      EDELT = DELTS/FAC

      RETURN
      END

