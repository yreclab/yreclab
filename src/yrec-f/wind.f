C$$$$$$
C MHP 10/02 UNUSED LFIRST REMOVED FROM CALL
      SUBROUTINE WIND(BL,DELTS,HSBOT,HSTOP,JSTART,JEND,LJDOT,OMEGAS,
C      *                SJTOT,SMASS,TEFFL,HICZ,HJM,LFIRST)  ! KC 2025-05-31
     *                SMASS,TEFFL,HICZ,HJM)

      PARAMETER(JSON=5000)
      IMPLICIT REAL*8(A-H,O-Z)
      IMPLICIT LOGICAL*4(L)
      DIMENSION HJM(JSON)
      COMMON/CONST/CLSUN,CLSUNL,CLNSUN,CMSUN,CMSUNL,CRSUN,CRSUNL,CMBOL
      COMMON/CONST1/ CLN,CLNI,C4PI,C4PIL,C4PI3L,CC13,CC23,CPI
      COMMON/CONST2/CGAS,CA3,CA3L,CSIG,CSIGL,CGL,CMKH,CMKHN
      COMMON/CONST3/CDELRL,CMIXZ,CMIXL2,CMIXL3,CLNDP,CSECYR
C MHP 8/17 ADDED EXCEN, C_2 TO COMMON BLOCK FOR MATT ET AL. 2012 CENT. TERM
      COMMON/CWIND/WMAX,EXMD,EXW,EXTAU,EXR,EXM,EXL,EXPR,CONSTFACTOR,
     *             STRUCTFACTOR,EXCEN,C_2,LJDOT0
C      COMMON/CWIND/WMAX,EXMD,EXW,EXTAU,EXR,EXM,CONSTFACTOR,STRUCTFACTOR,LJDOT0
C MHP 3/09 ADDED OPTION TO SCALE THE SATURATION RATE BY THE OVERTURN TIMESCALE
      COMMON/DEUTER/DRATE(JSON),DRATE0(JSON),FMASSACC,JCZ
      COMMON/ROT/WNEW,WALPCZ,ACFPFT,ITFP1,ITFP2,LROT,LINSTB,LWNEW
      COMMON/OVRTRN/LNEWTCZ,LCALCENV,TAUCZ,TAUCZ0,PPHOT,PPHOT0,FRACSTEP
      SAVE
C IF DESIRED, REMOVE ANGULAR MOMENTUM FROM OUTER CONVECTION ZONE
C USING A WEBER-DAVIS MAGNETIC WIND MODEL
C MHP 3/09 IF WMAX > 1 THEN ASSUME THAT THE PARAMETER WMAX IS DEFINED BY
C WMAX = WMAX(SUN)*TAUCZ(SUN) AND THE SATURATION THRESHOLD WSAT = WMAX/TAUCZ(STAR)
C ONLY APPLY THIS IF ANGULAR MOMENTUM TRANSPORT ENABLED
      IF(.NOT.LINSTB)THEN
         WSAT = WMAX
      ELSE IF(WMAX.GT.1.0D0)THEN
         IF(TAUCZ.GT.1.0D0)THEN
            WSAT = WMAX/TAUCZ
c            WRITE(*,912)WSAT,TAUCZ
c 912        FORMAT('Omega sat, Tau',1p2e12.3)
         ELSE
            WRITE(*,911)WMAX,TAUCZ
 911        FORMAT('ERROR IN WIND - TAUCZ NOT DEFINED ',1P2E12.3,'STOPPED')
            STOP
         ENDIF
      ELSE
         WSAT = WMAX
      ENDIF
      IF (LJDOT) THEN
C FIND TOTAL RADIUS OF STAR.
         CGRAV = DEXP(CLN*CGL)
         RL=0.5D0*(BL+CLSUNL-C4PIL-CSIGL-4.D0*TEFFL)
         RTOT = DEXP(CLN*RL)
C DMDOT IS THE MASS LOSS RATE IN SOLAR MASSES PER YEAR.
         DMDOT = 2.0D-14
C DJ/DT = DT*CONSTFACTOR*(DMDOT/1.0D-14)**EXMD*OMEGA**EXW*(M/MSUN)**EXM
C         *(R/RSUN)**EXR
C TEST : THE LOSS RATE DEPENDS ON OMEGA, AND FOR TIMESTESP THAT ARE
C TOO LARGE, ROTATION RATES THAT ARE TOO HIGH, AND THIN SURFACE C.Z.S
C NEGATIVE SURFACE ANGULAR VELOCITIES CAN BE PRODUCED.
C TO AVOID THIS, CHECK THAT THE TIMESTEP IS SMALL ENOUGH TO ALLOW
C A POSITIVE SOLUTION FOR OMEGA IN THE FIRST GUESS AT THE LOSS RATE.
C IF NOT, USE A SERIES OF SMALL STEPS.
C MHP 12/91 CAP LOSS RATE AT WSAT.
         DWTEST = (DELTS/HICZ)*CONSTFACTOR*(DMDOT/1.0D-14)**EXMD
     *          *OMEGAS*(RTOT/CRSUN)**EXR*SMASS**EXM
     *          *MIN(OMEGAS,WSAT)**(EXW-1.0D0)
         IF(DWTEST.GT.OMEGAS)THEN
            NSTEP = INT(DWTEST/OMEGAS)+1
            DT = DELTS/DFLOAT(NSTEP)
         ELSE
            NSTEP = 1
            DT = DELTS
         ENDIF
C         WRITE(*,3)NSTEP
C    3    FORMAT(5X,I5)
         WS = OMEGAS
         DO 100 N = 1,NSTEP
C  THE CONSTANT AND EXPONENTS ARE SET IN PARMIN BASED ON THE INPUT
C  INDEX ALFA;SEE PARMIN FOR DETAILS ON THE DEPENDENCE OF EACH ON ALFA.
C  ITERATIVE SOLUTION : FOR FIRST GUESS, USE OMEGA=INITIAL OMEGA IN
C  COMPUTING LOSS RATE. TO COMPUTE SUBSEQUENT RATES, USE THE AVERAGE OF THE
C  STARTING OMEGA AND THE *PREVIOUS* ENDING OMEGA (I.E. AVERAGE EXPLICIT
C  AND IMPLICIT SOLUTIONS).
            K = 0
            W = WS
            WNEW0 = WS
    5       CONTINUE
            K = K + 1
            WNEW = WS - (DT/HICZ)*CONSTFACTOR*(DMDOT/1.0D-14)**EXMD
     *             *W*(RTOT/CRSUN)**EXR*SMASS**EXM
     *             *MIN(W,WSAT)**(EXW-1.0D0)
            DW = 2.0D0*ABS((WNEW0-WNEW)/(WNEW0+WNEW))
C         WRITE(*,4)WS,W,WNEW,DW,HICZ
C    4    FORMAT(1X,1P5E14.6)
            IF(DW.GT.1.0D-6)THEN
               W = 0.5D0*(WS+WNEW)
               WNEW0 = WNEW
               IF(K.LE.20)GOTO 5
            ENDIF
            WS = WNEW
  100    CONTINUE
C        CON = DELTS*CONSTFACTOR*(DMDOT/1.0D-14)**EXMD*OMEGAS**(EXW-1.0D0)
C    *           *(RTOT/CRSUN)**EXR*SMASS**EXM
C        FJDOT = CON*OMEGAS/(1.0D0+(EXW*CON/HICZ))
C DM IS THE TOTAL MASS IN THE CONVECTION ZONE.
         DM = HSTOP - HSBOT
C FIND CHANGE IN ANGULAR MOMENTUM PER UNIT MASS AND SUBTRACT THIS
C NUMBER FROM THE J/M OF EACH SHELL IN THE SURFACE CONVECTION ZONE.
         FJDOM = (OMEGAS-WNEW)*HICZ/DM
C         WRITE(*,11)FJDOM,HJM(JSTART),HJM(JEND)
C   11    FORMAT(5X,1P3E14.6)
C        FJDOM=FJDOT/DM
C        TAUJ=SJTOT/(FJDOT/DELTS)/CSECYR
         DO 10 II = JSTART,JEND
            HJM(II) = HJM(II) - FJDOM
  10     CONTINUE
      ENDIF
      RETURN
      END
