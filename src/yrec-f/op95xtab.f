C
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C     OP95XTAB
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      SUBROUTINE OP95XTAB(X)
c MHP 7/98 COMPUTE OPACITY TABLE AT FIXED X FROM THE OP95 TABLE AT
C THE MODEL Z
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT LOGICAL*4(L)
      PARAMETER (NUMT=70)
      PARAMETER (NUMD=19)
      PARAMETER (NUMX=10)
      PARAMETER (NUMZ=13)
      PARAMETER (NUMXZ=126)
C FULL SET OF TABLES: OPACITY AS A FUNCTION OF Z AND X, T, RHO/T6**3
C TABLES ARE INCREMENTED IN SETS OF NZ*NX.  SO THE TABLES FOR THE
C THIRD METAL ABUNDANCE (3 X 10**-4)BEGIN AT TABLE 21 AND END AT TABLE 30.
C FOR THE HIGH VALUES OF Z, THE NUMBER OF X TABLES IS NOT THE SAME (I.E.
C X<0.9 FOR Z=0.1).
C FOR EACH COMPOSITION A FULL GRID IN (T,RHO/T6**3) IS RETAINED.
      COMMON /LLOT95A/TGR(NUMT),XXG(NUMX),R0GR(NUMD),ZZG(NUMZ),
     *                CAPPA2(NUMXZ,NUMT,NUMD),NUMXM(NUMZ),NZ(NUMZ)
C GRID ENTRIES FOR TEMPERATURE, ABUNDANCE (X), AND RHO/T6**3
C OPACITY AS A FUNCTION OF X, T, AND RHO/T6**3
      COMMON /LLOT95/CAPPA(NUMX,NUMT,NUMD),ZTAB
C AS ABOVE FOR DESIRED SURFACE VALUE OF X.
      COMMON /LLOT95E/CAPPAE(NUMT,NUMD),XTAB
      DIMENSION A(4),B(4)
      SAVE
      XTAB = X
C  FIND 4 NEAREST TABLES IN X TO DESIRED VALUE.
      IF(X.LE.0.8D0)THEN
C DON'T NEED TO WORRY ABOUT MISSING X TABLES AT HIGH Z.
         DO I = 3,NUMX-1
            IF(XXG(I).GE.X)THEN
               IDX = I - 2
               GOTO 10
            ENDIF
         END DO
         IDX = NUMX - 3
 10      CONTINUE
         DO I = 1,4
            A(I) = XXG(IDX+I-1)
         END DO
      ELSE IF(ZTAB.LE.0.04D0)THEN
C HIGH X TABLES PRESENT AT LOW Z.
         DO I = 3,NUMX-1
            IF(XXG(I).GE.X)THEN
               IDX = I - 2
               GOTO 20
            ENDIF
         END DO
         IDX = NUMX - 3
 20      CONTINUE
         DO I = 1,4
            A(I) = XXG(IDX+I-1)
         END DO
      ELSE IF(ZTAB.GE.0.1D0)THEN
C USE TABLES 6-9
         IDX = 6
         DO I = 1,4
            A(I) = XXG(IDX+I-1)
         END DO
      ELSE
C IF Z IS BETWEEN 0.04 AND 0.1, TABLES 1-8 AND 10 EXIST.
C SINCE WE HAVE ALREADY DETERMINED THAT X > 0.8, USE 6-8 AND 10.
         IDX = 11
         A(1) = XXG(6)
         A(2) = XXG(7)
         A(3) = XXG(8)
         A(4) = XXG(10)
      ENDIF
C
C  FIND INTERPOLATION FACTORS IN Z.
      CALL INTRP2(A,B,X)
C INDICES FOR 4 DESIRED COMPOSITIONS.
      IF(IDX.LT.10)THEN
         I1 = IDX
         I2 = IDX + 1
         I3 = IDX + 2
         I4 = IDX + 3
      ELSE
         I1 = 6
         I2 = 7
         I3 = 8
         I4 = 10
      ENDIF
      DO J = 1,NUMT
         DO K = 1,NUMD
            CAPPAE(J,K) = B(1)*CAPPA(I1,J,K) +
     *      B(2)*CAPPA(I2,J,K) + B(3)*CAPPA(I3,J,K) +
     *      B(4)*CAPPA(I4,J,K)
         END DO
      END DO
      RETURN
      END
