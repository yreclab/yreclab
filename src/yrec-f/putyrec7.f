C
C
C$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C PUTYREC7 - Write out a stallar model in YREC7 format
C
C llp  4/16/03
C$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
      SUBROUTINE PUTYREC7(BL,CFENV,CMIXL,DAGE,DDAGE,FTRI,HCOMP,HD,HL,
C      * HP,HR,HS,HSTOT,HT,IWRITE,ISHORT,JCORE,JENV,LC,LEXCOM,LROT,M,
C      * MODEL,OMEGA,PS,RS,SMASS,TEFFL,TLUMX,TRIL,TRIT,TS,  ! KC 2025-05-31
     * HP,HR,HS,HSTOT,HT,IWRITE,JCORE,JENV,LC,LEXCOM,LROT,M,
     * MODEL,OMEGA,PS,RS,SMASS,TEFFL,TLUMX,TRIL,TRIT,TS)
C      & ATM,EOS,HIK,LDIFY,LDIFZ,LDISK,LINSTB,LJDOT0,ALOK,
C      & LOVSTC,LOVSTE,LOVSTM,LPUREZ,LSEMIC,COMPMIX,PDISK,TDISK,WMAX)  ! KC 2025-05-31
C First three lines above are YREC7 inputs
C Last two lines are MODEL2 add-ons

C     WRITE MODEL OUT IN ASCII FORMAT
      PARAMETER (JSON=5000)
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT LOGICAL*4(L)

      COMMON/CONST/CLSUN,CLSUNL,CLNSUN,CMSUN,CMSUNL,CRSUN,CRSUNL,CMBOL

c     CHARACTER*4 ATM, LOK, HIK, COMPMIX
c MHP 4/25 changed LOK name to make it unique, used elsewhere
C       CHARACTER*4 ATM,HIK,ALOK,COMPMIX
C       CHARACTER*6 EOS

      DIMENSION HCOMP(15,JSON),HD(JSON),HL(JSON),HP(JSON),HR(JSON),
     * HS(JSON),HT(JSON),LC(JSON),TRIT(3),TRIL(3),PS(3),TS(3),RS(3),
     * CFENV(9),TLUMX(8),OMEGA(JSON),OMEGAL(JSON)

      save

C PUTYREC7 writes the model provided to it out to the LU IWRITE file.
C Any error messages go to LU ISHORT.


C  THE 6 HEADER RECORDS WRITTEN FIRST

      IF (DDAGE .GE. 1D6) THEN
       WRITE(IWRITE,10) MODEL,M,SMASS,TEFFL,BL,HSTOT,DAGE,DDAGE
   10    FORMAT('NMOD ',2I5,3F8.5,1X,F14.10,F13.9,F13.0)
      ELSE
       WRITE(IWRITE,20) MODEL,M,SMASS,TEFFL,BL,HSTOT,DAGE,DDAGE
   20    FORMAT('NMOD ',2I5,3F8.5,1X,F14.10,F13.9,1PE13.6)
      ENDIF

C write PHYSICS FLAGS- ROTATION,EXTENDED COMP,CENTRAL AND SURFACE CZ'S
      WRITE(IWRITE,30)LROT,LEXCOM,JCORE,JENV,CMIXL
   30 FORMAT(2(L1,1X),'F F ',2I4,F7.3)

C write LUMINOSITIES
C If TLUMX are in solar units, convert to ergs.  Decide by
C comparing to 10**20.  If smaller, multiply by CLSUN.
      CCCMAX = DMAX1(TLUMX(1),TLUMX(2),TLUMX(3),TLUMX(4),TLUMX(5),
     *     DABS(TLUMX(6)),TLUMX(7))
      IF(CCCMAX.LE.1.0D20) THEN
       DO J = 1,7
          TLUMX(J) = TLUMX(J) * CLSUN
         ENDDO
      ENDIF
      WRITE(IWRITE,40) (TLUMX(J),J=1,7)
   40 FORMAT('TLUMX',5X,1P7E10.3)

C write ENVELOPE DATA
      DO 60 I = 1,3
      I0 = I
      IF(FTRI.LT.0D0) I0 = -I
      WRITE(IWRITE,50)I0,TRIT(I),TRIL(I),PS(I),TS(I),RS(I),
     *(CFENV(3*I-3+J),J=1,3)
   50 FORMAT('ENV',I2,F7.5,4F8.5,1P3E12.5)
   60 CONTINUE

C write HENYEY POINTS, one line per shell
      DO 110 I = 1,M
       IX = IDINT(1.0D6*HCOMP(1,I) + 0.50D0)
       IZ = IDINT(1.0D6*HCOMP(3,I) + 0.50D0)
       WRITE(IWRITE,100) HS(I),HR(I),HL(I),HP(I),HT(I),HD(I),LC(I),
     *   IX,IZ
  100    FORMAT(0PF13.10,F10.7,1PE14.7,0PF10.7,2F10.7,L1,2I6)
  110 CONTINUE

C WRITE OUT COMPOSITION ARRAY - CENTRAL AND SURFACE
C CONVECTION ZONES HAVE ONLY 1 COMPOSITION STORED PER ZONE
C IF STAR IS FULLY CONVECTIVE, WRITE OUT 1 COMP.
      IF(JENV.LE.JCORE) THEN
       WRITE(IWRITE,200)(HCOMP(I,JCORE),I=4,11)
       WRITE(IWRITE,200)(HCOMP(I,JENV),I=4,11)
      ELSE
       DO 210 J = JCORE,JENV
          WRITE(IWRITE,200) (HCOMP(I,J),I = 4,11)
  200       FORMAT(8(1PE9.3,1X))
  210    CONTINUE
      ENDIF
C EXTENDED COMP - WRITE OUT 1 ABUND IF FULLY CONVECTIVE,
C OTHERWISE WRITE OUT 2 POINTS PER LINE.
      IF(LEXCOM) THEN
       IF(JENV.LE.JCORE) THEN
          WRITE(IWRITE,220)(HCOMP(I,JENV),I=12,15)
  220       FORMAT(4(1PE9.3,1X))
       ELSE
          DO 230 K = JCORE,JENV-1,2
             WRITE(IWRITE,200)((HCOMP(I,J),I = 12,15),J = K,K+1)
  230       CONTINUE
C   IF AN ODD NUMBER OF ABUNDANCES EXISTS, WRITE IN LAST VALUE
          KTEST = JENV-1 - JCORE
          IF(MOD(KTEST,2).NE.0)
     *         WRITE(IWRITE,220)(HCOMP(I,K),I = 12,15)
       ENDIF
      ENDIF
C WRITE OUT RUN OF OMEGA,STORED 8 ELEMENTS PER LINE
C (- THE LOG OF OMEGA IS STORED)
      IF(LROT) THEN
       DO 300 I = 1,M
          IF (OMEGA(I).LE.1.D-59) OMEGA(I)=1.D-59
          OMEGAL(I) = DABS(DLOG10(OMEGA(I)))
  300    CONTINUE
       N1 = INT(M/8)
       N2 = M - N1*8
       J1 = 1
       DO 320 I = 1,N1
          WRITE(IWRITE,310)(OMEGAL(J),J = J1,J1 + 7)
  310       FORMAT(0P8F10.7)
          J1 = J1 + 8
  320    CONTINUE
       IF(N2.GT.0) WRITE(IWRITE,310)(OMEGAL(J),J=J1,J1+N2-1)
      ENDIF

      RETURN
      END
