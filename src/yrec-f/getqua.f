      SUBROUTINE GETQUA(HD,HG,HRU,OMEGA,M)

      PARAMETER (JSON=5000)
      IMPLICIT REAL*8(A-H,O-Z)
      IMPLICIT LOGICAL*4(L)
      COMMON/CONST1/ CLN,CLNI,C4PI,C4PIL,C4PI3L,CC13,CC23,CPI
      COMMON/CONST2/CGAS,CA3,CA3L,CSIG,CSIGL,CGL,CMKH,CMKHN
      COMMON/TRIDI/A(JSON),B(JSON),C(JSON),D(JSON),U(JSON),GAMA(JSON)
      COMMON/QUADRU/QUAD(JSON),GG(JSON)
      DIMENSION HD(JSON),HG(JSON),HRU(JSON),OMEGA(JSON),DW2(JSON)
      SAVE

C SOLVE FOR THE QUADRUPOLE MOMENT OF A ROTATING STAR, USING THE
C SWEET(1950) FORMULATION AS GIVEN IN ZAHN(1992).
C WE ARE SOLVING THE EQUATION
C 1/R D**2/DR2 (R*QUAD) -6*QUAD/R**2 - 4*PI*G*RHO*D(RHO)/DP * QUAD =
C  -4/3*PI*G*R**2/(GM/R**2) * D/DR (RHO*OMEGA**2)
C THIS CAN BE EXPRESSED AS D**2/DR2(QUAD) + 2/R DQUAD/DR -6*QUAD/R**2
C +4*PI*G*RHO*QUAD = 4*PI*G*RHO*R**2 *(OMEGA**2 - 2*OMEGA/G *DOMEGA/DR)
C USING HYDROSTATIC EQUILIBRIUM AND THE IDEAL GAS LAW
C D RHO/DR = DP/DR = -RHO*G.
C THIS DEFINES A TRIDIAGONAL MATRIX SYSTEM OF THE FORM
C A*QUAD(I-1)+B*QUAD(I)+C*QUAD(I+1) = D, WHICH CAN BE SOLVED IN
C THE STANDARD WAY FOR THE RUN OF QUAD(I) WITH APPROPRIATE BOUNDARY
C CONDITIONS.  THE CENTRAL B.C. IS THAT QUAD(0)=0; THE SURFACE B.C.
C IS THAT THERE IS NO CONTRIBUTION TO THE QUADRUPOLE FROM OUTSIDE THE
C FINAL SHELL, SUCH THAT QUAD(I) VARIES AS 1/R**4.
      DO I = 1,M
         DW2(I)=EXP(CLN*HD(I))*OMEGA(I)**2
      END DO
      C4PIG = EXP(CLN*(C4PIL+CGL))
C CENTER NUMERICAL DERIVATIVES, USING THE SMALLER OF THE LEFT AND
C RIGHT HANDED INTERVALS.
      DR = MIN(HRU(1),HRU(2)-HRU(1))
      FPL = DR/(HRU(2)-HRU(1))
      FMI = DR/HRU(1)
      FAC = 1.0D0/DR**2
      DRI = 1.0D0/DR/HRU(1)
C CENTRAL BOUNDARY CONDITION : QUADRUPOLE GOES TO ZERO.
      A(1) = 0.0D0
      DRHO = (EXP(CLN*HD(2))-EXP(CLN*HD(1)))/DR
      B(1) = -FAC*(FPL+FMI)+DRI*(FMI-FPL)-6.0D0/HRU(1)**2
     *        -C4PIG*DRHO/HG(1)
      C(1) = FPL*(FAC+DRI)
      D(1) = -C4PIG*DRHO*(HRU(1)*OMEGA(1))**2/3.0D0/HG(1)
C GENERAL CASE : SECOND DERIVATIVE NUMERICALLY DIFFERENTIATED AS
C
      DO I = 2,M-1
         DRPL = HRU(I+1)-HRU(I)
         DRMI = HRU(I)-HRU(I-1)
         DR = MIN(DRMI,DRPL)
         FPL = DR/DRPL
         FMI = DR/DRMI
         FAC = 1.0D0/DR**2
         DRI = 1.0D0/DR/HRU(I)
         DRHO = 0.5D0*(FPL*EXP(CLN*HD(I+1))+(FMI-FPL)*
     *          EXP(CLN*HD(I))-FMI*EXP(CLN*HD(I)))/DR
         A(I) = FMI*(FAC-DRI)
         B(I) = -FAC*(FPL+FMI)+DRI*(FMI-FPL)-6.0D0/HRU(I)**2
     *          -C4PIG*DRHO/HG(I)
         C(I) = FPL*(FAC+DRI)
         DRHOW2 = 0.5D0*(FPL*DW2(I+1) +
     *            (FMI-FPL)*DW2(I) -
     *            FMI*DW2(I-1))/DR
         D(I) = -DRHOW2*C4PIG*HRU(I)**2/3.0D0/HG(I)
      END DO
C SURFACE B.C. D PHI/DR + 3 PHI/R = 0
C THE SECOND AND THIRD TERMS IN THE EXPRESSION CANCEL IN THIS CASE.
C OUTSIDE THE STAR PHI(R) = PHI(RSTAR)*(RSTAR/R)**3.
      DR = HRU(M) - HRU(M-1)
      RPLU = HRU(M)+DR
      FX = (HRU(M)/RPLU)**3
      FAC = 1.0D0/DR**2
      A(M) = FAC
      B(M) = (FX-2.0D0)*FAC-1.2D1/HRU(M)**2
      C(M) = 0.0D0
      D(M) = 0.0D0
C  SOLUTION OF SYSTEM FROM NUMERICAL RECIPES.
      IF(B(1).EQ.0.0D0)STOP
      BET = B(1)
      U(1) = D(1)/BET
      DO J = 2,M
         GAMA(J) = C(J-1)/BET
         BET = B(J) - A(J)*GAMA(J)
         IF(BET.EQ.0.0D0)STOP
         U(J) = (D(J) - A(J)*U(J-1))/BET
      END DO
C BACKSUBSTITUTION.
      DO J = M-1,1,-1
         U(J) = U(J) - GAMA(J+1)*U(J+1)
      END DO
      DO I = 1,M
         QUAD(I) = U(I)
         GG(I) = HG(I)
      END DO

      RETURN
      END
