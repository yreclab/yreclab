C  THIS IS THE INTERPOLATION FACILITY FOR THE LIVERMORE OPACITY TABLES
C USING CUBIC SPLINE INTERPLOATION SCHEME.
C*** YLLO3D AND SETLLO ARE THE ROUTINES TO BE CALLED!
C      SUBROUTINE YLLO3D(DL,TL,X,OF,OLF,QODF,QOTF)
C      SUBROUTINE YLLO2D(T,D,M1,M2,M3,O,OL,QODF,QOTF)
C      SUBROUTINE YLLO3D2(DL,TL,X,OF,OLF,QODF,QOTF)
C      SUBROUTINE YLLO2D2(T,D,M1,M2,M3,O,OL,QODF,QOTF)
C      SUBROUTINE SETLLO(IOCTRL)                     READ LLOT
C      SUBROUTINE YLLOC                              SPLINE COEFF.
C      SUBROUTINE LL4TH(X) SURFACE TABLES
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C YLLO3D
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      SUBROUTINE YLLO3D(DL,TL,X,OF,OLF,QODF,QOTF)
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT LOGICAL*4 (L)
      PARAMETER (NUMT=50)
      PARAMETER (NUMD=17)
      PARAMETER (NUMX=3)
      PARAMETER (NUMXT=NUMT*NUMX)
      PARAMETER (NUM4D=4*NUMD)
      COMMON /GLLOT/GRIDT(NUMT),GRIDX(NUMX),GRHOT3(NUMD)
      COMMON /LLOT/OPACITY(NUMXT,NUMD),NUMXM,NUMTM
      COMMON /LLOT4/XENV4,ZENV4,CFORD4(NUMT,NUM4D),KIPM1
      COMMON /KIPMLL/M1,M2,M3
      DATA  M1,M2,M3/1,1,1/
      SAVE

      LONE=.FALSE.
C INDEPENDENT PARAMETER IN LIVERMORE OPACITY TABLE;
C T6 = LN(T/10E6)
      T6=TL-6.0D0
C RHOT3 = LN(RHO/(T/10E6)**3)
      RHOT3=DL-3.0D0*T6

      IF(DABS(XENV4-X).LE.1.0D-5)THEN
         M1=4
         LONE=.TRUE.
         GO TO 131
      ENDIF
      DO 130 IM1=1,NUMX
         IF(DABS(GRIDX(IM1)-X).LE.1.0D-5)THEN
            M1=IM1
            LONE=.TRUE.
            GO TO 131
         ENDIF
 130  CONTINUE
      CALL FINDEX(GRIDX,NUMX,X,M1)
      IF(M1.LT.0)M1=-M1
      IF(M1.LE.1.AND.RHOT3.GT.-1.0D0)M1=2
      IF(M1.GE.3)M1=2
      IF(M1.LE.0)STOP ' ERROR IN X GRID'
 131  CONTINUE
      CALL FINDEX(GRIDT,NUMTM,T6,M2)
      IF(M2.LT.0.AND.GRIDT(NUMTM).EQ.T6)M2=-M2
      IF(M2.LT.0)STOP ' T OUT OF TABLE '
      CALL YLLO2D(T6,RHOT3,M1,M2,M3,O0,OL0,QOD0,QOT0)
      IF (LONE)THEN
C USE ONLY ONE X TABLE
         OLF=OL0
         OF=O0
         QODI=QOD0
         QOTI=QOT0
      ELSE
C LINEAR EXTRAPOLATION IN X
         CALL YLLO2D(T6,RHOT3,M1+1,M2,M3,O1,OL1,QOD1,QOT1)
         GRDNT=(X-GRIDX(M1))/(GRIDX(M1+1)-GRIDX(M1))
         OLF=(OL1-OL0)*GRDNT+OL0
         QODI=(QOD1-QOD0)*GRDNT+QOD0
         QOTI=(QOT1-QOT0)*GRDNT+QOT0
         OF=10.0D0**OLF
      ENDIF
C CONVERSION FROM THE DERIVATIVE WITH CONSTANT RHOT3 TO CONSTANT RHO
      QODF=QODI
      QOTF=QOTI-3.0D0*QODF

      RETURN
      END
