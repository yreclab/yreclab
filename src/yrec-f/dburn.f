C$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C DBURN - COMPUTE THE ABUNDANCE CHANGES RESULTING FROM DEUTERIUM BURNING
C$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
      SUBROUTINE DBURN(IBEGIN,IEND,M,HS2,HCOMP,DT)
      PARAMETER(JSON=5000)
      IMPLICIT REAL*8(A-H,O-Z)
      IMPLICIT LOGICAL*4(L)
      COMMON/CONST1/ CLN,CLNI,C4PI,C4PIL,C4PI3L,CC13,CC23,CPI
      COMMON/DEUTER/DRATE(JSON),DRATE0(JSON),FMASSACC,JCZ
      COMMON/MASSCHG/DMDT0,FCZDMDT,FTOTDMDT,COMPACC(15),CREIM,
     *               LREIMER,LMDOT
      COMMON/OLDMOD/HPO(JSON),HTO(JSON),HRO(JSON),HLO(JSON),
     *     HDO(JSON),HCOMPP(15,JSON),HSS(JSON),LCO(JSON),LCZO(JSON),TEFFLO,MO
C       DATA C21/5.240358E-8/
      DIMENSION HS2(JSON),HCOMP(15,JSON)
      SAVE
      IF(IBEGIN.EQ.IEND)THEN
         X = HCOMPP(1,IBEGIN)
         XH2 = HCOMPP(12,IBEGIN)
         XHE3 = HCOMPP(4,IBEGIN)
         RATE0 = DRATE0(IBEGIN)
         RATE1 = DRATE(IBEGIN)
      ELSE
         SUMM = 0.0D0
         SUMR0 = 0.0D0
         SUMR1 = 0.0D0
         X = 0.0D0
         XH2 = 0.0D0
         XHE3 = 0.0D0
         DO I = IBEGIN,IEND
            SUMM = SUMM + HS2(I)
            SUMR0 = SUMR0 + HS2(I)*DRATE0(I)
            SUMR1 = SUMR1 + HS2(I)*DRATE(I)
            X = X + HCOMPP(1,I)*HS2(I)
            XH2 = XH2 + HCOMPP(12,I)*HS2(I)
            XHE3 = XHE3 + HCOMPP(4,I)*HS2(I)
         END DO
         RATE0 = SUMR0/SUMM
         RATE1 = SUMR1/SUMM
         X = X/SUMM
         XH2 = XH2/SUMM
         XHE3 = XHE3/SUMM
      ENDIF
      IF(LMDOT .AND. IEND.EQ.M .AND. DMDT0.GT.0.0D0)THEN
         XH2TEST = (XH2*SUMM+COMPACC(12)*FMASSACC)/(SUMM+FMASSACC)
      ELSE
         XH2TEST = XH2
      ENDIF
      IF(XH2TEST.LT.1.0D-11)THEN
         DO I = IBEGIN,IEND
            HCOMP(12,I) = 0.0D0
         END DO
         GOTO 9999
      ENDIF
C BURN DEUTERIUM IN A SERIES OF SMALL STEPS,
C INCREMENTING THE BURNING RATE FROM THE STARTING
C TO ENDING VALUES.
C RESTRICT THE MAXIMUM BURNING TO 0.5 IN THE
C NATURAL LOG (<1% ERROR FROM A SIMPLE SUM
C OF DISCRETE STEPS OF THIS SIZE).
C MHP 10/02 INITIALIZE RATE
      RATE = MAX(RATE0,RATE1)
      ISTEP = INT(RATE*DT/0.1D0)+1
      FSTEP = DFLOAT(ISTEP)
      TSTEP = DT/FSTEP
      R0 = RATE0
      DELRATE = (RATE1-RATE0)/FSTEP
      XH2NO = XH2
      DO I = 1,ISTEP
         RATE = R0 + 0.5D0*DELRATE
         XH2NO = XH2NO*EXP(-2.0D0*RATE*TSTEP)
         R0 = R0 + DELRATE
      END DO
C INCLUDE MASS ACCRETION FROM DEUTERIUM BURNING
      IF(LMDOT .AND. IEND.EQ.M)THEN
C ACCRETED MATTER IS EXPOSED TO BURNING FOR, ON
C AVERAGE. 1/2 OF THE TIMESTEP.  FMASSACC IS
C DEFINED AS DMDT*DT/ORIGINAL CZ MASS.
C BURN BOTH THE ACCRETED D AND THE ORIGINAL D
C SEPARATELY AND FIND THE NEW MASS-WEIGHTED
C AVERAGE D.  NOTE THAT ACCRETION OF ELEMENTS
C 1-11 HAS ALREADY BEEN TREATED.
         R0 = RATE0
         XH2NACC = COMPACC(12)
         XH2NA = 0.0D0
         DO I = 1,ISTEP
            RATE = R0 + 0.5D0*DELRATE
C FULLY BURN PRIOR ACCRETED MATTER
            XH2NA = XH2NA*EXP(-2.0D0*RATE*TSTEP)
C EXPOSE NEWLY ACCRETED MATTER TO 1/2 BURNING
            X2NEW = XH2NACC/FSTEP*EXP(-RATE*TSTEP)
            XH2NA = XH2NA + X2NEW
            R0 = R0 + DELRATE
         END DO
C NOW PERFORM A WEIGHTED SUM OF THE ORIGINAL
C AND NEWLY ACCRETED MATTER
         DXH2O = XH2NO - XH2
         DXH2A = XH2NA - COMPACC(12)
         XH2N = (XH2NO*SUMM+XH2NA*FMASSACC)/(SUMM+FMASSACC)
         XN = X + 0.5D0*(DXH2O*SUMM+DXH2A*FMASSACC)/(SUMM+FMASSACC)
         XHE3N = XHE3 -1.5D0*(DXH2O*SUMM+DXH2A*FMASSACC)/(SUMM+FMASSACC)
      ELSE
C INCREMENT H,D,HE3 WITHOUT MASS ACCRETION
         DXH2 = XH2NO - XH2
         XH2N = XH2NO
         XN = X + 0.5D0*DXH2
         XHE3N = XHE3 - 1.5D0*DXH2
      ENDIF
C      IF(IEND.EQ.M)THEN
C      WRITE(*,911)IBEGIN,IEND,DRATE(IBEGIN),RATE0,RATE1,
C     *            XH2,XH2N,XHE3,XHE3N
C      WRITE(*,*)DXH2O,DXH2A,FMASSACC
C      ENDIF
C 911  FORMAT(1X,'DBURN: MIX D BURNING',2I5,1P7E12.3)
      DO I = IBEGIN,IEND
         HCOMP(1,I) = XN
         HCOMP(4,I) = XHE3N
         HCOMP(12,I) = XH2N
      END DO
 9999 CONTINUE
      RETURN
      END
