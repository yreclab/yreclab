C
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C     LL95TBL
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      SUBROUTINE LL95TBL(FLIV95)
c MHP 7/98 MODIFIED TO READ IN ALL METAL ABUNDACES FOR OPACITY TABLES.
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT LOGICAL*4(L)
      PARAMETER (NUMT=70)
      PARAMETER (NUMD=19)
      PARAMETER (NUMX=10)
      PARAMETER (NUMZ=13)
      PARAMETER (NUMXZ=126)
      CHARACTER*256 FLIV95
C MHP 8/25 Removed character file names from common block
      COMMON /LOPAL95/ILIV95
      SAVE
C FULL SET OF TABLES: OPACITY AS A FUNCTION OF Z AND X, T, RHO/T6**3
C TABLES ARE INCREMENTED IN SETS OF NZ*NX.  SO THE TABLES FOR THE
C THIRD METAL ABUNDANCE (3 X 10**-4)BEGIN AT TABLE 21 AND END AT TABLE 30.
C FOR THE HIGH VALUES OF Z, THE NUMBER OF X TABLES IS NOT THE SAME (I.E.
C X<0.9 FOR Z=0.1).
C FOR EACH COMPOSITION A FULL GRID IN (T,RHO/T6**3) IS RETAINED.
      COMMON /LLOT95A/TGR(NUMT),XXG(NUMX),R0GR(NUMD),ZZG(NUMZ),
     *                CAPPA2(NUMXZ,NUMT,NUMD),NUMXM(NUMZ),NZ(NUMZ)
C INDICES FOR INTERPOLATION IN Z,X,T, AND R
      COMMON/OP95INDX/JZ,JX(4,4),JT,JD(4)
      COMMON /NEWOPAC/ZLAOL1,ZLAOL2,ZOPAL1,ZOPAL2, ZOPAL951,
     +       ZOPAL952, ZALEX1, ZALEX2, ZKUR1, ZKUR2,
     +       LLAOL89,LOPAL92,LOPAL95,LKUR90,LALEX95,L2Z
      COMMON/LUOUT/ILAST,IDEBUG,ITRACK,ISHORT,IMILNE,IMODPT,ISTOR,IOWR
C  LOCAL VECTOR, USED TO SKIP LONG HEADER.
      CHARACTER*132 DUMMY
C NUMBER OF COMPOSITION TABLES AT LOWER Z FOR EACH ABUNDANCE
      DATA NZ/0,10,20,30,40,50,60,70,80,90,100,109,118/
C NUMBER OF X TABLES AT EACH Z
      DATA NUMXM/10,10,10,10,10,10,10,10,10,10,9,9,8/
C TABULTED SET OF X
      DATA XXG/0.0D0, 0.1D0,0.2D0,0.35D0,0.5D0,0.7D0,0.8D0,0.9D0,
     *          0.95D0,1.0D0/
C TABULATED SET OF Z
      DATA ZZG/0.0D0, 0.0001D0, 0.0003D0, 0.001D0, 0.002D0,
     +         0.004D0, 0.01D0, 0.02D0, 0.03D0,
     +         0.04D0, 0.06D0, 0.08D0, 0.10D0/
c INDICES FOR INTERPOLATION
      DATA JZ,JX,JT,JD/1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1/
C     READ IN OPACITY TABLES, SKIPPING HEADER.  HEY, IT WORKS.
      OPEN(ILIV95,FILE=FLIV95,STATUS='OLD',ACCESS='SEQUENTIAL')
      NFMT=1

 10   READ(ILIV95,1,END=9999)DUMMY
      IF( DUMMY(NFMT:NFMT+4).NE.'TABLE' ) GO TO 10
  1   FORMAT(A)
      READ(DUMMY,'(36X,F7.4,11X,F7.4)')XX,ZZ
      IZ = 1
      IX = 1
      NN = 1
C ENTRY POINT FOR GETTING NEW TABLES.
 15   CONTINUE
      N = NZ(IZ)+IX
      IF(IX.LT.NUMX)THEN
         XXT = XXG(IX)
      ELSE
         XXT = 1.0D0 - ZZG(IZ)
      ENDIF
      IF(ZZG(IZ).NE.ZZ .OR. XXT.NE.XX)THEN
       WRITE(ISHORT,*)' OPAL95: Z ERROR INCOMPATIBLE TABLE'
         STOP
      ENDIF

C READ IN HEADER INFO: GRID IN RHO/T6**3
      READ(ILIV95,20,END=1000) (R0GR(I),I=1,NUMD)
 20   FORMAT(///,4X,19F7.1,/)
C READ IN FULL TABLE: LOG CAPPA AS A FUNCTION OF LOG T AND
C LOG R = RHO/T6**3
      DO I = 1,57
         READ(ILIV95,30,END=9999)TGR(I),(CAPPA2(N,I,J),J=1,NUMD)
      END DO
 30   FORMAT(F4.2,19F7.3)
C MHP 12/97 NOW TREAT CORNER WITHOUT DATA.
      READ(ILIV95,31,END=9999)TGR(58),(CAPPA2(N,58,J),J=1,NUMD-1)
 31   FORMAT(F4.2,18F7.3)

      CAPPA2(N,58,19) = 9.999D0
      DO I = 59,60
         READ(ILIV95,32,END=9999)TGR(I),(CAPPA2(N,I,J),J=1,NUMD-2)
 32      FORMAT(F4.2,17F7.3)
         DO J = 18,19
            CAPPA2(N,I,J) = 9.999D0
         END DO
      END DO
      DO I = 61,64
         READ(ILIV95,33,END=9999)TGR(I),(CAPPA2(N,I,J),J=1,NUMD-3)
 33      FORMAT(F4.2,16F7.3)
         DO J = 17,19
            CAPPA2(N,I,J) = 9.999D0
         END DO
      END DO
      DO I = 65,69
         READ(ILIV95,34,END=9999)TGR(I),(CAPPA2(N,I,J),J=1,NUMD-4)
 34      FORMAT(F4.2,15F7.3)
         DO J = 16,19
            CAPPA2(N,I,J) = 9.999D0
         END DO
      END DO
      I = 70
      READ(ILIV95,35,END=9999)TGR(I),(CAPPA2(N,I,J),J=1,NUMD-5)
 35   FORMAT(F4.2,14F7.3)
      DO J = 15,19
         CAPPA2(N,I,J) = 9.999D0
      END DO

C EXIT IF CORRECT NUMBER OF TABLES READ IN.
      IF(NN.GE.NUMXZ)GOTO 1000
      NN = NN + 1
C NEED TO ACCOUNT FOR FEWER X VALUES AT HIGHER Z.
      IF(IX.LE.8)THEN
         NMAX = NUMZ
      ELSE IF(IX.EQ.9)THEN
         NMAX = NUMZ - 3
      ELSE
         NMAX = NUMZ - 1
      ENDIF
C READ IN NEXT METAL ABUNDANCE (AT FIXED X) OR READ IN FIRST METAL
C ABUNDANCE AT NEXT X.
      IF(IZ.LT.NMAX)THEN
         IZ = IZ + 1
      ELSE
         IZ = 1
         IX = IX + 1
      ENDIF

C RETURN TO READ IN NEXT TABLE.
      READ(ILIV95,900)XX,ZZ
 900  FORMAT(/36X,F7.4,11X,F7.4)
      GOTO 15
 1000 CONTINUE

      CLOSE(ILIV95)
C  NOW GENERATE A TABLE AT A FIXED VALUE OF Z.
C  NOTE THAT FOR METAL DIFFUSION A 4-D INTERPOLATION (IN X,Z,T,RHO)
C  IS PERFORMED RATHER THAN A LINEAR INTERPOLATION BETWEEN TWO FIXED
C  Z TABLES.
      Z = ZOPAL951

      CALL OP95ZTAB(Z)
 9999 CONTINUE
      RETURN
      END
