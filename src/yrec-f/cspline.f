C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C  SUBROUTINE CSPLINE
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C REAL*8 VERSION FOR OPACITIES
C TAKEN FROM NUMERICAL RECIPES, PRESS ET AL, P88
C GIVEN ARRAYS X AND Y OF LENGTH N CONTAINING A TABULATED FUNCITION,
C I.E. Y[I] = F(X[I]) WITH X[1] < X[2] < ... X[N], AND GIVEN VALUES
C YP1 AND YPN FOR THE FIRST DERIVATIVE OF THE INTERPOLATING FUNCTION
C AT POINTS 1 AND N, RESPECTIVELY, THIS ROUTINE RETURNS AND ARRAY Y2
C OF LENGTH N WHICH CONTAINS THE SECOND DERIVATIVES OF THE INTERPOLATING
C FUNCTION AT THE TABULATED POINTS X[I].  IF YP1 AND/OR YPN ARE EQUAL
C TO 1.0E30 OR LARGER, THE ROUTINE IS SIGNALLED TO SET THE CORRESPONDING
C BOUNDARY CONDITION FOR A NATURAL SPLINE, WITH ZERO SECOND DERIVATIVE
C ON THAT BOUNDARY.
      SUBROUTINE CSPLINE(X, Y, N, YP1, YPN, Y2)
      IMPLICIT REAL*8 (A-H,O-Z)
      PARAMETER(JSON=5000)
      DIMENSION X(N), Y(N), Y2(N), U(JSON)
      SAVE
      IF (YP1 .GT. 0.99D30) THEN
         Y2(1) = 0.0D0
         U(1) = 0.0D0
      ELSE
         Y2(1) = -0.5D0
         U(1) = (3.D0/(X(2)-X(1)))*((Y(2)-Y(1))/(X(2)-X(1))-YP1)
      ENDIF
      DO I=2, N-1
         SIG = (X(I)-X(I-1))/(X(I+1)-X(I-1))
         P = SIG*Y2(I-1)+2.0D0
         Y2(I) = (SIG-1.0D0)/P
         U(I) = (6.0D0*((Y(I+1)-Y(I))/(X(I+1)-X(I))-(Y(I)-Y(I-1))
     1          /(X(I)-X(I-1)))/(X(I+1)-X(I-1))-SIG*U(I-1))/P
      END DO
      IF (YPN .GT. 0.99D30) THEN
         QN = 0.0D0
         UN = 0.0D0
      ELSE
         QN = 0.5D0
         UN = (3.D0/(X(N)-X(N-1)))*(YPN-(Y(N)-Y(N-1))/(X(N)-X(N-1)))
      END IF
      Y2(N) = (UN-QN*U(N-1))/(QN*Y2(N-1)+1.0D0)
      DO K=N-1, 1, -1
         Y2(K) = Y2(K)*Y2(K+1)+U(K)
      ENDDO
      RETURN
      END
