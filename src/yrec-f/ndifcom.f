C $$$$$$
      SUBROUTINE NDIFCOM(DT,COD2,ECOD2,EM,HD,HL,HP,HR,HS,HS1,HS2,
C      *                   HSTOT,HV,IBEG,IEND,IMAX,IMIN,LCZ,LOK,M,NTOT,  ! KC 2025-05-31
     *                   HSTOT,HV,IBEG,IEND,IMAX,IMIN,LCZ,LOK,M,
     *                   HCOMP,JBEG,JEND)
      PARAMETER (JSON=5000)
      IMPLICIT REAL*8(A-H,O-Z)
      IMPLICIT LOGICAL*4(L)
      DIMENSION COD2(JSON),ECOD2(JSON),EM(JSON),HD(JSON),HL(JSON),
     *     HP(JSON),HR(JSON),
     *     HCOMP(15,JSON),HS(JSON),HS1(JSON),HS2(JSON),HV(JSON),
     *     LCZ(JSON)
      SAVE
C  DIFCOM CALCULATES THE DIFFUSION OF COMPOSITION DUE TO ANGULAR MOMENTUM
C  TRANSPORT.  THIS IS DONE BY TRANSFORMING TO AN EQUALLY SPACED GRID IN
C  RADIUS, SOLVING A DIFFUSION EQUATION, AND TRANSFORMING BACK.
C
C  INPUT VARIABLES:
C  DT : DIFFUSION TIMESTEP(SEC).
C  COD2 : DIFFUSION COEFFICIENTS FOR COMPOSITION TRANSPORT AT THE ORIGINAL
C         MODEL POINTS.
C  ECOD2 : DIFFUSION COEFFICIENTS FOR COMPOSITION TRANSPORT AT THE EQUALLY
C          SPACED GRID POINTS.
C  EM : MASSES OF THE EQUALLY SPACED GRID POINTS(GM).
C     NOTE: FOR CONVECTIVE BOUNDARIES THE MASS OF THE LAST GRID POINT IS
C           THE MASS OF THE ENTIRE CONVECTION ZONE.
C  HCOMP : ARRAY OF MASS FRACTION OF ALL OF THE SPECIES AT THE ORIGINAL
C          MODEL POINTS.
C  HS1 : LOCATION IN MASS(UNLOGGED) OF THE ORIGINAL MODEL POINTS.
C  HS2 : MASSES OF THE ORIGINAL MODEL POINTS(UNLOGGED).
C  HV : RUN OF DIFFUSION VELOCITIES.
C  IBEG,IEND :THE FIRST/LAST UNSTABLE POINTS IN THE REGION.
C     NOTE: FOR CONVECTIVE BOUNDARIES THESE ARE ONLY THE FIRST CONVECTIVE
C           POINTS ADJACENT TO AN UNSTABLE RADIATIVE REGION.
C  IMIN : THE INNERMOST RADIATIVE ZONE OUTSIDE OF ZONE 1.
C  LCZ : FLAG WHICH TELLS WHICH OF THE ORIGINAL MODEL POINTS ARE
C        CONVECTIVE FOR ANGULAR MOMENTUM TRANSPORT PURPOSES(I.E. INCLUDES
C        OVERSHOOT REGIONS). LCZ=T IF CONVECTIVE.
C  LOK : FLAG SET T WHEN FINAL ITERATION IS BEING PERFORMED.
C  M : NUMBER OF MODEL POINTS.

C  OUTPUT VARIABLES:
C  HCOMP IS UPDATED IN DIFCOM TO GIVE THE NEW RUN OF COMPOSITION AFTER
C  ANGULAR MOMENTUM TRANSPORT.
C
C  BEFORE THE LAST ITERATION(LOK=F),ONLY DIFFUSION OF H,HE4,HE3 CALCULATED
C  TO CALCULATE CHANGE IN MU GRADIENTS CAUSED BY DIFFUSION.
C
C  WHEN LOK IS T, ONLY DIFFUSION OF SPECIES HEAVIER THAN HE4 IS PERFORMED
C  (AS DIFFUSION OF LIGHTER SPECIES HAS ALREADY BEEN DONE).
C  THE EQUALLY SPACED GRID POINTS FROM THE LAST UNSTABLE REGION SOLVED
C  ARE USED INITIALLY, AND THEN THE PROGRAM CHECKS FOR ADDITIONAL UNSTABLE
C  REGIONS INTERIOR TO THAT.
C CALL MIXCOM FOR PREVIOUSLY IDENTIFIED UNSTABLE REGION IF THIS IS NOT
C THE CONVERGED CALL
      IF(.NOT.LOK) THEN
         CALL MIXCOM(DT,ECOD2,EM,HS2,IBEG,IEND,LCZ,LOK,M,HCOMP,
     *               JBEG,JEND)
      ELSE
C FIND UNSTABLE REGIONS IN ORDER, AND CALL MIXGRID TO SET UP THE
C EQUALLY SPACED GRID AND MIXCOM TO MIX THEM IN ORDER
C  EACH UNSTABLE REGION IS SOLVED SEPARATELY STARTING HERE.
C  LTEST IS SET T IF A NON-ZERO VELOCITY IS ENCOUNTERED.
C  IBEG IS THE ZONE BELOW THE FIRST NON-ZERO V;IEND IS THE ZONE ABOVE
C  THE LAST NON-ZERO V.
         LDUM = .FALSE.
         ISTART = IMIN
   60    CONTINUE
         LTEST = .FALSE.
         DO J = ISTART,IMAX
            IF(HV(J).GT.0.0D0) THEN
               LTEST = .TRUE.
               IF(.NOT.LDUM) THEN
C  START OF UNSTABLE REGION
                  LDUM = .TRUE.
                  IBEG = J - 1
               ENDIF
            ELSE IF(LDUM) THEN
C  END OF UNSTABLE REGION
               IEND = J - 1
               LDUM = .FALSE.
               ISTART = J + 1
               GOTO 80
            ENDIF
         END DO
C  IF THE LAST INTERFACE IS UNSTABLE (NON-ZERO V) ENSURE THAT IEND IS SET
C  PROPERLY.
         IF(LDUM) IEND = IMAX
         ISTART = IMAX + 1
   80    CONTINUE
C  IF NO NON-ZERO V'S ENCOUNTERED, EXIT.
         IF(.NOT.LTEST) GOTO 90
C  TRANSFORM TO EQUAL GRID SPACING IN R FOR THE REGION.
         CALL MIXGRID(COD2,HD,HL,HP,HR,HS,HS1,HS2,
     *                HSTOT,IBEG,IEND,LCZ,M,ECOD2,EM,LDUM2)
C  LDUM2=T IF TWO ZONES IN UNSTABLE REGION;
C  SKIP IF THIS OCCURS.
         IF(LDUM2) THEN
            IF(ISTART.LE.IMAX) THEN
               GOTO 60
            ELSE
               GOTO 90
            ENDIF
         ENDIF
C  PERFORM COMPOSITION DIFFUSION.
C  UNTIL THE FINAL ITERATION, ONLY COMPOSITION DIFFUSION OF SPECIES WHICH
C  AFFECT GRADIENTS IN MEAN MOLECULAR WEIGHT IS COMPUTED (H,HE3,HE4).
C  ON THE FINAL ITERATION, DIFFUSION OF ALL SPECIES IS PERFORMED.
         CALL MIXCOM(DT,ECOD2,EM,HS2,IBEG,IEND,LCZ,LOK,M,HCOMP,
     *               JBEG,JEND)
C  RETURN FOR NEXT REGION IF APPLICABLE
         IF(ISTART.LE.IMAX) GOTO 60
   90    CONTINUE
      ENDIF
      RETURN
      END
