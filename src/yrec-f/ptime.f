C
C
C$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C PTIME
C$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
      SUBROUTINE PTIME(DELTS,HL,HP,HR,HT,M,PDELT)

      PARAMETER(JSON=5000)
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT LOGICAL*4(L)

      COMMON/CONST/CLSUN,CLSUNL,CLNSUN,CMSUN,CMSUNL,CRSUN,CRSUNL,CMBOL
      COMMON/CONST1/ CLN,CLNI,C4PI,C4PIL,C4PI3L,CC13,CC23,CPI
      COMMON/CONST2/CGAS,CA3,CA3L,CSIG,CSIGL,CGL,CMKH,CMKHN
      COMMON/CONST3/CDELRL,CMIXL,CMIXL2,CMIXL3,CLNDP,CSECYR
      COMMON/CTLIM/ATIME(14),TCUT(5),TSCUT,TENV0,TENV1,TENV,TGCUT
      COMMON/OLDMOD/HPO(JSON),HTO(JSON),HRO(JSON),HLO(JSON),HDO(JSON),
     *              HCOMPP(15,JSON),HSS(JSON),LCO(JSON),LCZO(JSON),TEFFLO,MO
      DIMENSION HT(JSON),HL(JSON),HP(JSON),HR(JSON)
      DIMENSION NHMAX(4),HMAX(4)
      SAVE

C  THIS SR LIMITS THE TIMESTEP BASED ON THE REQUIREMENT THAT THE CHANGES FROM
C  ONE MODEL TO THE NEXT IN T,P,R,AND L BE LESS THAN THE USER PARAMETERS
C  ATIME(8)-ATIME(11) AT ANY MODEL POINT.
C  THIS CRITERION DOMINATES IN THE PRE-MAIN SEQUENCE, AND THIS SR MUST BE
C  USED (LPTIME=T) TO PERFORM PRE-MAIN SEQUENCE EVOLUTION.
C
C INPUT VARIABLES
C DECLARED VARIABLES :
C   DELTS : PREVIOUS MODEL TIMESTEP.
C   HL : RUN OF LUMINOSITY(AS A FUNCTION OF MASS) IN THE CURRENT MODEL.
C   HP : RUN OF PRESSURE IN THE CURRENT MODEL.
C   HR : RUN OF RADIUS IN THE CURRENT MODEL.
C   HT : RUN OF TEMPERATURE IN THE CURRENT MODEL.
C   M : NUMBER OF POINTS IN THE MODEL.
C VARIABLES IN COMMON BLOCKS :
C   ATIME(8) : MAX DELTA LOG T (= 0.02 DEFAULT)
C   ATIME(9) :  "    "    "  P (= 0.04   "    )
C   ATIME(10):  "    "    "  R (= 0.02   "    )
C   ATIME(11):  "  DELTA L/L   (= 0.02   "    )
C   ATIME(12): maximum percentage change in the timestep from the previous one
C   HLO : RUN OF LUMINOSITY(AS A FUNCTION OF MASS) IN THE PREVIOUS MODEL.
C   HPO : RUN OF PRESSURE IN THE PREVIOUS MODEL.
C   HRO : RUN OF RADIUS IN THE PREVIOUS MODEL.
C   HTO : RUN OF TEMPERATURE IN THE PREVIOUS MODEL.
C
C OUTPUT VARIABLES :
C   PDELT : TIME STEP FOR REQUESTED CHANGE IN P,T,R,L.
C
C FIND MAXIMUM ABSOLUTE TIME DIFFERENCES FOR EACH QUANTITY
C
C  PRESSURE
      HMAX(1)=ABS(HPO(1)-HP(1))
C  TEMPERATURE
      HMAX(2)=ABS(HTO(1)-HT(1))
C  RADIUS
      HMAX(3)=ABS(HRO(1)-HR(1))
C  LUMINSOITY
      IF(HL(1)+HL(2).GT.0.0D0) THEN
       HMAX(4)=ABS((HLO(1)-HL(1))*2.D0/(HL(2)+HL(1)))
      ELSE
       HMAX(4) = 0.0D0
      ENDIF
      DO 40 I = 2,M
       TESTP = ABS(HPO(I)-HP(I))
       IF(HMAX(1).LE.TESTP) THEN
          HMAX(1) = TESTP
          NHMAX(1) = I
       ENDIF
       TESTT = ABS(HTO(I)-HT(I))
       IF(HMAX(2).LE.TESTT) THEN
          HMAX(2) = TESTT
          NHMAX(2) = I
       ENDIF
       TESTR = ABS(HRO(I)-HR(I))
       IF(HMAX(3).LE.TESTR) THEN
          HMAX(3) = TESTR
          NHMAX(3) = I
       ENDIF
       IF(HL(I)+HL(I-1).GT.0.0D0) THEN
          TESTL = ABS((HLO(I)-HL(I))*2.0D0/(HL(I)+HL(I-1)))
       ELSE
          TESTL = 0.0D0
       ENDIF
       IF(HMAX(4).LE.TESTL) THEN
          HMAX(4) = TESTL
          NHMAX(4) = I
       ENDIF
   40 CONTINUE
C NOW ACTUALLY LIMIT THE TIMESTEP BY A FACTOR THAT REDUCES THE
C TIME CHANGES IN ALL QUANTITIES TO THE PS VALUES OR LESS
      FAC = HMAX(1)/ATIME(9)
      IF (HMAX(2)/ATIME(8).GT.FAC) FAC=HMAX(2)/ATIME(8)
      IF (HMAX(3)/ATIME(10).GT.FAC) FAC=HMAX(3)/ATIME(10)
      IF (HMAX(4)/(ATIME(11)*2.3026D0).GT.FAC) THEN
        FAC=HMAX(4)/(ATIME(11)*2.3026D0)
      ENDIF
C  IF NO CHANGE FROM PREVIOUS MODEL,SET PDELT TO TIMESTEP
C  STORED IN THE PREVIOUS MODEL.
      IF (FAC.EQ.0.D0) FAC=1.0D0
C RESTRICT CHANGE IN TIMESTEP TO NO MORE THAN A FACTOR OF ATIME(12)%
C UP OR DOWN.
C      TEMP=1.0D0+ATIME(12)
C MHP 10/14 USE ATIME(13) AS THE GLOBAL FACTOR FOR LIMITING TIMESTEP CHANGES
      TEMP = ATIME(13)
      IF (FAC.GT.TEMP) FAC=TEMP
      IF (FAC.LT.1.0D0/TEMP) FAC=1.0D0/TEMP
      PDELT = DELTS/FAC

      RETURN
      END
