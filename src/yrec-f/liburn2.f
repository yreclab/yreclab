C
C
C$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C LIBURN
C$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C 11/91 HR ADDED TO CALL.
C       SUBROUTINE LIBURN2(DELTS,HCOMP,HD,HR,HS1,HS2,HT,JNENV,JNENV0,M)  ! KC 2025-05-31
      SUBROUTINE LIBURN2(DELTS,HCOMP,HR,HS1,HS2,HT,JNENV,JNENV0,M)
      PARAMETER(JSON=5000)
      IMPLICIT REAL*8(A-H,O-Z)
      IMPLICIT LOGICAL*4(L)

      COMMON/LUOUT/ILAST,IDEBUG,ITRACK,ISHORT,IMILNE,IMODPT,ISTOR,IOWR
      COMMON/CONST1/ CLN,CLNI,C4PI,C4PIL,C4PI3L,CC13,CC23,CPI
      COMMON/NEWRAT/RLI6(JSON),RLI7(JSON),RBE9(JSON)
      COMMON/OLDMOD/HPO(JSON),HTO(JSON),HRO(JSON),HLO(JSON),HDO(JSON),
     *              HCOMPP(15,JSON),HSS(JSON),LCO(JSON),LCZO(JSON),TEFFLO,MO
      COMMON/OLDRAT/RLI60(JSON),RLI70(JSON),RBE90(JSON)
      COMMON/SCRTCH/SESUM(JSON),SEG(7,JSON),SBETA(JSON),SETA(JSON),
     * LOCONS(JSON),SO(JSON),SDEL(3,JSON),SFXION(3,JSON),SVEL(JSON),SCP(JSON)
C MHP 10/91 COMMON BLOCK ADDED FOR OVERSHOOT.
      COMMON/DPMIX/DPENV,ALPHAC,ALPHAE,ALPHAM,BETAC,IOV1,IOV2,
     *      IOVIM, LOVSTC, LOVSTE, LOVSTM, LSEMIC, LADOV, LOVMAX
C MHP 11/91 COMMON BLOCK ADDED FOR OVERSHOOT.
      COMMON/LIOV/PSCAL0,PSCAL1
C MHP 11/91 COMMON BLOCKS ADDED FOR LITHIUM BURNING IN ROTATING
C MODELS WITH LINSTB=T.
      COMMON/ROT/WNEW,WALPCZ,ACFPFT,ITFP1,ITFP2,LROT,LINSTB,LWNEW
      COMMON/MDPHY/AMUM(JSON),CPM(JSON),DELM(JSON),DELAM(JSON),
     *             DELRM(JSON),ESUMM(JSON),OM(JSON),QDTM(JSON),
     *             THDIFM(JSON),VELM(JSON),VISCM(JSON),EPSM(JSON)
C 11/91 COMMON BLOCK ADDED FOR THE EXACT LOCATION OF THE BASE OF THE
C SURFACE CZ AND THE INTEGRATED BURNING RATES THROUGHOUT IT. USED TO
C REMEMBER THE PREVIOUS "END OF TIMESTEP VALUES" FOR THE NEW
C "BEGINNING OF TIMESTEP VALUES".
      COMMON/PREVCZ/RBASE0,BLI6OLD,BLI7OLD,BBE9OLD,JENVOS
C       DIMENSION HCOMP(15,JSON),HD(JSON),HS2(JSON),HT(JSON),HS1(JSON)  ! KC 2025-05-31
      DIMENSION HCOMP(15,JSON),HS2(JSON),HT(JSON),HS1(JSON)
      DIMENSION RATLI6(JSON),RATLI7(JSON),RATBE9(JSON)
      DIMENSION HR(JSON)
C       DATA TLIM/6.0D0/
      SAVE
C DETERMINE LITHIUM AND BERYLLIUM BURNING.
C THE BURNING RATES DEPEND ON THE LOCAL T AND RHO, THE ABUNDANCE, AND
C IN THE CASE OF CONVECTION ZONES THE MIXING.
C
C IN RADIATIVE REGIONS BURNING IS DONE IMPLICITLY.
C IN CONVECTIVE REGIONS THE PROGRAM ITERATES BETWEEN BURNING AND MIXING
C TO CORRECT THE RATES FOR THE CHANGE IN ABUNDANCE AS A FUNCTION OF TIME.
C THE DEGREE OF LITHIUM BURNING IN A SURFACE CZ DEPENDS SENSITIVELY
C ON THE TEMPERATURE AT ITS BASE - SO ACCURATELY LOCATING IS IMPORTANT.
C DETERMINE THE TRUE LOCATION (FX) OF THE BASE OF THE CZ AT THE END
C OF THE TIMESTEP, AND THE LOCATION OF THE EDGE OF OVERSHOOT REGIONS
C IF APPLICABLE.
      IF(JNENV.GT.1.AND.JNENV.LT.M)THEN
         IF(LROT.AND.LINSTB)THEN
            DDJ = DELAM(JNENV) - DELRM(JNENV)
            DDJM1 = DELAM(JNENV-1) - DELRM(JNENV-1)
         ELSE
C EVALUATE DEL(AD) - DEL(RAD) AT THE LAST CONVECTIVE POINT AND THE ONE
C BELOW IT.
            DDJ = SDEL(3,JNENV)-SDEL(1,JNENV)
            DDJM1 = SDEL(3,JNENV-1)-SDEL(1,JNENV-1)
         ENDIF
C USE LINEAR INTERPOLATION TO FIND THE DISTANCE OF THE TRUE LOCATION
C OF THE BASE FROM THE ZONE MIDPOINT. IF FX IS NEGATIVE,THEN THE TRUE
C BASE IS HIGHER; IF IT IS POSITIVE, THE TRUE BASE IS LOWER.
         FX = MAX(-0.5D0,0.5D0-DDJM1/(DDJM1-DDJ))
         FX = MIN(0.5D0,FX)
         IF(.NOT.LOVSTE)THEN
            JENV = JNENV
            JENV0 = JNENV0
         ELSE
C STARTING CZ DEPTH
            IF(RBASE0.EQ.0.0D0)THEN
               RBASE0 = 0.5D0*(EXP(CLN*HRO(JNENV0))
     *                  +EXP(CLN*HRO(JNENV0-1)))
               ROS = RBASE0 - PSCAL0
               DO J = JNENV0-1,1,-1
                  R = EXP(CLN*HRO(J))
                  IF(R.LT.ROS)THEN
                     JENV0 = J + 1
                     GOTO 11
                  ENDIF
               END DO
               JENV0 = 1
   11          CONTINUE
            ELSE
               JENV0 = JENVOS
            ENDIF
C ENDING CZ DEPTH : DETERMINE OVERSHOOT FROM TRUE CZ BASE.
            DR = EXP(CLN*HR(JNENV))-EXP(CLN*HR(JNENV-1))
            RBASE = 0.5D0*(EXP(CLN*HR(JNENV))+EXP(CLN*HR(JNENV-1)))
     *              -FX*DR
            RBASE0 = RBASE
            ROS = RBASE - PSCAL1
            DO J = JNENV-1,1,-1
               R = EXP(CLN*HR(J))
               IF(R.LT.ROS)THEN
                  JENV = J + 1
                  DR = EXP(CLN*HRO(J+1))-R
                  FX = 0.5D0-((ROS-R)/DR)
                  FX = MAX(-0.5D0,FX)
                  FX = MIN(0.5D0,FX)
                  GOTO 12
               ENDIF
            END DO
            JENV = 1
   12       CONTINUE
         ENDIF
      ELSE
         JENV = JNENV
         JENV0 = JNENV0
      ENDIF
      JENVOS = JENV
C RADIATIVE INTERIOR.
      MINJ = MIN(JENV,JENV0)
      MAXJ = MAX(JENV,JENV0)
      DO I = 1,MINJ-1
         IF(RBE9(I).LE.1.0D-32 .OR. RBE90(I).LE.1.0D-32)GOTO 50
         IF(HCOMP(13,I).LT.1.0D-24.AND.HCOMP(14,I).LT.1.0D-24
     *   .AND.HCOMP(15,I).LT.1.0D-24)GOTO 50
         IF(HT(I).GT.7.0D0)THEN
            HCOMP(13,I) = 0.0D0
            HCOMP(14,I) = 0.0D0
            HCOMP(15,I) = 0.0D0
            GOTO 50
         ENDIF
         TLI6 = 0.5D0*(LOG(RLI6(I)) + LOG(RLI60(I)))
         TLI7 = 0.5D0*(LOG(RLI7(I)) + LOG(RLI70(I)))
         TBE9 = 0.5D0*(LOG(RBE9(I)) + LOG(RBE90(I)))
         RATLI6(I) = DELTS*EXP(TLI6)
         RATLI7(I) = DELTS*EXP(TLI7)
         RATBE9(I) = DELTS*EXP(TBE9)
C THE REACTION RATES ARE OF THE FORM
C    DLI/DT = F(RHO,T,X)*LI =>DLN LI = DT*F(RHO,T,X)
C SOLVE FOR D LN (SPECIES)/DT AND ZERO OUT IF THE DEPLETION IS TOO HIGH.
         IF(RATLI6(I).LT.3.0D1)THEN
            HCOMP(13,I) = HCOMP(13,I)/EXP(RATLI6(I))
         ELSE
            HCOMP(13,I) = 0.0D0
         ENDIF
         IF(RATLI7(I).LT.3.0D1)THEN
            HCOMP(14,I) = HCOMP(14,I)/EXP(RATLI7(I))
         ELSE
            HCOMP(14,I) = 0.0D0
         ENDIF
         IF(RATBE9(I).LT.3.0D1)THEN
            HCOMP(15,I) = HCOMP(15,I)/EXP(RATBE9(I))
         ELSE
            HCOMP(15,I) = 0.0D0
         ENDIF
C WRITE NEW ABUNDANCES AND EXIT.
         IF(HCOMP(13,I).LT.1.0D-24)HCOMP(13,I)=0.0D0
         IF(HCOMP(14,I).LT.1.0D-24)HCOMP(14,I)=0.0D0
         IF(HCOMP(15,I).LT.1.0D-24)HCOMP(15,I)=0.0D0
   50    CONTINUE
      END DO
C CONVECTION ZONE.
C
C SKIP IF WHOLE CZ IS BELOW THE BURNING THRESHOLD.
      IF(RBE90(JENV0).LE.1.0D-32.OR.RBE9(JENV).LE.1.0D-32)GOTO 200
C FIND RATES AT THE BEGINNING OF THE TIMESTEP (USING THE DEPTH AT THE START).
      FLI60 = 0.0D0
      FLI70 = 0.0D0
      FBE90 = 0.0D0
      FM0 = 0.0D0
      DO 65 I = JENV0,M
         FLI60 = FLI60+HCOMP(13,I)*HS2(I)
         FLI70 = FLI70+HCOMP(14,I)*HS2(I)
         FBE90 = FBE90+HCOMP(15,I)*HS2(I)
         FM0 = FM0 + HS2(I)
   65 CONTINUE
C    67 CONTINUE
      FLI60 = FLI60/FM0
      FLI70 = FLI70/FM0
      FBE90 = FBE90/FM0
      IF(BLI6OLD.LE.0.0D0)THEN
C COMPUTE MASS-WEIGHTED AVERAGE RATES AT THE START OF THE STEP.
         BLI60 = 0.0D0
         BLI70 = 0.0D0
         BBE90 = 0.0D0
         DO 68 I = JENV0,M
            BLI60 = BLI60 + RLI60(I)*HS2(I)
            BLI70 = BLI70 + RLI70(I)*HS2(I)
            BBE90 = BBE90 + RBE90(I)*HS2(I)
   68    CONTINUE
         BLI60 = LOG(BLI60/FM0)
         BLI70 = LOG(BLI70/FM0)
         BBE90 = LOG(BBE90/FM0)
      ELSE
C USE THE RATE FROM THE END OF THE PREVIOUS TIMESTEP.
         BLI60 = BLI6OLD
         BLI70 = BLI7OLD
         BBE90 = BBE9OLD
      ENDIF
C USE THE LOCATION OF THE TRUE EDGE OF THE CONVECTION ZONE (FX, FOUND AT
C BEGINNING OF SR) TO ADJUST THE BURNING RATE AND MASS OF THE BOTTOM POINT
C SUCH THAT IT INCLUDES THE ENTIRE C.Z.
      HS2SAV = HS2(JENV)
      IF(JENV.GT.1.AND.JENV.LT.M)THEN
         HS2(JENV) = HS2(JENV)+FX*(HS1(JENV)-HS1(JENV-1))
         RLI6(JENV) = RLI6(JENV)+0.5D0*FX*(RLI6(JENV-1)-RLI6(JENV))
         RLI7(JENV) = RLI7(JENV)+0.5D0*FX*(RLI7(JENV-1)-RLI7(JENV))
         RBE9(JENV) = RBE9(JENV)+0.5D0*FX*(RBE9(JENV-1)-RBE9(JENV))
      ENDIF
C FIND RATES AT THE END OF THE TIMESTEP (USING THE DEPTH AT THE END).
C ALSO STORE INITIAL ABUNDANCES(FLI60,FLI70,FBE90).
C FM IS THE TOTAL MASS IN THE CZ.
      FLI6 = 0.0D0
      FLI7 = 0.0D0
      FBE9 = 0.0D0
      FM = 0.0D0
      BLI6 = 0.0D0
      BLI7 = 0.0D0
      BBE9 = 0.0D0
      DO 70 I = JENV,M
         BLI6 = BLI6 + RLI6(I)*HS2(I)
         BLI7 = BLI7 + RLI7(I)*HS2(I)
         BBE9 = BBE9 + RBE9(I)*HS2(I)
         FLI6 = FLI6+HCOMP(13,I)*HS2(I)
         FLI7 = FLI7+HCOMP(14,I)*HS2(I)
         FBE9 = FBE9+HCOMP(15,I)*HS2(I)
         FM = FM + HS2(I)
   70 CONTINUE
C    75 CONTINUE
      FLI6 = FLI6/FM
      FLI7 = FLI7/FM
      FBE9 = FBE9/FM
C MASS-WEIGHTED AVERAGE RATES AT THE END OF THE STEP.
      BLI6 = LOG(BLI6/FM)
      BLI7 = LOG(BLI7/FM)
      BBE9 = LOG(BBE9/FM)
C RESTORE ORIGINAL MASS TO BASE OF C.Z.
      HS2(JENV) = HS2SAV
C FOR THE STARTING ABUNDANCE, USE THE MIXED ABUNDANCE FOR THE
C DEEPER CZ.
      IF(JENV.LT.JENV0)THEN
         FLI60 = FLI6
         FLI70 = FLI7
         FBE90 = FBE9
      ENDIF
C INITIALIZE ABUNDANCES.
         FLI6 = FLI60
         FLI7 = FLI70
         FBE9 = FBE90
C TIME-AVERAGED RATES USING LINEAR INTERPOLATION IN THE LOG.
         CZLI6 = 0.5D0*(BLI6+BLI60)
         CZLI7 = 0.5D0*(BLI7+BLI70)
         CZBE9 = 0.5D0*(BBE9+BBE90)
         DLI6 = DELTS*EXP(CZLI6)
         DLI7 = DELTS*EXP(CZLI7)
         DBE9 = DELTS*EXP(CZBE9)
         IF(DLI6.LT.3.0D1 .AND. FLI6.GT.1.0D-24)THEN
            FLI6 = FLI6/EXP(DLI6)
         ELSE
            FLI6 = 0.0D0
         ENDIF
         IF(FLI6.LT.1.0D-24)FLI6 = 0.0D0
         IF(DLI7.LT.3.0D1 .AND. FLI7.GT.1.0D-24)THEN
            FLI7 = FLI7/EXP(DLI7)
         ELSE
            FLI7 = 0.0D0
         ENDIF
         IF(FLI7.LT.1.0D-24)FLI7 = 0.0D0
         IF(DBE9.LT.3.0D1 .AND. FBE9.GT.1.0D-24)THEN
            FBE9 = FBE9/EXP(DBE9)
         ELSE
            FBE9 = 0.0D0
         ENDIF
         IF(FBE9.LT.1.0D-24)FBE9 = 0.0D0
      DO I = MAXJ,M
         HCOMP(13,I) = FLI6
         HCOMP(14,I) = FLI7
         HCOMP(15,I) = FBE9
      END DO
C STORE ENDING RATE FOR USE AT THE BEGINNING OF THE NEXT STEP.
      BLI6OLD = BLI6
      BLI7OLD = BLI7
      BBE9OLD = BBE9
C NOW SOLVE FOR ABUNDANCES IN THE REGION WHICH BEGAN CONVECTIVE AND
C ENDED RADIATIVE.
      IF(JENV.LE.JENV0)GOTO 200
C FIND STARTING AND ENDING LOCATION IN MASS OF THE CZ BASE, AND
C ASSUME THAT THE FRACTION OF TIME SPENT IN THE CZ IS PROPORTIONAL TO
C THE LOCATION IN MASS (I.E. A POINT 1/3 FROM THE OLD TO THE NEW CZ
C BASE IN MASS SPENT 1/3 OF THE TIME IN THE CZ AND 2/3 OUT OF IT).
      IF(JENV0.GT.1)THEN
         HSBEG = 0.5D0*(HS1(JENV0)+HS1(JENV0-1))
      ELSE
         HSBEG = 0.0D0
      ENDIF
      HSEND = 0.5D0*(HS1(JENV)+HS1(JENV-1))
      DMASS = HSBEG - HSEND
      DO I = JENV0,JENV-1
C MHP 9/91 CHANGE TO AVOID DIVISION BY ZERO.
C SKIP IF SHELL TEMPERATURE DROPS BELOW BURNING THRESHOLD.
         IF(RBE9(I).LE.1.0D-32)GOTO 200
         FRAD = (HS1(I)-HSBEG)/DMASS
C USE FRAD*RADIATIVE RATE AND (1-FRAD)*CONVECTIVE RATE.
         DLI6 = DELTS*EXP(FRAD*LOG(RLI6(I))+(1.0D0-FRAD)*BLI60)
         DLI7 = DELTS*EXP(FRAD*LOG(RLI7(I))+(1.0D0-FRAD)*BLI70)
         DBE9 = DELTS*EXP(FRAD*LOG(RBE9(I))+(1.0D0-FRAD)*BBE90)
C***REMEMBER TO ADD FAILSAFES FOR LARGE DEPLETION***
         HCOMP(13,I) = HCOMP(13,I)/EXP(DLI6)
         IF(HCOMP(13,I).LT.1.0D-24)HCOMP(13,I)=0.0D0
         HCOMP(14,I) = HCOMP(14,I)/EXP(DLI7)
         IF(HCOMP(14,I).LT.1.0D-24)HCOMP(14,I)=0.0D0
         HCOMP(15,I) = HCOMP(15,I)/EXP(DBE9)
         IF(HCOMP(15,I).LT.1.0D-24)HCOMP(15,I)=0.0D0
      END DO
 200  CONTINUE
      RETURN
      END
