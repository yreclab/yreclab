CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C MICRODIFF
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      SUBROUTINE MICRODIFF(DT,HCOMP,HQPR,HR,HD,HS1,HT,LC,M,STOT)

      PARAMETER (JSON=5000)
C SET NLIGHT TO THE NUMBER OF LIGHT ELEMENTS TO BE DIFFUSED.
      PARAMETER (NLIGHT=3)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      IMPLICIT REAL*8(A-H,O-Z)
      IMPLICIT LOGICAL*4(L)
      COMMON/GRAVST/GRTOL,ILAMBDA,NITER_GS,LDIFY
C MHP 3/94 ADDED METAL DIFFUSION
      COMMON/GRAVS3/FGRY,FGRZ,LTHOUL,LDIFZ
      COMMON/GRAVS4/LNEWDIF,LDIFLI
      COMMON/CONST1/CLN,CLNI,C4PI,C4PIL,C4PI3L,CC13,CC23,CPI
      COMMON/LUOUT/ILAST,IDEBUG,ITRACK, ISHORT,IMILNE,IMODPT,ISTOR,IOWR
      DIMENSION HCOMP(15,JSON),HQPR(JSON),HR(JSON),HS1(JSON),HT(JSON),
     *          HD(JSON),HRU(JSON),HTU(JSON),LC(JSON),ODEN(JSON),
C KC 2025-05-30 fixed -Wconversion
C      *          OTEM(JSON),CSLIGHT(NLIGHT),ASLIGHT(NLIGHT),ZSLIGHT(NLIGHT)
     *          OTEM(JSON),ASLIGHT(NLIGHT),ZSLIGHT(NLIGHT)
      INTEGER CSLIGHT(NLIGHT)
      DIMENSION EM(JSON),ER(JSON),EDEN(JSON),ETEM(JSON),EHQPR(JSON),
     *          EDELR(JSON),EX(JSON),EY(JSON),EZ(JSON),ELI(NLIGHT,JSON),
     *          ESPEC(3,JSON),EDXDR(JSON)
      DIMENSION EM_H(JSON),ER_H(JSON),EDEN_H(JSON),ETEM_H(JSON),EHQPR_H(JSON),
     *          EDELR_H(JSON),EX_H(JSON),EY_H(JSON),EZ_H(JSON),
     *          ELI_H(NLIGHT,JSON),ESPEC_H(3,JSON),EDXDR_H(JSON),
     *          EDELX(JSON),EDELZ(JSON),EDELLI(NLIGHT,JSON)
C
C     SET UP VECTORS FOR LIGHT ELEMENT DIFFUSION. THE LENGTH
C     OF EACH SHOULD EQUAL NLIGHT.
      DATA CSLIGHT/13,14,15/
      DATA ASLIGHT/6.015D0,7.016D0,9.012D0/
      DATA ZSLIGHT/3.0D0,3.0D0,4.0D0/
      SAVE

C  GARRETT SOMERS - 05/2015
C  MICRODIFF IS AN UPDATED HELIUM, METAL, AND LIGHT ELEMENT DIFFUSION
C  ROUTINE. MUCH IS ADAPTED FROM GRSETT.F, WHICH IS RETAINED FOR BACKWARDS
C  COMPATABILITY. FOLLOWS THE FORMULISM OF BAHCALL AND LOEB 1989.
C
C  MICRODIFF PERFORMS THE FOLLOWING OPERATIONS:
C  1) TRANSFORMS PYHSICAL VARIABLES TO AN EQUALLY SPACED GRID IN RADIUS
C     (AND MANIPULATES THE MODEL VARIABLES INTO BAHCALL AND LOEB UNITS).
C  2) CALCULATES DIFFUSION COEFFICIENTS ALONG THE GRID FOR Y, Z, AND FOR
C     LIGHT ELEMENTS. THE DIFFUSION EQUATION HAS TWO TERMS : ONE THAT DEPENDS
C     ON DLNP/DR*DX/DR AND ONE THAT DEPENDS ON D^2X/DR^2. THE DIFFUSION
C     COEFFICIENTS ARE THEMSELVES FUNCTIONS OF BOTH THE THERMAL STRUCTURE,
C     ABUNDANCE, AND THE HYDROGEN GRADIENT.
C  3) SOLVES THIS DIFFUSION EQUATION IN TWO STEPS. THE FIRST TERM IS SOLVED
C     EXPLICITLY USING THE TWO-STEP LAX-WENDROFF SCHEME (NUMERICAL RECIPES,
C     PRESS ET AL. 1986,CAMBRIDGE UNIVERSITY PRESS,P.633). DIFFUSION
C     COEFFICIENTS ARE RECALCUALTED AT THE GRID MID-POINTS IN THE PROCESS.
C  4) THE SECOND TERM IS SOLVED IMPLICITLY, USING THE MODIFIED RUN OF
C     ABUNDANCE FOUND IN STEP 3) AS THE 'INITIAL' RUN OF ABUNDANCE.
C     THE IMPLICIT SCHEME ITERATES ON THE DIFFUSION COEFFICIENTS UNTIL
C     THE SOLUTION CONVERGES TO WITHIN THE USER-SPECIFIED TOLERANCE GRTOL.
C  5) UPDATES ABUNDANCE ARRAYS, TRANSFORMS BACK TO THE MODEL GRID, AND EXITS
C
C  DT - TIMESTEP (SEC)
C  HCOMP - RUN OF MASS FRACTIONS OF SPECIES.HCOMP(1,..)=X,HCOMP(2,...)=Y
C          HCOMP(3,...)=Z
C  HQPR - DLNP/DR
C  HR - LOG RADIUS (CM)
C  HS1 - MASS (GM), UNLOGGED
C  HT - LOG TEMPERATURE (K)
C  LC - FLAG T/F FOR CONVECTION
C  M - NUMBER OF MODEL POINTS
C  [COMMON] SDEL(2,...) - DEL (=DLNT/DLNP)
C
C  OUTPUT VARIABLES :
C
C  NEW RUN OF HCOMP(1-N,...).
C
C  CONVERT MODEL QUANTITIES TO BAHCALL AND LOEB UNITS; LOCATE
C  BOUNDARIES OF CONVECTIVE CORE AND ENVELOPE IF APPLICABLE;
C  COLLECT PHYSICAL VARIABLES FOR DIFFCO CALCULATION.
C
      CALL MICRODIFF_SETUP(DT,HQPR,HR,HD,HS1,HT,LC,M,STOT,HCOMP,HRU,HTU,
     *            IBEGIN,IEND,LALLCZ,ODEN,OTEM)
C
C SKIP SETTLING FOR FULLY CONVECTIVE MODELS.
      IF(LALLCZ) RETURN
C  TRANSFORM TO AN EQUALLY SPACED GRID IN RADIUS.
C  NOTE : PREFIX E ALONE=VARIABLE AT EQUALLY SPACED GRID POINTS.
C         PREFIX E + SUFFIX _H= VARIABLE AT MIDPOINT BETWEEN EQUALLY
C         SPACED GRID POINTS.(BOTH ARE NEEDED FOR THE DIFFUSION TECHNIQUE).
C
C       CALL MICRODIFF_MTE(NLIGHT,CSLIGHT,HCOMP,HQPR,HRU,HS1,HTU,IBEGIN,
C      *               IEND,M,DR,NPT,LC,ODEN,OTEM,EM,ER,EDEN,ETEM,EHQPR,EDELR,  ! KC 2025-05-31
      CALL MICRODIFF_MTE(NLIGHT,CSLIGHT,HCOMP,HQPR,HRU,HS1,IBEGIN,
     *               IEND,M,DR,NPT,ODEN,OTEM,EM,ER,EDEN,ETEM,EHQPR,EDELR,
     *               EX,EY,EZ,ELI,EM_H,ER_H,EDEN_H,ETEM_H,EHQPR_H,EDELR_H,
     *               EX_H,EY_H,EZ_H,ELI_H)
C
C  PARTIALLY CONSTRUCT THE VECTORS ESPEC AND ESPEC_H, WHICH ARE THE
C  THREE SPECIES USED IN THE THOUL CALCULATION. 1=X, 2=Y, 3=METAL.
      DO I=1,NPT-1
         ESPEC(1,I) = EX(I)
         ESPEC(2,I) = EY(I)
         ESPEC_H(1,I) = EX_H(I)
         ESPEC_H(2,I) = EY_H(I)
      ENDDO
      ESPEC(1,NPT) = EX(NPT)
      ESPEC(2,NPT) = EY(NPT)
c
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C  WITH THE EQUAL GRID CALCULATED, RUN THE DIFFUSION ROUTINE FOR EACH
C  DESIRED ELEMENT. PASS IN EQUAL GRID AND THREE PARAMETERS:
C     J = THOUL COLUMN OF DIFFUSED SPECIES (1 FOR X, 3 OTHERWISE)
C     THEA = ATOMIC WEIGHT OF DIFFUSED SPECIES
C     THEZ = ATOMIC CHARGE OF DIFFUSED SPECIES
C  IN THE CASE OF HYDROGEN DIFFUSION, PASS IN THEA/THEZ FOR IRON SINCE
C  THESE VALUES ARE USED FOR THE METAL ABUNDANCE IN THOUL.
c
C     DIFFUSE HYDROGEN.
      IF(LDIFY)THEN
         J = 1
         THEA = 55.86D0
         THEZ = 26.0D0
C        SET ESPEC(3,*) TO THE HEAVY METAL ABUNDANCE.
         DO I=1,NPT-1
            ESPEC(3,I) = EZ(I)
            ESPEC_H(3,I) = EZ_H(I)
         ENDDO
         ESPEC(3,NPT) = EZ(NPT)
C        PASS THE EVEN GRID INTO MICRODIFF_RUN TO PERFORM THE DIFFUSION.
C          CALL MICRODIFF_RUN(NLIGHT,CSLIGHT,DR,DT,STOT,NPT,EM,ER,EDEN,ETEM,  ! KC 2025-05-31
         CALL MICRODIFF_RUN(DR,DT,STOT,NPT,EM,ER,EDEN,ETEM,
     *               EHQPR,EDELR,ESPEC,EDXDR,EM_H,ER_H,EDEN_H,ETEM_H,EHQPR_H,
     *               EDELR_H,ESPEC_H,EDXDR_H,THEA,THEZ,J)
C        STORE THE RUN OF CHANGES TO HYDROGEN
         DO I = 1,NPT
            EDELX(I) = ESPEC(1,I)
         ENDDO
C        RESTORE THE ORIGINAL ESPEC HYDROGEN VECTORS.
         DO I = 1,NPT-1
            ESPEC(1,I) = EX(I)
            ESPEC_H(1,I) = EX_H(I)
         ENDDO
         ESPEC(1,NPT) = EX(NPT)
      ENDIF
c
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C     DIFFUSE HEAVY METALS.
      IF(LDIFZ)THEN
         J = 3
         THEA = 55.86D0
         THEZ = 26.0D0
C        SET ESPEC(3,*) TO THE HEAVY METAL ABUNDANCE.
         DO I=1,NPT-1
            ESPEC(3,I) = EZ(I)
            ESPEC_H(3,I) = EZ_H(I)
         ENDDO
         ESPEC(3,NPT) = EZ(NPT)
C        PASS THE EVEN GRID INTO MICRODIFF_RUN TO PERFORM THE DIFFUSION.
C          CALL MICRODIFF_RUN(NLIGHT,CSLIGHT,DR,DT,STOT,NPT,EM,ER,EDEN,ETEM,  ! KC 2025-05-31
         CALL MICRODIFF_RUN(DR,DT,STOT,NPT,EM,ER,EDEN,ETEM,
     *               EHQPR,EDELR,ESPEC,EDXDR,EM_H,ER_H,EDEN_H,ETEM_H,EHQPR_H,
     *               EDELR_H,ESPEC_H,EDXDR_H,THEA,THEZ,J)
C        STORE THE RUN OF CHANGES TO HEAVY METALS
         DO I=1,NPT
            EDELZ(I) = ESPEC(3,I)
         ENDDO
C        THE ORIGINAL HEAVY VECTOR DOESN'T NEED RESTORATION, SINCE IT WILL
C        BE OVERWRITTEN BELOW.
      ENDIF
c
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C     DIFFUSE LIGHT ELEMENTS.
      IF(LDIFLI)THEN
C        ITERATE OVER THE DIFFUSION ROUTINES FOR EACH LIGHT ELEMENT.
         J = 3
         DO II = 1,NLIGHT
            THEA = ASLIGHT(II)
            THEZ = ZSLIGHT(II)
C           SET ESPEC(3,*) TO THE LIGHT ELEMENT ABUNDANCE.
            DO I=1,NPT-1
               ESPEC(3,I) = ELI(II,I)
               ESPEC_H(3,I) = ELI_H(II,I)
            ENDDO
            ESPEC(3,I) = ELI(II,NPT)
C           PASS THE EVEN GRID INTO MICRODIFF_RUN TO PERFORM THE DIFFUSION.
C             CALL MICRODIFF_RUN(NLIGHT,CSLIGHT,DR,DT,STOT,NPT,EM,ER,EDEN,ETEM,  ! KC 2025-05-31
            CALL MICRODIFF_RUN(DR,DT,STOT,NPT,EM,ER,EDEN,ETEM,
     *                  EHQPR,EDELR,ESPEC,EDXDR,EM_H,ER_H,EDEN_H,ETEM_H,
     *                  EHQPR_H,EDELR_H,ESPEC_H,EDXDR_H,THEA,THEZ,J)
C           STORE THE NEW RUN OF LIGHT METALS
            DO I=1,NPT
              EDELLI(II,I) = ESPEC(3,I)
            ENDDO
         ENDDO
      ENDIF
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC

C TRANSFORM BACK TO ORIGINAL MODEL GRID; UPDATE HELIUM ARRAY USING
C X+Y+Z=1.  PRINT DIAGNOSTIC OUTPUT.
c
      CALL MICRODIFF_ETM(DT,ER,EDELX,EDELZ,EDELLI,IBEGIN,IEND,NPT,
     *     HCOMP,HQPR,HRU,HS1,HTU,M,STOT,NLIGHT,CSLIGHT)
      RETURN
      END
