C$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C MIX
C$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C THE ENERGY GENERATION ROUTINE (ENGEB) IS USED FOR CALCULATIONS OF
C    EPSILON AND ITS DERIVATIVES IN THE HENYEY PORTION OF THE CODE (COEFFT)
C PRIOR TO CALLING MIX THE FIRST TIME, STORE ORIGINAL VARIABLES IN VECTOR
C    HCOMPP.  REZONE HCOMPP ALONG WITH HCOMP IN HPOINT.
C THE MIX IS CALLED PRIOR TO REZONING AND ON THE THIRD LEVEL OF
C    ITERATION IN THE HENYEY PORTION.
C
C OUTLINE OF MIX :
C
C 1) CALL CONVEC TO DETERMINE LOCATION OF CZ EDGES AND EDGES OF MIXED
C    REGIONS WITH OVERSHOOT.
C 2) DETERMINE RUN OF RATES FOR GIVEN RUN OF COMP, RHO, T.
C    IF DESIRED, THE SR SPITS OUT SOLAR NEUTRINO FLUXES TO I/O UNIT ISNU.
C 3) USE KEMCOM TO IMPLICITLY SOLVE FOR ABUNDANCES IN RADIATIVE REGIONS.
C 4) SOLVE FOR BURNING IN EACH CONVECTION ZONE.
C 5) COMPUTE SEMI-CONVECTION IF DESIRED.
C 6) COMPUTE MICROSCOPIC DIFFUSION IF DESIRED.
C
C THE RATE SUBROUTINE CALLED BY MIX IS A STRIPPED-DOWN VERSION OF ENGEB
C (CALLED NRATE) WHICH HAS NO DERIVATIVES OR ENERGY YIELDS.  IT DOES
C CONTAIN NEUTRINO FLUXES FOR SOLAR NEUTRINO CALCULATIONS.

      SUBROUTINE MIX(DELTS,HCOMP,HD,HL,HP,HR,HS,HS1,HS2,HSTOT,
C      *     HT,ITLVL,LC,M,QDT,QDP,DDAGE,JCORE,JENV,  ! KC 2025-05-31
     *     HT,ITLVL,LC,M,DDAGE,JCORE,JENV,MXZONE,MXZON0,TEFFL)

      PARAMETER(JSON=5000)
      IMPLICIT REAL*8(A-H,O-Z)
      IMPLICIT LOGICAL*4(L)

      COMMON/LUOUT/ILAST,IDEBUG,ITRACK,ISHORT,IMILNE,IMODPT,ISTOR,IOWR
      COMMON/BURN/HCOMPM(15,JSON)
      COMMON/COMP/XENV,ZENV,ZENVM,AMUENV,FXENV(12),XNEW,ZNEW,STOTAL,
     *     SENV
      COMMON/CONST1/ CLN,CLNI,C4PI,C4PIL,C4PI3L,CC13,CC23,CPI
C***MHP COMMON BLOCK ADDED FOR G.S.
      COMMON/CONST2/CGAS,CA3,CA3L,CSIG,CSIGL,CGL,CMKH,CMKHN
      COMMON/CONST3/CDELRL,CMIXL,CMIXL2,CMIXL3,CLNDP,CSECYR
      COMMON/CTLIM/ATIME(14),TCUT(5),TSCUT,TENV0,TENV1,TENV,TGCUT
C MHP 05/02 DEUTERIUM BURNING RATE ADDED
      COMMON/DEUTER/DRATE(JSON),DRATE0(JSON),FMASSACC,JCZ
      COMMON/DPMIX/DPENV,ALPHAC,ALPHAE,ALPHAM,BETAC,IOV1,IOV2,
     *      IOVIM, LOVSTC, LOVSTE, LOVSTM, LSEMIC, LADOV, LOVMAX
      COMMON/FLAG/LEXCOM
C*** MHP 5/90 ADD COMMON BLOCK FOR GRAVITATIONAL SETTLING.
      COMMON/GRAVST/GRTOL,ILAMBDA,NITER_GS,LDIFY
C*** MHP 6/90 ADDITIONAL COMMON BLOCK FOR SETTLING.
      COMMON/GRAVS2/DT_GS,XMIN,YMIN,LTHOULFIT
c GES 6/15 INCLUDED COMMON BLOCK FOR NEW DIFFUSION ROUTINES.
      COMMON/GRAVS4/LNEWDIF,LDIFLI

      COMMON/OLDMOD/HPO(JSON),HTO(JSON),HRO(JSON),HLO(JSON),
     *     HDO(JSON),HCOMPP(15,JSON),HSS(JSON),LCO(JSON),LCZO(JSON),TEFFLO,MO
C*** MHP 6/91 COMMON BLOCK FOR NEW MIXING AND SEMI-CONVECTION.
C THESE ARE PARMIN PARAMETERS IN MARK6.
      COMMON/NEWENG/NITER4,LNEWS,LSNU
      COMMON/THEAGE/DAGE
C ADDED FOR MASS ACCRETION
      COMMON/MASSCHG/DMDT0,FCZDMDT,FTOTDMDT,COMPACC(15),CREIM,
     *               LREIMER,LMDOT
      DIMENSION HD(JSON),HP(JSON),HS(JSON),HS1(JSON),HL(JSON),
     * HS2(JSON),HT(JSON),LC(JSON),HCOMP(15,JSON),HQPR(JSON), HR(JSON)
      DIMENSION HR1(JSON),HR2(JSON),HR3(JSON),HR4(JSON),
     * HR5(JSON),HR6(JSON),HR7(JSON),HR8(JSON),HR9(JSON),HR10(JSON),
     * HR11(JSON),HR12(JSON),HR13(JSON),HF1(JSON),HF2(JSON),XSUM(15)
      DIMENSION MRZONE(13,2),MXZONE(12,2),MXZON0(12,2)
C       DATA BURMIN/1.0D-30/
C MHP 1/95
      DIMENSION LCC(JSON)
      SAVE

C NSPEC IS THE NUMBER OF SPECIES BEING TRACKED.
      IF(LEXCOM)THEN
         NSPEC = 15
      ELSE
         NSPEC = 11
      ENDIF
C  DDAGE IS THE TIMESTEP IN YEARS.
      DDAGE=DELTS/CSECYR
C
C DETERMINE THE LOCATION OF CONVECTION ZONES WITH AND WITHOUT OVERSHOOT.
C
C ******* INTRODUCE DPENV PARAMETER FOR DEEP MIXING *******************
C  DPENV MIXES THE STAR FROM THE SURFACE TO MASS FRACTION DPENV
C  E.G. DPENV = 0.7 MEANS THE OUTER .3 OF THE STAR IS MIXED
      IF(DPENV.LT.1.0D0 .AND. ITLVL.GT.1) THEN
C MIX FROM CENTER TO A FIXED MASS FRACTION
         HSMAX = DPENV*EXP(CLN*HSTOT)
         DO I = 1,M
            IF(HS1(I).GT.HSMAX)THEN
               DO J = I,M
                  LCC(J) = LC(J)
               END DO
               GO TO 5
            ELSE
               LCC(I) = .TRUE.
            ENDIF
         END DO
C MHP 1/95 CHANGE - DPENV MIXES TO A FIXED FRACTION OF THE MAXIMUM
C LUMINOSITY.
C         HLMAX = HL(1)
C         IMAX = 1
C         DO I = 2,M
C            IF(HL(I).GT.HLMAX)THEN
C               HLMAX = HL(I)
C               IMAX = I
C            ENDIF
C         END DO
C         HLTEST = HLMAX*DPENV
C         DO I = 1,M
C            IF(HL(I).LE.HLTEST)THEN
C               LCC(I) = LC(I)
C            ELSE
C               DO J = I,M
C                  LCC(J) = .TRUE.
C               END DO
C               GOTO 5
C            ENDIF
C         END DO
 5       CONTINUE
      ELSE
         DO J = 1,M
            LCC(J) = LC(J)
         END DO
      ENDIF

      CALL CONVEC(HCOMP,HD,HP,HR,HS,HT,LCC,M,MRZONE,MXZONE,MXZON0,
     *             JCORE,JENV,NRZONE,NZONE,NZONE0)

C FIND BURNING RATES (HR1- HR13,HF1,HF2).
      IF(LMDOT .AND. DMDT0.GT.0.0D0)THEN
         XH2TEST = MAX(HCOMP(12,M),COMPACC(12))
      ELSE
         XH2TEST = HCOMP(12,M)
      ENDIF
      DO 10 I = 1,M
C EXIT LOOP ONCE T DROPS BELOW NUCLEAR REACTION T CUTOFF
         IF(HT(I).LE.TCUT(1)) GOTO 20
C SCALAR VARIABLES ARE USED IN THE CALLS TO THE ENERGY GENERATION ROUTINES.
C SET SCALARS EQUAL TO THE GLOBAL ARRAYS FOR THE VARIABLES OF INTEREST.
C DL-LOG(DENSITY),TL-LOG TEMPERATURE,X***-MASS FRACTION OF SPECIES ***,
C WITH HYDROGEN,HELIUM,AND METALS DENOTED AS USUAL BY X,Y,Z.
         DL = HD(I)
         TL = HT(I)
         X = HCOMP(1,I)
         Y = HCOMP(2,I)
         Z = HCOMP(3,I)
         XHE3 = HCOMP(4,I)
         XC12 = HCOMP(5,I)
         XC13 = HCOMP(6,I)
         XN14 = HCOMP(7,I)
         XN15 = HCOMP(8,I)
         XO16 = HCOMP(9,I)
         XO17 = HCOMP(10,I)
         XO18 = HCOMP(11,I)
         XH2 = HCOMP(12,I)
         XLI6 = HCOMP(13,I)
         XLI7 = HCOMP(14,I)
         XBE9 = HCOMP(15,I)
C SETUP NUCLEAR ENERGY TERMS
C          CALL RATES(DL,TL,X,Y,Z,XHE3,XC12,XC13,XN14,XN15,
C      *        XO16,XO17,XO18,XH2,XLI6,XLI7,XBE9,I,HR1,HR2,HR3,HR4,  ! KC 2025-05-31
         CALL RATES(DL,TL,X,Y,XHE3,XC12,XC13,XN14,
     *        XO16,XO18,I,HR1,HR2,HR3,HR4,
     *        HR5,HR6,HR7,HR8,HR9,HR10,HR11,HR12,HR13,HF1,HF2)
C MHP 5/02 COMPUTE RATE FOR DEUTERIUM BURNING
         IF(XH2TEST.GT.1.0D-11)THEN
C             CALL DEUTRATE(DL,TL,X,XH2,I,ITLVL)  ! KC 2025-05-31
            CALL DEUTRATE(DL,TL,X,I,ITLVL)
         ELSE
            DRATE(I) = 0.0D0
         ENDIF
   10 CONTINUE
      I = M + 1
   20 CONTINUE
      DO 21 II = I,M
         HR1(II) = 0.0D0
         HR2(II) = 0.0D0
         HR3(II) = 0.0D0
         HR4(II) = 0.0D0
         HR5(II) = 0.0D0
         HR6(II) = 0.0D0
         HR7(II) = 0.0D0
         HR8(II) = 0.0D0
         HR9(II) = 0.0D0
         HR10(II) = 0.0D0
         HR11(II) = 0.0D0
         HR12(II) = 0.0D0
         HR13(II) = 0.0D0
         HF1(II) = 0.0D0
         HF2(II) = 0.0D0
C MHP 5/02 ZERO OUT DEUTERIUM BURNING RATE
         DRATE(II) = 0.0D0
   21 CONTINUE
C    22 FORMAT(5X,1P5E12.4/5X,E12.4)
C      WRITE(*,912)ITLVL,(DRATE(III),III=1,5),
C     *            (HT(JJJ),JJJ=1,5)
C 912  FORMAT(I2,1P5E14.6/2X,5E14.6)
C
C  NOW IMPLICITLY SOLVE FOR THE NEW ABUNDANCES AT THE END OF THE
C  TIMESTEP.  THIS IS DONE SHELL BY SHELL FOR RADIATIVE REGIONS,
C  AND FOR EACH CONVECTION ZONE AS A UNIT.
C
C RADIATIVE ZONES.
C
      DO 40 K = 1,NRZONE
         DO 30 J = MRZONE(K,1),MRZONE(K,2)
C EXIT LOOP ONCE T DROPS BELOW NUCLEAR REACTION T CUTOFF
            IF(HT(J).LE.TCUT(1)) GOTO 45
            IBEGIN = J
            IEND = J
            CALL KEMCOM(HT,IBEGIN,IEND,HR1,HR2,HR3,HR4,HR5,HR6,
C      *           HR7,HR8,HR9,HR10,HR11,HR12,HR13,HF1,HS2,HCOMP,
C      *           DDAGE,ITLVL)  ! KC 2025-05-31
     *           HR7,HR8,HR10,HR11,HR12,HF1,HS2,HCOMP,DDAGE)
   30    CONTINUE
   40 CONTINUE
   45 CONTINUE
C
C CONVECTION ZONES.
C NOTE KEMCOM ALSO AUTOMATICALLY HOMOGENIZE CONVECTION ZONES.
C
      DO 50 K = 1,NZONE
         IBEGIN = MXZONE(K,1)
         IEND = MXZONE(K,2)
         CALL KEMCOM(HT,IBEGIN,IEND,HR1,HR2,HR3,HR4,HR5,HR6,
C      *        HR7,HR8,HR9,HR10,HR11,HR12,HR13,HF1,HS2,HCOMP,
C      *        DDAGE,ITLVL)  ! KC 2025-05-31
     *        HR7,HR8,HR10,HR11,HR12,HF1,HS2,HCOMP,DDAGE)
   50 CONTINUE
C    51 CONTINUE
      DO I = 1,M
         HCOMPM(1,I) = HR1(I)
         HCOMPM(2,I) = HR2(I)
         HCOMPM(3,I) = HR3(I)
         HCOMPM(4,I) = HR4(I)
         HCOMPM(5,I) = HR5(I)
         HCOMPM(6,I) = HR6(I)
         HCOMPM(7,I) = HR7(I)
         HCOMPM(8,I) = HR8(I)
         HCOMPM(9,I) = HR9(I)
         HCOMPM(10,I) = HR10(I)
         HCOMPM(11,I) = HR11(I)
         HCOMPM(12,I) = HR12(I)
         HCOMPM(13,I) = HR13(I)
         HCOMPM(14,I) = HF1(I)
         HCOMPM(15,I) = HF2(I)
      END DO
C
C IF ITLVL=1 THEN THE RATES OF HYDROGEN AND HELIUM BURNING ARE
C   COMPUTED EXPLICITLY ASSUMING EQUILIBRIUM HE3 AND CN.
C   IF THIS IS THE CASE, CALL OLD ENERGY GENERATION ROUTINE AND
C   OVERWRITE THE HYDROGEN ABUNDANCES (H BURNING) AND Z,C12,O16
C   ABUNDANCES (HE BURNING).
      IF(ITLVL.EQ.1)THEN
C
C  EXPLICITLY SOLVE FOR THE NEW HYDROGEN AND HELIUM BURNING RATES
C  AT THE BEGINNING OF THE TIMESTEP, ASSUMING EQUILIBRIUM HE3 AND CN
C  CYCLE ABUNDANCES.  THIS IS DONE SHELL BY SHELL FOR RADIATIVE REGIONS,
C  AND FOR EACH CONVECTION ZONE AS A UNIT.
C
C RADIATIVE ZONES.
C
         DT = DDAGE*1.0D-9
         DO K = 1,NRZONE
            DO J = MRZONE(K,1),MRZONE(K,2)
C EXIT LOOP ONCE T DROPS BELOW NUCLEAR REACTION T CUTOFF
               IF(HT(J).LE.TCUT(1)) GOTO 60
               IBEGIN = J
               IEND = J
C                CALL EQBURN(HF1,HR1,HR2,HR3,HR4,HR5,HR6,HR7,
C      *              HR8,HR9,HR10,HR11,HR12,HS2,HT,IBEGIN,IEND,  ! KC 2025-05-31
               CALL EQBURN(HR1,HR2,HR3,HR4,HR5,HR6,HR7,
     *              HR10,HR12,HS2,HT,IBEGIN,IEND,
     *              DCDT,DODT,DXDT,DYDT,XC12,XO16,X,Z)
C  USE THE EXPLICIT HYDROGEN BURNING RATE.
               IF(DXDT.NE.0.0D0)THEN
                  HCOMP(1,J) = X + DXDT*DT
               ENDIF
C  USE THE HELIUM BURNING RATE FROM EQBURN AND THE CARBON,ALPHA
C  BURNING RATE
               IF(DYDT.NE.0.0D0)THEN
                  HCOMP(3,J) = Z - DYDT*DT
                  HCOMP(5,J) = XC12 + DCDT*DT
                  HCOMP(9,J) = XO16 + DODT*DT
               ENDIF
            END DO
         END DO
 60      CONTINUE
C
C CONVECTION ZONES.
C NOTE KEMCOM ALSO AUTOMATICALLY HOMOGENIZE CONVECTION ZONES.
C
         DO K = 1,NZONE
            IBEGIN = MXZONE(K,1)
            IEND = MXZONE(K,2)
C             CALL EQBURN(HF1,HR1,HR2,HR3,HR4,HR5,HR6,HR7,HR8,
C      *           HR9,HR10,HR11,HR12,HS2,HT,IBEGIN,IEND,  ! KC 2025-05-31
            CALL EQBURN(HR1,HR2,HR3,HR4,HR5,HR6,HR7,
     *           HR10,HR12,HS2,HT,IBEGIN,IEND,
     *           DCDT,DODT,DXDT,DYDT,XC12,XO16,X,Z)
C  USE THE EXPLICIT HYDROGEN BURNING RATE.
            IF(DXDT.NE.0.0D0)THEN
               X = X + DXDT*DT
               DO I = IBEGIN,IEND
                  HCOMP(1,I) = X
               END DO
            ENDIF
C  USE THE HELIUM BURNING RATE FROM EQBURN AND THE CARBON,ALPHA
C  BURNING RATE
            IF(DYDT.NE.0.0D0)THEN
               Z = Z - DYDT*DT
               XC12 = XC12 + DCDT*DT
               XO16 = XO16 + DODT*DT
               DO I = IBEGIN,IEND
                  HCOMP(3,I) = Z
                  HCOMP(5,I) = XC12
                  HCOMP(9,I) = XO16
               END DO
            ENDIF
         END DO
      ENDIF
C
C DETERMINE EXTENT OF SEMI-CONVECTION IF APPLICABLE.
C
      IF(LSEMIC)THEN
         IF(ITLVL.GT.1)
     *        CALL SCONVEC(DELTS,HCOMP,HD,HL,HP,HR,HS,HT,M,MXZONE,NZONE,
     *                     TEFFL)
      ENDIF
      IF (LSEMIC .OR. (ITLVL .EQ. 1)) THEN
C
C    MIX CONVECTIVE REGIONS IN ORDER.
C    THIS NEEDS TO BE DONE IF SEMI-CONVECTION IS BEING CHECKED, OR
C    IF EXPLICIT H AND HE BURNING IS BEING DONE.
C
C NZONE IS THE NUMBER OF DISTINCT CONVECTION ZONES.
C MXZONE(J,1) AND MXZONE(J,2) ARE THE FIRST AND LAST SHELLS CONTAINED
C IN THE JTH CONVECTION ZONE.
         DO 100 J = 1,NZONE
C I1 AND I2 ARE THE FIRST AND LAST CONVECTIVE SHELLS IN THE GIVEN REGION.
            I1 = MXZONE(J,1)
            I2 = MXZONE(J,2)
            IF(I1.NE.1 .AND. I1.GE.I2) GOTO 100
C INITIALIZE SUMS.
            WTSUM = 0.0D0
            DO 55 I = 1,NSPEC
               XSUM(I) = 0.0D0
 55         CONTINUE
C  ADD UP THE TOTAL MASS OF EACH SPECIES IN THE CONVECTIVE REGION.
C  (HS2 IS THE MASS CONTAINED WITHIN A SHELL IN GRAMS).
            DO 65 JJ = I1,I2
               WTSUM = WTSUM + HS2(JJ)
               DO 61 I = 1,NSPEC
                  XSUM(I) = XSUM(I) + HCOMP(I,JJ)*HS2(JJ)
 61            CONTINUE
 65         CONTINUE
C  DIVIDE BY THE TOTAL MASS TO FIND THE MEAN MASS FRACTION IN THE REGION.
            DO 70 I = 1,NSPEC
               XSUM(I) = XSUM(I)/WTSUM
 70         CONTINUE
C  APPLY THE MEAN MASS FRACTION OF ALL SPECIES THROUGHOUT THE CZ.
            DO 90 JJ = I1,I2
               DO 80 I = 1,NSPEC
                  HCOMP(I,JJ) = XSUM(I)
 80            CONTINUE
 90         CONTINUE
 100     CONTINUE
      ENDIF
C  WRITE OUT THE LOCATIONS OF MIXED REGIONS.
      IF(NZONE.GE.1) THEN
         WRITE(ISHORT,110) ((MXZONE(I,J),J=1,2),I=1,NZONE)
 110     FORMAT(' ZONES MIXED IN ORDER--',12('(',I5,',',I5,') ') )
      ENDIF
C
C MICROSCOPIC DIFFUSION OF HELIUM.
C
C***MHP 12/89 MIX MODIFIED TO INCLUDE CALL TO GRAVITATIONAL SETTLING
C      ROUTINE USING THE BAHCALL AND LOEB METHOD.
C FIRST DEFINE VARIABLES NEEDED FOR SETTLING -
C HQPR=VECTOR OF D LN P/DR.
C STOT=TOTAL STELLAR MASS(UNLOGGED).
      IF(LDIFY)THEN
         IF(HCOMP(1,1).LT.XMIN)THEN
            LDIFY=.FALSE.
            GOTO 170
         ENDIF
         STOT=EXP(CLN*HSTOT)
         DO 130 I = 1,M
            HQPR(I)=-EXP(CLN*(HD(I)+CGL+HS(I)-2.0D0*HR(I)-HP(I)))
 130     CONTINUE
C MHP 6/90 CHANGE ADDED : THE TIMESTEP FOR SETTLING IS RESTRICTED TO
C   A FRACTION OF THE TIMESCALE FOR SETTLING AT THE OUTER BOUNDARY.
C   THE OUTER BOUNDARY IS EITHER THE SURFACE CONVECTION ZONE OR THE
C   FIRST POINT WHERE THE HELIUM ABUNDANCE RISES ABOVE A USER-SPECIFIED
C   MINIMUM VALUE (YMIN).
C
C   LOCATE OUTER BOUNDARY.
         IF(.NOT.LC(M))THEN
            WRITE(ISHORT,911)
C DBG 2/92 CHANGED STOP TO JUST A WARNING MESSAGE, EXECUTION CONTINUES
 911        FORMAT(1X,'NO SURFACE CZ - DIFFUSION NOT MEANINGFUL')
            GOTO 170
         ENDIF
         DO 140 J = JENV,1,-1
            IF(HCOMP(2,J).GT.YMIN)GOTO 150
 140     CONTINUE
C   Y<YMIN FOR THE WHOLE STAR IF THE CODE GETS HERE.
         GOTO 170
 150     CONTINUE
C  FM IS THE MASS FRACTION ABOVE THE OUTER POINT.
         FM = (STOT-HS1(J))/STOT
C  TSCALE IS THE TIMESCALE FOR SETTLING OF HELIUM AT THE OUTER
C  BOUNDARY (MICHAUD ET AL 1984, APJ V.282,P.206)
         TSCALE = 4.348D21*CSECYR*FM/EXP(CLN*1.5D0*HT(J))
C  RESTRICT TIMESTEP TO THE MINIMUM OF THE MODEL TIMESTEP AND
C  A USER SPECIFIED FRACTION (DT_GS) OF THE SETTLING TIMESCALE.
         DTMAX = DT_GS*TSCALE
         NSTEP = INT(DELTS/DTMAX)
         IF(MOD(DTMAX,DELTS).NE.0.0D0)NSTEP=NSTEP+1
         DT = DELTS/DFLOAT(NSTEP)
         DO 160 NS = 1,NSTEP
C PERFORM GRAVITATIONAL SETTLING. IF LNEWDIF = TRUE, USE THE NEW ROUTINES
C IN MICRODIFF. ELSE, USE THE OLD ROUTINES IN GRSETT.
            IF(LNEWDIF)THEN
               CALL MICRODIFF(DT,HCOMP,HQPR,HR,HD,HS1,HT,LCC,M,STOT)
            ELSE
               CALL GRSETT(DT,HCOMP,HQPR,HR,HD,HS1,HT,LCC,M,STOT)
            ENDIF
 160     CONTINUE
 170     CONTINUE
      ENDIF
C RENORMALIZE COMPOSITION TO GUARD AGAINST ANOMALIES (I.E. SMALL NEGATIVE
C ABUNDANCES...).
      DO 180 I = 1,M
         DO 175 J = 1,NSPEC
            HCOMP(J,I) = MAX(HCOMP(J,I),0.0D0)
            HCOMP(J,I) = MIN(HCOMP(J,I),1.0D0)
 175     CONTINUE
         HCOMP(3,I) = MIN(HCOMP(3,I),1.0D0-HCOMP(1,I)-HCOMP(4,I))
         HCOMP(2,I) = 1.0D0 - HCOMP(1,I) - HCOMP(3,I) - HCOMP(4,I)
 180  CONTINUE
C MHP 1/95 ADDED CALL TO RESET JENV,JCORE FOR DEEP MIXING.
      IF(DPENV.LT.1.0D0 .AND. ITLVL.GT.1)THEN
         CALL CONVEC(HCOMP,HD,HP,HR,HS,HT,LC,M,MRZONE,MXZONE,MXZON0,
     *        JCORE,JENV,NRZONE,NZONE,NZONE0)
      ENDIF
C MHP 5/02 ADDED DEUTERIUM BURNING
      IF(LEXCOM)THEN
C RADIATIVE ZONES.
C
         DT = DDAGE*1.0D-9
         DO K = 1,NRZONE
            DO J = MRZONE(K,1),MRZONE(K,2)
C EXIT LOOP ONCE T DROPS BELOW NUCLEAR REACTION T CUTOFF
               IF(HT(J).LE.TCUT(1)) GOTO 190
               IBEGIN = J
               IEND = J
               CALL DBURN(IBEGIN,IEND,M,HS2,HCOMP,DT)
           END DO
        END DO
 190    CONTINUE
C
C CONVECTION ZONES.
C NOTE KEMCOM ALSO AUTOMATICALLY HOMOGENIZE CONVECTION ZONES.
C
         DO K = 1,NZONE
            IBEGIN = MXZONE(K,1)
            IEND = MXZONE(K,2)
            CALL DBURN(IBEGIN,IEND,M,HS2,HCOMP,DT)
         END DO
      ENDIF
      RETURN
      END
