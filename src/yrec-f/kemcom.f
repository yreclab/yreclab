C$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C KEMCOM
C$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
      SUBROUTINE KEMCOM(HT,IBEGIN,IEND,HR1,HR2,HR3,HR4,HR5,HR6,HR7,HR8,
C      *HR9,HR10,HR11,HR12,HR13,HF1,HS2,HCOMP,DDAGE,ITLVL)  ! KC 2025-05-31
     *HR10,HR11,HR12,HF1,HS2,HCOMP,DDAGE)

      PARAMETER(JSON=5000)
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT LOGICAL*4(L)
      COMMON/BURTOL/CMIN,ABSTOL,RELTOL,KEMMAX
      COMMON/CTLIM/ATIME(14),TCUT(5),TSCUT,TENV0,TENV1,TENV,TGCUT
      COMMON/LUOUT/ILAST,IDEBUG,ITRACK,ISHORT,IMILNE,IMODPT,ISTOR,IOWR
      COMMON/OLDMOD/HPO(JSON),HTO(JSON),HRO(JSON),HLO(JSON),HDO(JSON),
     *     HCOMPP(15,JSON),HSS(JSON),LCO(JSON),LCZO(JSON),TEFFLO,MO
      DIMENSION A(56),B(7),W(7),XSUM(11)
      DIMENSION HT(JSON),HR1(JSON),HR2(JSON),HR3(JSON),HR4(JSON),
C      *HR5(JSON),HR6(JSON),HR7(JSON),HR8(JSON),HR9(JSON),HR10(JSON),
C      *HR11(JSON),HR12(JSON),HR13(JSON),HF1(JSON),HCOMP(15,JSON),  ! KC 2025-05-31
     *HR5(JSON),HR6(JSON),HR7(JSON),HR8(JSON),HR10(JSON),
     *HR11(JSON),HR12(JSON),HF1(JSON),HCOMP(15,JSON),
     * HS2(JSON)
      EQUIVALENCE (A(50),B(1))
      SAVE

C KEMCOM USES THE REACTION RATES COMPUTED IN ENGEB TO IMPLICITLY SOLVE
C FOR THE ABUNDANCES OF H,HE3,HE4,C12,C13,N14,O16, AND O18 SIMULTANEUOSLY.
C DON VANDENBERGS NOTES EXPLAIN WHAT THE SR IS DOING.
C
C INPUT VARIABLES :
C
C DDAGE - MODEL TIME STEP IN YEARS.
C HCOMP - ARRAY CONTAINING RUN OF MASS FRACTIONS OF DIFFERENT SPECIES.
C  HCOMP(1,..)=H; 2=HE4; 3=Z; 4=HE3; 5=C12; 6=C13; 7=N14; 9=O16; 11=O18.
C  ELEMENTS 8(N15) AND 10(O17) NOT CURRENTLY USED;12-15 ARE LIGHT ELEMENTS
C  WHOSE BURNING IS TREATED ELSEWHERE.
C HCOMPP - THE ARRAY OF ABUNDANCES AT THE START OF THE TIMESTEP.
C HS2 - RUN OF MASS CONTAINED IN EACH SHELL.
C HT - RUN OF MODEL TEMPERATURE.
C IBEGIN, IEND - STARTING AND ENDING SHELLS; DIFFERENT FOR A CONVECTION ZONE.
C HR1-HR13,HF1 - REACTION RATES (EXCLUDING TERMS THAT DEPEND ONLY ON
C  THE COMPOSITION) PER GIGAYEAR PER AMU.  NEEDS TO BE MULTIPLIED BY THE
C  MASS FRACTIONS OF THE REACTANTS AND THE ATOMIC WEIGHT (IN AMU) OF THE
C  PRODUCT TO GET THE RATE OF CHANGE OF THE MASS FRACTION.
C
C NUMERICAL PARAMETERS IN COMMON BLOCK /BURTOL/:
C ABUNDANCES BELOW CMIN ARE ZEROED OUT.
C ABSTOL AND RELTOL ARE THE ABSOLUTE AND RELATIVE TOLERANCES FOR CONVERGENCE.
C KEMMAX IS THE MAXIMUM NUMBER OF ITERATIONS BEFORE THE PROGRAM WILL HALT.
C
C OUTPUT VARIABLES : THE NEW RUN OF ABUNDANCE HCOMP.
C
      IF(IBEGIN.NE.IEND)THEN
C HOMOGENIZE CONVECTION ZONES.
C XSUM IS THE MASS-WEIGHTED AVERAGE ABUNDANCE FOR THE CZ.
C WTSUM IS THE TOTAL MASS OF THE CZ.
C INITIALIZE SUMS.
         WTSUM = 0.0D0
         DO 1 J = 1,11
            XSUM(J) = 0.0D0
    1    CONTINUE
         DO 5 I = IBEGIN,IEND
            WTSUM = WTSUM + HS2(I)
            DO 3 J = 1,11
               XSUM(J) = XSUM(J)+HCOMPP(J,I)*HS2(I)
    3       CONTINUE
    5    CONTINUE
         DO 7 J = 1,11
            XSUM(J) = XSUM(J)/WTSUM
    7    CONTINUE
      ELSE
         DO 9 J = 1,11
            XSUM(J) = HCOMPP(J,IBEGIN)
    9    CONTINUE
      ENDIF
C SKIP BURNING CALCULATIONS IF STARTING SHELL BELOW T CUTOFF FOR REACTIONS.
      IF(HT(IBEGIN).LT.TCUT(1))THEN
         DO 13 I = IBEGIN,IEND
            DO 11 J = 1,11
               HCOMP(J,I) = XSUM(J)
   11       CONTINUE
   13    CONTINUE
         GOTO 200
      ENDIF
C
C SET UP NUMERICAL PARAMETERS.
C
C TIMESTEP IN GIGAYEARS. THE OTHER DELTS ARE THIS TIMESTEP MULTIPLIED
C  BY THE ATOMIC MASS OF THE DIFFERENT SPECIES (IN AMU).
      DELT=DDAGE*1.0D-9
      DELT3 = 3.0D0*DELT
      DELT4 = 4.0D0*DELT
      DELT12 = 12.0D0*DELT
      DELT13 = 13.0D0*DELT
      DELT14 = 14.0D0*DELT
      DELT16 = 16.0D0*DELT
      DCMIN=CMIN
C COUNTER FOR THE NUMBER OF ITERATIONS.
      ICON=0
C
C NUCLEAR REACTION RATES.
C
C THESE REACTIONS ARE NO LONGER INCLUDED AND ARE ZEROED OUT.
      GR9 = 0.0D0
      GR13 = 0.0D0
      IF(IBEGIN.EQ.IEND)THEN
C PP
         GR1 = HR1(IBEGIN)
C HE3,HE3
         GR2 = HR2(IBEGIN)
C HE3,HE4
         GR3 = HR3(IBEGIN)
C C12,P
         GR4 = HR4(IBEGIN)
C C13,P
         GR5 = HR5(IBEGIN)
C N14,P + N15,P
         GR6 = HR6(IBEGIN)
C O16,P + O17,P.
         GR7 = HR7(IBEGIN)
C C13,ALPHA
         GR8 = HR8(IBEGIN)
C O16,ALPHA (NOT USED)
C        GR9 = HR9(IBEGIN)
C C12,ALPHA
         GR10 = HR10(IBEGIN)
C N14,ALPHA
         GR11 = HR11(IBEGIN)
C TRIPLE ALPHA
         GR12 = HR12(IBEGIN)
C C12,C12 (NOT USED)
C        GR13 = HR13(IBEGIN)
C BRANCHING RATIO FOR N15,P :
C F3 = FRACTION GOING TO C12+ALPHA
C F4 = FRACTION GOING TO O16
         F3 = HF1(IBEGIN)
         F4 = 1.0D0 - F3
      ELSE
C USE THE MASS-WEIGHTED AVERAGE RATES FOR THE CZ
         GR1 = 0.0D0
         GR2 = 0.0D0
         GR3 = 0.0D0
         GR4 = 0.0D0
         GR5 = 0.0D0
         GR6 = 0.0D0
         GR7 = 0.0D0
         GR8 = 0.0D0
         GR10 = 0.0D0
         GR11 = 0.0D0
         GR12 = 0.0D0
         F3 = 0.0D0
         DO 15 I = IBEGIN,IEND
            GR1 = GR1 + HS2(I)*HR1(I)
            GR2 = GR2 + HS2(I)*HR2(I)
            GR3 = GR3 + HS2(I)*HR3(I)
            GR4 = GR4 + HS2(I)*HR4(I)
            GR5 = GR5 + HS2(I)*HR5(I)
            GR6 = GR6 + HS2(I)*HR6(I)
            GR7 = GR7 + HS2(I)*HR7(I)
            GR8 = GR8 + HS2(I)*HR8(I)
            GR10 = GR10 + HS2(I)*HR10(I)
            GR11 = GR11 + HS2(I)*HR11(I)
            GR12 = GR12 + HS2(I)*HR12(I)
            F3 = F3 + HS2(I)*HF1(I)
   15    CONTINUE
         GR1 = GR1/WTSUM
         GR2 = GR2/WTSUM
         GR3 = GR3/WTSUM
         GR4 = GR4/WTSUM
         GR5 = GR5/WTSUM
         GR6 = GR6/WTSUM
         GR7 = GR7/WTSUM
         GR8 = GR8/WTSUM
         GR10 = GR10/WTSUM
         GR11 = GR11/WTSUM
         GR12 = GR12/WTSUM
         F3 = F3/WTSUM
         F4 = 1.0D0 - F3
      ENDIF
C
C INITIAL ABUNDANCES OF SPECIES.
C
      XPR = XSUM(1)
      Y3R = XSUM(4)
      YPR = XSUM(2)
      X2R = XSUM(5)
      X3R = XSUM(6)
      X4R = XSUM(7)
      X6R = XSUM(9)
C STARTING GUESS FOR FINAL ABUNDANCES OF SPECIES :
C FOR CALLS IN CRRECT (ITLVL>1) USE THE PREVIOUS RESULT.
C OTHERWISE USE THE FINAL ABUNDANCES FOUND FOR THE PREVIOUS SHELL
C UNLESS I = 1.
      IF(IBEGIN.NE.1) THEN
         W(1) = HCOMP(1,IBEGIN-1)
         W(2) = HCOMP(4,IBEGIN-1)
         W(3) = HCOMP(2,IBEGIN-1)
         W(4) = HCOMP(5,IBEGIN-1)
         W(5) = HCOMP(6,IBEGIN-1)
         W(6) = HCOMP(7,IBEGIN-1)
         W(7) = HCOMP(9,IBEGIN-1)
      ELSE
         W(1) = HCOMP(1,IBEGIN)
         W(2) = HCOMP(4,IBEGIN)
         W(3) = HCOMP(2,IBEGIN)
         W(4) = HCOMP(5,IBEGIN)
         W(5) = HCOMP(6,IBEGIN)
         W(6) = HCOMP(7,IBEGIN)
         W(7) = HCOMP(9,IBEGIN)
      ENDIF
C
C ITERATION FOR NEW ABUNDANCES.
C
   10 CONTINUE
C FUNCTIONS TO BE MINIMIZED. THESE ARE OF THE FORM
C A(#)=ABUNDANCE(END STEP)-ABUNDANCE (START STEP)-TIMESTEP*(D(SPECIES)/DT)
C BECAUSE THE BURNING RATES D(SPECIES)/DT USE THE ABUNDANCES AT THE
C END OF THE TIMESTEP W(1)-W(7), THIS IS AN IMPLICIT SCHEME.
C EQUATION FOR H (X)
      A(50) = W(1)-XPR-DELT*(-3.D0*GR1*W(1)**2+2.D0*GR2*W(2)**2-GR3
     *  *W(2)*W(3)-GR4*W(1)*W(4)-GR5*W(1)*W(5)-2.D0*W(1)*(GR6*W(6)+
     *  GR7*W(7)))
C HE3
      A(51)= W(2)-Y3R-DELT3*(GR1*W(1)**2-2.D0*GR2*W(2)**2-GR3*W(2)
     * *W(3))
C HE4 (Y)
      A(52) = W(3)-YPR-DELT4*(GR2*W(2)**2+GR3*W(2)*W(3)+F3*GR6*W(6)
     *  *W(1)+GR7*W(1)*W(7)-GR10*W(3)*W(4)-GR8*W(3)*W(5)-GR11*W(3)
     *   *W(6)-3.D0*GR12*W(3)**3)
C C12
      A(53) = W(4)-X2R-DELT12*(GR12*W(3)**3-GR4*W(1)*W(4)-GR10*W(3)
     *  *W(4)+F3*GR6*W(1)*W(6))
C C13
      A(54)=W(5)-X3R-DELT13*(GR4*W(1)*W(4)-GR5*W(1)*W(5)-GR8*W(3)*W(5))
C N14
      A(55) = W(6)-X4R-DELT14*(GR5*W(1)*W(5)+GR7*W(1)*W(7)-GR6*W(1)*
     * W(6) - GR11*W(3)*W(6))
C O16*W(4)
      A(56) = W(7)-X6R-DELT16*(F4*GR6*W(1)*W(6)+GR10*W(3)*W(4)-GR7*W(1)
     *   *W(7)+GR8*W(3)*W(5))
C NOTE THAT THE O18 ABUNDANCE IS COMPUTED EXPLICITLY AT THE END OF THE
C SUBROUTINE.
C THE REMAINING VALUES OF A CORRESPOND TO THE DERIVATIVES OF THE ABOVE 7
C FUNCTIONS WITH RESPECT TO THE 7 SPECIES.
C D(RATES)/DX
      A(1) = 1.D0+DELT*(6.D0*GR1*W(1)+GR4*W(4)+GR5*W(5)+2.D0*(GR6*W(6)+
     *   GR7*W(7)))
      A(2) = -2.D0*DELT3*GR1*W(1)
      A(3) = -DELT4*(F3*GR6*W(6)+GR7*W(7))
      A(4) = -DELT12*(F3*GR6*W(6)-GR4*W(4))
      A(5) = -DELT13*(GR4*W(4)-GR5*W(5))
      A(6) = -DELT14*(GR5*W(5)+GR7*W(7)-GR6*W(6))
      A(7) = -DELT16*(F4*GR6*W(6)-GR7*W(7))
C D(RATES)/DHE3
      A(8) = -DELT*(4.D0*GR2*W(2)-GR3*W(3))
      A(9) = 1.D0+DELT3*(4.D0*GR2*W(2)+GR3*W(3))
      A(10) = -DELT4*(2.D0*GR2*W(2)+GR3*W(3))
      A(11) = 0.D0
      A(12) = 0.D0
      A(13) = 0.D0
      A(14) = 0.D0
C D(RATES)/DHE4
      A(15) = DELT*GR3*W(2)
      A(16) = DELT3*GR3*W(2)
      A(17) = 1.D0-DELT4*(GR3*W(2)-GR10*W(4)-GR8*W(5)-GR11*W(6)
     *  -9.D0*GR12*W(3)**2)
      A(18) = -DELT12*(3.D0*GR12*W(3)**2-GR10*W(4))
      A(19) = DELT13*GR8*W(5)
      A(20) = DELT14*GR11*W(6)
      A(21) = -DELT16*(GR10*W(4)+GR8*W(5))
C D(RATES)/DC12
      A(22) = DELT*GR4*W(1)
      A(23) = 0.D0
      A(24) = DELT4*GR10*W(3)
      A(25) = 1.D0+DELT12*(GR4*W(1)+GR10*W(3))
      A(26) = -DELT13*GR4*W(1)
      A(27) = 0.D0
      A(28) = -DELT16*GR10*W(3)
C D(RATES)/DC13
      A(29) = DELT*GR5*W(1)
      A(30) = 0.D0
      A(31) = DELT4*GR8*W(3)
      A(32) = 0.D0
      A(33) = 1.D0+DELT13*(GR5*W(1)+GR8*W(3))
      A(34) = -DELT14*GR5*W(1)
      A(35) = -DELT16*GR8*W(3)
C D(RATES)/DN14
      A(36) = 2.D0*DELT*GR6*W(1)
      A(37) = 0.D0
      A(38) = -DELT4*(F3*GR6*W(1)-GR11*W(3))
      A(39) = -DELT12*F3*GR6*W(1)
      A(40) = 0.D0
      A(41) = 1.D0+DELT14*(GR6*W(1)+GR11*W(3))
      A(42) = -F4*DELT16*GR6*W(1)
C D(RATES)/DO16
      A(43) = 2.D0*DELT*GR7*W(1)
      A(44) = 0.D0
      A(45) = -DELT4*GR7*W(1)
      A(46) = 0.D0
      A(47) = 0.D0
      A(48) = -DELT14*GR7*W(1)
      A(49) = 1.D0+DELT16*GR7*W(1)
C REDUCE THE 7X8 SYSTEM TO FIND THE CORRECTIONS TO THE RELATIVE
C ABUNDANCES.
      J1J = 0
      J2J = 1
      DO 20 I = 1,49
         IF(I.EQ.J2J) THEN
            J2J = J2J+8
         ELSE IF(DABS(A(I)).LT.1.D-8) THEN
            A(I) = 0.D0
         ELSE
            J1J = 1
         ENDIF
   20 CONTINUE
      IF(J1J.NE.0) CALL SIMEQC(A,8,7)
C CHECK TO SEE IF THE SYSTEM HAS CONVERGED WITHIN THE DESIRED TOLERANCES.
      BIGB = 0.D0
      BIGD = 0.D0
      DO 30 I = 1,7
         W(I) = W(I)-B(I)
         IF(DABS(B(I)).GE.BIGD) BIGD = DABS(B(I))
         IF(W(I).LT.DCMIN) THEN
            W(I) = 0.D0
         ELSE
            XB = DABS(B(I)/W(I))
            IF(XB.GE.BIGB) BIGB = XB
         ENDIF
   30 CONTINUE
      IF(BIGD.GE.ABSTOL.OR.BIGB.GE.RELTOL) THEN
C SYSTEM NOT CONVERGED - SEE IF MAXIMUM NUMBER OF ITERATIONS EXCEEDED.
         ICON = ICON+1
         IF(ICON.GE.KEMMAX) THEN
C MHP 10/02 IU not defined
C            WRITE (ISHORT,1000) IU
            WRITE (ISHORT,1000) IBEGIN
 1000       FORMAT(1X,39('>'),40('<')/1X,'ERROR IN SUBROUTINE KEMCOM'/
     *      1X,'UNABLE TO SOLVE FOR NEW ABUNDANCES IN SHELL',I4/1X,
     *      'RUN STOPPED AFTER 50 ATTEMPTS')
            STOP
         ELSE
            GOTO 10
         ENDIF
      ENDIF
C  SYSTEM HAS CONVERGED.
C  UPDATE COMPOSITION MATRIX.
C  UPDATE O18.
      XO18 = XSUM(11)+18.0D0*DELT*GR11*W(3)*W(6)
C  CHANGE METAL ABUNDANCE IF X<5.0D-7.
      IF(HCOMPP(1,IEND).LT.5.0D-7)THEN
         Z = 1.0D0-W(1)-W(2)-W(3)
      ELSE
         Z = XSUM(3)
      ENDIF
      DO 40 IU = IBEGIN,IEND
         HCOMP(1,IU) = W(1)
         HCOMP(4,IU) = W(2)
         HCOMP(2,IU) = W(3)
         HCOMP(5,IU) = W(4)
         HCOMP(6,IU) = W(5)
         HCOMP(7,IU) = W(6)
         HCOMP(9,IU) = W(7)
         HCOMP(3,IU) = Z
         HCOMP(11,IU) = XO18
   40 CONTINUE
  200 RETURN
      END

