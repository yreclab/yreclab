


C       SUBROUTINE EQBURN(HF1,HR1,HR2,HR3,HR4,HR5,HR6,HR7,HR8,HR9,
C      *                  HR10,HR11,HR12,HS2,HT,IBEGIN,IEND,  ! KC 2025-05-31
      SUBROUTINE EQBURN(HR1,HR2,HR3,HR4,HR5,HR6,HR7,
     *                  HR10,HR12,HS2,HT,IBEGIN,IEND,
     *                  DCDT,DODT,DXDT,DYDT,XC12,XO16,X,Z)

      PARAMETER (JSON=5000)
      IMPLICIT REAL*8(A-H,O-Z)
      IMPLICIT LOGICAL*4(L)
      COMMON/CTLIM/ATIME(14),TCUT(5),TSCUT,TENV0,TENV1,TENV,TGCUT
      COMMON/OLDMOD/HPO(JSON),HTO(JSON),HRO(JSON),HLO(JSON),HDO(JSON),
     *              HCOMPP(15,JSON),HSS(JSON),LCO(JSON),LCZO(JSON),TEFFLO,MO
      DIMENSION XSUM(11)
      DIMENSION HT(JSON),HR1(JSON),HR2(JSON),HR3(JSON),HR4(JSON),
C      *HR5(JSON),HR6(JSON),HR7(JSON),HR8(JSON),HR9(JSON),HR10(JSON),
C      *HR11(JSON),HR12(JSON),HF1(JSON),HS2(JSON)  ! KC 2025-05-31
     *HR5(JSON),HR6(JSON),HR7(JSON),HR10(JSON),HR12(JSON),HS2(JSON)
      SAVE

C SKIP BURNING CALCULATIONS IF STARTING SHELL BELOW T CUTOFF FOR REACTIONS.
      DXDT = 0.0D0
      DYDT = 0.0D0
      DCDT = 0.0D0
      DODT = 0.0D0
      IF(HT(IBEGIN).LT.TCUT(1)) RETURN
C COMPUTE EXPLICIT HYDROGEN AND HELIUM BURNING RATES ASSUMING EQUILIBRIUM
C HE3 AND CN CYCLE ABUNDANCES.
C THIS IS USED FOR AN INITIAL GUESS AT THE BURNING RATES AT THE START
C OF THE TIME STEP, AND CAN BE SUPPLEMENTED BY A FULLY IMPLICIT
C NON-EQUILIBRIUM CALCULATION IN THE 3RD AND 4TH LEVELS OF ITERATION.
      IF(IBEGIN.NE.IEND)THEN
C HOMOGENIZE CONVECTION ZONES.
C XSUM IS THE MASS-WEIGHTED AVERAGE ABUNDANCE FOR THE CZ.
C WTSUM IS THE TOTAL MASS OF THE CZ.
C INITIALIZE SUMS.
         WTSUM = 0.0D0
         DO 1 J = 1,11
            XSUM(J) = 0.0D0
    1    CONTINUE
         DO 5 I = IBEGIN,IEND
            WTSUM = WTSUM + HS2(I)
            DO 3 J = 1,11
               XSUM(J) = XSUM(J)+HCOMPP(J,I)*HS2(I)
    3       CONTINUE
    5    CONTINUE
         DO 7 J = 1,11
            XSUM(J) = XSUM(J)/WTSUM
    7    CONTINUE
      ELSE
         DO 9 J = 1,11
            XSUM(J) = HCOMPP(J,IBEGIN)
    9    CONTINUE
      ENDIF
      IF(IBEGIN.EQ.IEND)THEN
C PP
         GR1 = HR1(IBEGIN)
C HE3,HE3
         GR2 = HR2(IBEGIN)
C HE3,HE4
         GR3 = HR3(IBEGIN)
C C12,P
         GR4 = HR4(IBEGIN)
C C13,P
         GR5 = HR5(IBEGIN)
C N14,P + N15,P
         GR6 = HR6(IBEGIN)
C O16,P + O17,P.
         GR7 = HR7(IBEGIN)
C C13,ALPHA
C        GR8 = HR8(IBEGIN)
C O16,ALPHA (NOT USED)
C        GR9 = HR9(IBEGIN)
C C12,ALPHA
         GR10 = HR10(IBEGIN)
C N14,ALPHA
C        GR11 = HR11(IBEGIN)
C TRIPLE ALPHA
         GR12 = HR12(IBEGIN)
C C12,C12 (NOT USED)
C        GR13 = HR13(IBEGIN)
C BRANCHING RATIO FOR N15,P :
C F3 = FRACTION GOING TO C12+ALPHA
C F4 = FRACTION GOING TO O16
C        F3 = HF1(IBEGIN)
C        F4 = 1.0D0 - F3
      ELSE
C USE THE MASS-WEIGHTED AVERAGE RATES FOR THE CZ
         GR1 = 0.0D0
         GR2 = 0.0D0
         GR3 = 0.0D0
         GR4 = 0.0D0
         GR5 = 0.0D0
         GR6 = 0.0D0
         GR7 = 0.0D0
C        GR8 = 0.0D0
         GR10 = 0.0D0
C        GR11 = 0.0D0
         GR12 = 0.0D0
C        F3 = 0.0D0
         DO 15 I = IBEGIN,IEND
            GR1 = GR1 + HS2(I)*HR1(I)
            GR2 = GR2 + HS2(I)*HR2(I)
            GR3 = GR3 + HS2(I)*HR3(I)
            GR4 = GR4 + HS2(I)*HR4(I)
            GR5 = GR5 + HS2(I)*HR5(I)
            GR6 = GR6 + HS2(I)*HR6(I)
            GR7 = GR7 + HS2(I)*HR7(I)
C           GR8 = GR8 + HS2(I)*HR8(I)
            GR10 = GR10 + HS2(I)*HR10(I)
C           GR11 = GR11 + HS2(I)*HR11(I)
            GR12 = GR12 + HS2(I)*HR12(I)
C           F3 = F3 + HS2(I)*HF1(I)
   15    CONTINUE
         GR1 = GR1/WTSUM
         GR2 = GR2/WTSUM
         GR3 = GR3/WTSUM
         GR4 = GR4/WTSUM
         GR5 = GR5/WTSUM
         GR6 = GR6/WTSUM
         GR7 = GR7/WTSUM
C        GR8 = GR8/WTSUM
         GR10 = GR10/WTSUM
C        GR11 = GR11/WTSUM
         GR12 = GR12/WTSUM
C        F3 = F3/WTSUM
C        F4 = 1.0D0 - F3
      ENDIF
C
C INITIAL ABUNDANCES OF SPECIES.
C
      X = XSUM(1)
      XHE3 = XSUM(4)
      Y = XSUM(2)
      Z = XSUM(3)
      XC12 = XSUM(5)
      XC13 = XSUM(6)
      XN14 = XSUM(7)
      XO16 = XSUM(9)
      IF(X.GT.1.0D-10 .AND. HT(IBEGIN).GT.TCUT(2))THEN
C FIND EQUILIBRIUM HELIUM-3 ABUNDANCE, USING THE QUADRATIC FORMULA.
C THE EQUATION IS -2*R(3,3)*XHE3**2 - R(3,4)*XHE3*XHE4 + R(1,1)X**2 = 0.
         R11 = GR1*X**2
         R34 = GR3*Y
         XHE3E = 0.25D0*(SQRT(R34**2+8.0D0*GR2*R11)-R34)/GR2
C USE THE MINIMUM OF THE CURRENT AND EQUILIBRIUM HE3 ABUNDANCE -
C NEEDED TO AVOID SPURIOUSLY HIGH BURNING RATES IN OUTER LAYERS,
C WHERE THE TIMESCALE FOR HELIUM-3 TO REACH EQUILIBRIUM IS LONG.
         XHE3 = MIN(XHE3,XHE3E)
C PP BURNING.
         DXDT = -3.0D0*R11+2.0D0*GR2*XHE3**2-R34*XHE3
      ELSE
         DXDT = 0.0D0
         GOTO 100
      ENDIF
      IF(HT(IBEGIN).GT.TCUT(3))THEN
C FIND EQUILIBRIUM C12,C13,N14 ABUNDANCES TREATING CN PROCESSING AS
C A CLOSED LOOP.
         SUMCNO = XC12/1.2D1+XC13/1.3D1+XN14/1.4D1
C FIND EXPLICIT RATES FOR P+C12,P+C13,P+N14(==R121,R131,R141)
         R1214 = GR6/GR4
         R1314 = GR6/GR5
C IN EQUILIBRIUM WE HAVE
C XC12 = XN14{R(1,14)/R(1,12)}
C XC13 = XN14{R(1,14)/R(1,13)}
C AND FROM CONSERVATION OF SPECIES XC12/12 +XC13/13 + XN14/14 IS UNCHANGED.
C EXPRESSING C12 AND C13 AS FUNCTIONS OF N14, SOLVE FOR THE NEW N14
C EQUILIBRIUM ABUNDANCE.
         XN14 = SUMCNO/(R1214/1.2D1 + R1314/1.3D1 + 1.0D0/1.4D1)
         XC12 = XN14*R1214
         XC13 = XN14*R1314
         DXDT = DXDT - GR4*X*XC12 - GR5*X*XC13 - 2.0D0*GR6*X*XN14
     *          - 2.0D0*GR7*X*XO16
      ENDIF
  100 CONTINUE
C HELIUM BURNING REACTIONS.
      IF(Y.GT.1.0D-10 .AND. HT(IBEGIN).GT.TCUT(4))THEN
         R3A = GR12*Y**3
         R412 = GR10*Y*XC12
         DYDT = -4.0D0*(R412 + 3.0D0*R3A)
         DCDT = 1.2D1*(R3A-R412)
         DODT = 1.6D1*R412
      ELSE
         DYDT = 0.0D0
         DCDT = 0.0D0
         DODT = 0.0D0
      ENDIF

      RETURN
      END
