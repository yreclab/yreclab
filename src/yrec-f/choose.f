

C$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C CHOOSE
C$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
      SUBROUTINE CHOOSE(P1,P2,M1,M2,Q1,Q2,EPS,NCASE)
C
C                                 SHAPE PRESERVING QUADRATIC SPLINES
C                                   BY D.F.MCALLISTER & J.A. ROULIER
C                                     CODED BY S.L.DODD & M.ROULIER
C
      REAL*8 P1,P2,M1,M2,Q1,Q2,MREF,MREF1,MREF2,SPQ,PROD,
     *     PROD1,PROD2,EPS
C
C CHOOSE DETERMINES THE CASE NEEDED FOR THE COMPUTATION OF THE PARAME-
C TERS OF THE QUADRATIC SPLINE AND RETURNS THE VALUE IN THE VARIABLE
C NCASE.
C
C ON INPUT--
C
C   (P1,P2) GIVES THE COORDINATES OF ONE OF THE POINTS OF INTERPOLATION.
C
C   M1 SPECIFIES THE DERIVATIVE CONDITION AT (P1,P2).
C
C   (Q1,Q2) GIVES THE COORDINATES OF ONE OF THE POINTS OF INTERPOLATION.
C
C   M2 SPECIFIES THE DERIVATIVE CONDITION AT (Q1,Q2).
C
C   EPS IS AN ERROR TOLERANCE USED TO DISTINGUISH CASES WHEN M1 OR M2 IS
C   RELATIVELY CLOSE TO THE SLOPE OR TWICE THE SLOPE OF THE LINE
C   SEGMENT JOINING (P1,P2) AND (Q1,Q2). IF EPS IS NOT EQUAL TO ZERO,
C   THEN EPS SHOULD BE GREATER THAN OR EQUAL TO MACHINE EPSILON.
C
C
C ON OUTPUT--
C
C   NCASE CONTAINS THE VALUE WHICH CONTROLS HOW THE PARAMETERS OF THE
C   QUADRATIC SPLINE ARE EVALUATED.
C
C AND
C
C   CHOOSE DOES NOT ALTER P1,P2,Q1,Q2,M1,M2,EPS.
C
C----------------------------------------------------------------------
C
C CALCULATE THE SLOPE SPQ OF THE LINE JOINING (P1,P2),(Q1,Q2).
      SPQ=(Q2-P2)/(Q1-P1)
C
C CHECK WHETHER OR NOT SPQ IS 0.
C ******MODIFICATION BY MARC PINSONNEAULT TO AVOID DIVISION BY ZERO
C ******IN SR CASES
      IF(M1.EQ.0.0D0.OR.M2.EQ.0.0D0) GOTO 9
C ******
      IF (SPQ .NE. 0.D0) GO TO 20
      IF ((M1*M2) .GE. 0.D0) GO TO 10
      NCASE=1
      RETURN
   9  CONTINUE
  10  NCASE=2
      RETURN
C
  20  PROD1=SPQ*M1
      PROD2=SPQ*M2
C
C FIND THE ABSOLUTE VALUES OF THE SLOPES SPQ,M1,AND M2.
      MREF=ABS(SPQ)
      MREF1=ABS(M1)
      MREF2=ABS(M2)
C
C IF THE RELATIVE DEVIATION OF M1 OR M2 FROM SPQ IS LESS THAN EPS, THEN
C CHOOSE CASE 2 OR CASE 3.
      IF ((ABS(SPQ-M1).LE.EPS*MREF) .OR. (ABS(SPQ-M2).LE.EPS*MREF))
     *    GO TO 30
C
C COMPARE THE SIGNS OF THE SLOPES SPQ,M1, AND M2.
      IF ((PROD1 .LT. 0.D0).OR.(PROD2 .LT. 0.D0)) GO TO 80
      PROD=(MREF-MREF1)*(MREF-MREF2)
      IF (PROD .GE. 0.D0) GO TO 40
C
C L1, THE LINE THROUGH (P1,P2) WITH SLOPE M1, AND L2, THE LINE THROUGH
C (Q1,Q2) WITH SLOPE M2, INTERSECT AT A POINT WHOSE ABSCISSA IS BETWEEN
C P1 AND Q1. THE ABSCISSA BECOMES A KNOT OF THE SPLINE.
      NCASE=1
      RETURN
C
  30  IF ((PROD1 .LT. 0.D0).OR.(PROD2 .LT. 0.D0)) GO TO 80
  40  IF (MREF1 .GT. (2.D0*MREF)) GO TO 50
      IF (MREF2 .GT. (2.D0*MREF)) GO TO 60
C
C BOTH L1 AND L2 CROSS THE LINE THROUGH (P1+Q1/2.,P2) AND (P1+Q1/2.,Q2),
C WHICH IS THE MIDLINE OF THE RECTANGLE FORMED BY (P1,P2),(Q1,P2),
C (Q1,Q2), AND (P1,Q2), OR BOTH M1 AND M2 HAVE SIGNS DIFFERENT THAN THE
C SIGN OF SPQ, OR ONE OF M1 AND M2 HAS OPPOSITE SIGN FROM SPQ AND L1
C AND L2 INTERSECT TO THE LEFT OF P1 OR TO THE RIGHT OF Q1. THE POINT
C (P1+Q1)/2. IS A KNOT OF THE SPLINE.
      NCASE=2
      RETURN
C
C CHOOSE CASE 4 IF MREF2 IS GREATER THAN (2.-EPS)*MREF; OTHERWISE,
C CHOOSE CASE 3.
  50  IF (MREF2 .GT. (2.D0-EPS)*MREF) GO TO 70
      NCASE=3
      RETURN
C
C IN CASES 3 AND 4, SIGN(M1)=SIGN(M2)=SIGN(SPQ).
C
C EITHER L1 OR L2 CROSSES THE MIDLINE, BUT NOT BOTH.
C CHOOSE CASE 4 IF MREF1 IS GREATER THAN (2.-EPS)*MREF; OTHERWISE,
C CHOOSE CASE 3.
  60  IF (MREF1 .GT. (2.D0-EPS)*MREF) GO TO 70
      NCASE=3
      RETURN
C
C IF NEITHER L1 NOR L2 CROSSES THE MIDLINE, THE SPLINE REQUIRES TWO
C KNOTS BETWEEN P1 AND Q1.
  70  NCASE=4
      RETURN
C
C THE SIGN OF AT LEAST ONE OF THE SLOPES M1,M2 DOES NOT AGREE WITH THE
C SIGN OF THE SLOPE SPQ.
  80  IF ((PROD1 .LT. 0.D0).AND.(PROD2 .LT. 0.D0)) GO TO 130
C
      IF (PROD1 .LT. 0.D0) GO TO 90
      GO TO 110
C
  90  IF (MREF2 .GT. ((1.D0+EPS)*MREF)) GO TO 100
      NCASE=2
      RETURN
C
  100 NCASE=1
      RETURN
C
  110 IF (MREF1 .GT. ((1.D0+EPS)*MREF)) GO TO 120
      NCASE=2
      RETURN
C
  120 NCASE=1
      RETURN
C
  130 NCASE=2
      RETURN
C
      END
