C $$$$$$
      SUBROUTINE CHECKC(HCOMP,IT,LPRT,M,DT,IREDO,LOK,LREDO)

      PARAMETER (JSON=5000)
      IMPLICIT REAL*8(A-H,O-Z)
      IMPLICIT LOGICAL*4(L)
      COMMON/COMP/XENV,ZENV,ZENVM,AMUENV,FXENV(12),XNEW,ZNEW,STOTAL,
     *     SENV
      COMMON/COMP2/YENV,Y3ENV
      COMMON/DIFUS/DTDIF,DJOK,ITDIF1,ITDIF2
      COMMON/FLAG/LEXCOM
      COMMON/LUOUT/ILAST,IDEBUG,ITRACK,ISHORT,IMILNE,IMODPT,ISTOR,IOWR
C      COMMON/MDPHY/HAMU(JSON),CP(JSON),DELM(JSON),DELAM(JSON),
C     *     DELRM(JSON),SESUM(JSON),OM(JSON),SQDT(JSON),
C     *     THDIF(JSON),SVEL(JSON),VISC(JSON),EPSM(JSON)
C JvS 09/25 UPDATED NAMES IN COMMON BLOCK FOR CONSISTENCY ACROSS ALL SUBROUTINES   
      COMMON/MDPHY/AMUM(JSON),CPM(JSON),DELM(JSON),DELAM(JSON),
     *             DELRM(JSON),ESUMM(JSON),OM(JSON),QDTM(JSON),
     *             THDIFM(JSON),VELM(JSON),VISCM(JSON),EPSM(JSON)     
      COMMON/OLDMOD/HPO(JSON),HTO(JSON),HRO(JSON),HLO(JSON),HDO(JSON),
     *     HCOMPP(15,JSON),HSS(JSON),LCO(JSON),LCZO(JSON),TEFFLO,MO
      DIMENSION HCOMP(15,JSON),ATOMWT(4)
      DATA ATOMWT/1.007825D0,4.002603D0,12.0D0,3.01603D0/
      SAVE

C  SR CHECKC PERFORMS SEVERAL FUNCTIONS.
C  FIRST, IT CHECKS FOR ANOMALOUS COMPOSITIONS.
C  IF THEY ARE ENCOUNTERED, THE TIMESTEP IS CUT.
C  IT ALSO COMPUTES THE CHANGE IN MEAN MOLECULAR WEIGHT DUE TO
C  COMPOSITION DIFFUSION.
C
C  INPUT VARIABLES:
C  HCOMP :  MASS FRACTION OF ALL THE SPECIES BEING TRACKED AS A FUNCTION
C     OF MASS.
C  IT : THE PROGRAM ITERATES FOR THE DIFFUSION COEFFICIENTS; IT IS THE
C     ITERATION NUMBER.
C  ITDIF2 : USER PARAMETER - MAXIMUM NUMBER OF ITERATIONS.
C  IREDO : NUMBER OF TIMES DIFFUSION TIMESTEP HAS BEEN CUT.
C  LOK : FLAG SET T IF DIFFUSION COEFFICEINTS HAVE CONVERGED.
C  LPRT : FLAG SET T IF OUTPUT ABOUT CHECMICAL DIFFUSION DESIRED.
C  M : NUMBER OF MODEL POINTS.
C
C  OUTPUT VARIABLES:
C  DT : DIFFUSION TIMESTEP, WHICH CAN BE CUT IF ERRORS IN THE COMPOSITION
C     DIFFUSION ARE DISCOVERED.
C  AMUM : NEW RUN OF MEAN MOLECULAR WEIGHT.
C  IREDO : NUMBER OF TIMES DIFFUSION TIMESTEP HAS BEEN CUT.
C  LOK : SET F IF ERRORS IN COMPOSITION DIFFUSION DISCOVERED.
C  LREDO : SET T IF ERRORS IN COMPOSITION DIFFUSION DISCOVERED.
C
C  CHECK FOR ANOMALOUS COMPOSITIONS.
C  PRIOR TO THE LAST ITERATION, ONLY DIFFUSION OF H,HE,HE3 PERFORMED.
C  FIND NUMBER OF SPECIES BEING DIFFUSED.
      IF(IT.EQ.ITDIF2)THEN
         JEND = 11
      ELSE
         JEND = 4
      ENDIF
      LREDO = .FALSE.
      DO 20 J = 1,JEND
C  HCOMP(3,...) IS Z, WHICH IS NOT DIFFUSED AS A UNIT.
         IF(J.EQ.3)GOTO 20
         DO 10 I = 2,M-1
            IF(HCOMP(J,I).LT.0.0D0.OR.HCOMP(J,I).GT.1.0D0)THEN
C  SOME SPECIES CAN BE MIXED INTO REGIONS WHERE THEY ARE DESTROYED VERY
C  QUICKLY.  THE INTERPLAY BETWEEN DIFFUSION AND NUCLEAR BURNING CAN LEAD
C  TO SMALL NEGATIVE ABUNDANCES.  THEREFORE, ZERO OUT THESE SMALL NUMBERS
C  RATHER THAN STOPPING THE CODE.
               IF(ABS(HCOMP(J,I)).LT.1.0D-5*HCOMP(J,M))THEN
                  IF(I.EQ.1.OR.I.EQ.M)THEN
                     HCOMP(J,I) = 0.0D0
                  ELSE
                     HCOMP(J,I) = MAX(0.0D0,0.5D0*(HCOMP(J,I+1)+
     *                            HCOMP(J,I-1)))
                  ENDIF
                  GOTO 10
               ENDIF
               IREDO = IREDO + 1
               IF(IREDO.GT.3)THEN
                  WRITE(6,1010) J,I,HCOMP(J,I)
                  WRITE(ISHORT,1010) J,I,HCOMP(J,I)
 1010 FORMAT(1X,39('>'),39('>')/' ERROR IN SR CHECKC'/
     *       ' ANOMALOUS COMP NUMBER',I2,' IN ZONE',I5,' ABUNDANCE ',
     *       1PE12.3/' 3 ATTEMPTS AT TIMESTEP CUTTING FAILED'/
     *       'RUN STOPPED')
                  STOP
               ELSE
                  LREDO = .TRUE.
                  LOK = .FALSE.
                  DT = 0.5D0*DT
                  WRITE(6,1015)IREDO,J,I,HCOMP(J,I)
                  WRITE(ISHORT,1015)IREDO,J,I,HCOMP(J,I)
 1015 FORMAT(' ERROR IN SR CHECKC'/' TIMESTEP CUT NUMBER ',I2,
     *       ' DUE TO ANOMALOUS COMP NUMBER',I2,' IN ZONE',I5,
     *       ' ABUNDANCE ',1PE12.3)
                  GOTO 100
               ENDIF
            ENDIF
   10    CONTINUE
   20 CONTINUE
      IF(IT.EQ.ITDIF2.AND.LPRT) THEN
C  FIND MAXIMUM FRACTIONAL CHANGE IN COMPOSITION AND PRINT IT OUT.
         DELC = 0.0D0
         ICMAX = 0
         JCMAX = 0
         DO 40 J = 1,JEND
            IF(J.EQ.3)GOTO 40
C CMIN IS USED TO GUARD AGAINST DIVISION BY ZERO.
            CMIN = MAX(1.0D-6*HCOMP(J,M),1.0D-20)
            DO 30 I = 1,M
               IF(HCOMPP(J,I).LT.CMIN)GOTO 30
               DCOMP = (HCOMP(J,I)-HCOMPP(J,I))/HCOMPP(J,I)
               IF(ABS(DCOMP).GT.ABS(DELC)) THEN
                  DELC = DCOMP
                  ICMAX = I
                  JCMAX = J
               ENDIF
   30       CONTINUE
   40    CONTINUE
         WRITE(*,50)DELC,JCMAX,ICMAX
   50 FORMAT(' MAX FRAC.COMP.CHANGE',1PE12.3,' SPECIES',I2,
     *       ' AT PT.',I5)
         IF(LEXCOM)WRITE(*,60)HCOMP(14,M),HCOMPP(14,M)
   60 FORMAT(5X,'NEW SURFACE LI',1PE14.4,'OLD VALUE',E14.4)
      ENDIF
C  FIND NEW RUN OF MEAN MOLECULAR WEIGHT ASSUMING FULLY IONIZED GAS.
C  AMUENV IS(1/MEAN MOLECULAR WEIGHT PER ION OF THE SURFACE MIXTURE.)
C  CORRECTION FOR PARTIAL IONIZATION NEEDED IN MASSIVE STARS.
      IF(IT.GT.1)THEN
         DO 90 I = 1,M
            DFX1 = HCOMP(1,I) - XENV
            DFX2 = HCOMP(2,I) - YENV
            DFX3 = HCOMP(3,I) - ZENV
            DFX4 = HCOMP(4,I) - Y3ENV
            TEMP = AMUENV + DFX1/ATOMWT(1) + DFX2/ATOMWT(2) +
     *            DFX3/ATOMWT(3) + DFX4/ATOMWT(4)
            AMU2 = 1.0D0/TEMP
            TEMP = HCOMP(1,I)/ATOMWT(1)+2.0D0*(HCOMP(4,I)/ATOMWT(4)
     *             + HCOMP(2,I)/ATOMWT(2)) + 0.5D0*HCOMP(3,I)
            EMU2 = 1.0D0/TEMP
            AMUM(I) = AMU2*EMU2/(AMU2+EMU2)
   90    CONTINUE
      ENDIF
  100 CONTINUE

      RETURN
      END
