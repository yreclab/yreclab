C$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C SLOPES
C$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
      SUBROUTINE SLOPES(XTAB,YTAB,MTAB,NUM)
C
C                                 SHAPE PRESERVING QUADRATIC SPLINES
C                                   BY D.F.MCALLISTER & J.A.ROULIER
C                                     CODED BY S.L.DODD & M.ROULIER
C                                       N.C.STATE UNIVERSITY
C
      PARAMETER (JSON=5000)
      REAL*8 XTAB(JSON),YTAB(JSON),MTAB(JSON),M1,M2,XBAR,XHAT,
     *     YDIF1,YDIF2,YXMID,XMID,M1S,M2S
C
C SLOPES CALCULATES THE DERIVATIVE AT EACH OF THE DATA POINTS. THE
C SLOPES PROVIDED WILL INSURE THAT AN OSCULATORY QUADRATIC SPLINE WILL
C HAVE ONE ADDITIONAL KNOT BETWEEN TWO ADJACENT POINTS OF INTERPOLATION.
C CONVEXITY AND MONOTONICITY ARE PRESERVED WHEREVER THESE CONDITIONS
C ARE COMPATIBLE WITH THE DATA.
C
C ON INPUT--
C
C   XTAB CONTAINS THE ABSCISSAS OF THE DATA POINTS.
C
C   YTAB CONTAINS THE ORDINATES OF THE DATA POINTS.
C
C   NUM IS THE NUMBER OF DATA POINTS (DIMENSION OF XTAB,YTAB).
C
C
C ON OUTPUT--
C
C   MTAB CONTAINS THE VALUE OF THE FIRST DERIVATIVE AT EACH DATA POINT.
C
C AND
C
C   SLOPES DOES NOT ALTER XTAB,YTAB,NUM.
C
C----------------------------------------------------------------------
C
      save

      NUM1= NUM - 1
      IM1=1
      I=2
      I1=3
C
C CALCULATE THE SLOPES OF THE TWO LINES JOINING THE FIRST THREE DATA
C POINTS
      YDIF1=YTAB(2) - YTAB(1)
      YDIF2=YTAB(3) - YTAB(2)
      M1=YDIF1/(XTAB(2) - XTAB(1))
      M1S=M1
      M2=YDIF2/(XTAB(3)-XTAB(2))
      M2S=M2
C
C IF ONE OF THE PRECEDING SLOPES IS ZERO OR IF THEY HAVE OPPOSITE SIGN,
C ASSIGN THE VALUE ZERO TO THE DERIVATIVE AT THE MIDDLE POINT.
  10  IF(M1.EQ.0.D0 .OR. M2.EQ.0.D0 .OR. (M1*M2).LE.0.D0) GO TO 20
      IF (ABS(M1) .GT. ABS(M2)) GO TO 30
      GO TO 40
  20  MTAB(I)= 0.D0
      GO TO 50
C
C CALCULATE THE SLOPE BY EXTENDING THE LINE WITH SLOPE M1.
  30  XBAR=(YDIF2/M1) + XTAB(I)
      XHAT= (XBAR + XTAB(I1))/2.D0
      MTAB(I)=YDIF2/(XHAT - XTAB(I))
      GO TO 50
C
C CALCULATE THE SLOPE BY EXTENDING THE LINE WITH SLOPE M2.
  40  XBAR=(-YDIF1/M2) + XTAB(I)
      XHAT=(XTAB(IM1) + XBAR)/2.D0
      MTAB(I)=YDIF1/(XTAB(I) - XHAT)
C
C INCREMENT COUNTERS
  50  IM1=I
      I=I1
      I1=I1+1
      IF (I .GT. NUM1) GO TO 60
C
C CALCULATE THE SLOPES OF THE TWO LINES JOINING THREE CONSECUTIVE DATA
C POINTS.
      YDIF1=YTAB(I) - YTAB(IM1)
      YDIF2=YTAB(I1) - YTAB(I)
      M1=YDIF1/(XTAB(I) - XTAB(IM1))
C KC 2025-05-31 PREVENT FLOATING POINT EXCEPTION
C       M2=YDIF2/(XTAB(I1) - XTAB(I))
      CALL SAFEDIVIDE(YDIF2, (XTAB(I1) - XTAB(I)), M2)
      GO TO 10
C
C CALCULATE THE SLOPE AT THE LAST POINT, XTAB(NUM).
  60  IF ((M1*M2) .LT. 0.D0) GO TO 80
      XMID= (XTAB(NUM1)+XTAB(NUM))/2.D0
      YXMID=MTAB(NUM1)*(XMID - XTAB(NUM1)) + YTAB(NUM1)
C KC 2025-05-31 PREVENT FLOATING POINT EXCEPTION
C       MTAB(NUM)=(YTAB(NUM)-YXMID)/(XTAB(NUM)-XMID)
      CALL SAFEDIVIDE((YTAB(NUM)-YXMID), (XTAB(NUM)-XMID), MTAB(NUM))
      IF ((MTAB(NUM)*M2) .LT. 0.D0) GO TO 70
      GO TO 90
  70  MTAB(NUM)=0.D0
      GO TO 90
  80  MTAB(NUM)=2.D0*M2
C
C CALCULATE THE SLOPE AT THE FIRST POINT, XTAB(1).
  90  IF((M1S*M2S) .LT. 0.D0) GO TO 110
      XMID=(XTAB(1) + XTAB(2))/2.D0
      YXMID=MTAB(2)*(XMID - XTAB(2)) + YTAB(2)
      MTAB(1)=(YXMID - YTAB(1))/(XMID - XTAB(1))
      IF ((MTAB(1) * M1S) .LT. 0.D0) GO TO 100
      RETURN
  100 MTAB(1)=0.D0
      RETURN
  110 MTAB(1)=2.D0*M1S
      RETURN
C
      END

C$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C KC 2025-05-31 SAFEDIVIDE
C$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
      SUBROUTINE SAFEDIVIDE(NUMERATOR, DENOMINATOR, RESULT)
      REAL*8 NUMERATOR, DENOMINATOR, RESULT

      RESULT = 0.D0
      IF (NUMERATOR .NE. 0.D0 .AND. DENOMINATOR .NE. 0.D0) THEN
         RESULT = NUMERATOR / DENOMINATOR
      END IF

      RETURN
      END SUBROUTINE SAFEDIVIDE
