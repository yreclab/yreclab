C$$$$$$
      SUBROUTINE WTIME(DELTS,M,OMEGA,DELTSW,DWMAX)

      PARAMETER (JSON=5000)
      IMPLICIT REAL*8(A-H,O-Z)
      IMPLICIT LOGICAL*4(L)
      COMMON/CT2/DTWIND
      COMMON/OLDROT/WOLD(JSON),HJX(JSON),HIO(JSON),HGO(JSON),R0X(JSON),
     *              ETA2X(JSON)
      COMMON/CTLIM/ATIME(14),TCUT(5),TSCUT,TENV0,TENV1,TENV,TGCUT
      DIMENSION OMEGA(JSON)
      SAVE
C  FOR ROTATING MODELS, LIMIT TIMESTEP BASED ON MAX CHANGE IN OMEGA.
C  THE USER PARAMETER DTWIND GOVERNS THE MAXIMUM CHANGE ALLOWED BETWEEN
C  MODELS.
C INPUT VARIABLES :
C DECLARED VARIABLES
C   DELTS : PREVIOUS MODEL TIMESTEP
C   M : NUMBER OF MODEL POINTS
C   OMEGA : RUN OF ANGULAR VELOCITY IN THE CURRENT MODEL.
C VARIABLES IN COMMON BLOCKS :
C   DTWIND : MAX DELTA OMEGA/OMEGA (DEFAULT =0.08).
C   WOLD : RUN OF ANGULAR VELOCITY IN THE PREVIOUS MODEL.
C
C OUTPUT VARIABLES :
C   DELTSW : ROTATION-BASED TIMESTEP.
C   DWMAX : MAXIMUM FRACTIONAL CHANGE IN OMEGA.
C

      II = 1
      DWMAX = 2.0D0*ABS(OMEGA(II)-WOLD(II))/(OMEGA(II)+WOLD(II))
      IWMAX = II
      DO 50 I = II+1,M
         TESTW=2.0D0*ABS(OMEGA(I)-WOLD(I))/(OMEGA(I)+WOLD(I))
         IF(TESTW.GT.DWMAX) THEN
            DWMAX = TESTW
            IWMAX = I
         ENDIF
 50   CONTINUE
      FAC = DWMAX/DTWIND
C  IF NO CHANGE FROM PREVIOUS MODEL,SET DELTSW TO TIMESTEP
C  STORED IN THE PREVIOUS MODEL.
C      IF (FAC.EQ.0.D0) FAC=1.0D0
      IF (FAC.EQ.0.D0)THEN
          DELTSW = 1.0D20
          GOTO 999
      ENDIF
C  RESTRICT CHANGE IN TIMESTEP TO NO MORE THAN A FACTOR OF ATIME(14)%
C UP OR DOWN.
C      TEMP=1.0D0+ATIME(14)
C MHP 10/14 USE ATIME(13) AS THE GLOBAL TIMESTEP LIMITER FOR THE CODE
      TEMP = ATIME(13)
      IF (FAC.GT.TEMP) FAC=TEMP
      IF (FAC.LT.1.0D0/TEMP) FAC=1.0D0/TEMP
      DELTSW = DELTS/FAC
 999  CONTINUE
      RETURN
      END
