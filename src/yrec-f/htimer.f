C
C
C$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C HTIMER
C$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C       SUBROUTINE HTIMER(DELTS,DELTSH,M,HD,HL,HS1,HS2,HT,LC,HCOMP,JCORE,
C      * JXMID,TLUMX,DAGE,DDAGE,QDT,QDP,NK,HP,HR,OMEGA,DWMAX,JXBEG,TEFFL)  ! KC 2025-05-31
      SUBROUTINE HTIMER(DELTS,DELTSH,M,HD,HL,HS1,HS2,HT,HCOMP,JCORE,
     * JXMID,TLUMX,DAGE,DDAGE,NK,HP,HR,OMEGA,DWMAX,JXBEG,TEFFL)

      PARAMETER(JSON=5000)
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT LOGICAL*4(L)
      COMMON/CTLIM/ATIME(14),TCUT(5),TSCUT,TENV0,TENV1,TENV,TGCUT
      COMMON/CONST/CLSUN,CLSUNL,CLNSUN,CMSUN,CMSUNL,CRSUN,CRSUNL,CMBOL
      COMMON/CONST3/CDELRL,CMIXL,CMIXL2,CMIXL3,CLNDP,CSECYR
      COMMON/CT3/LPTIME
      COMMON/FLAG/LEXCOM
      COMMON/GOVS/LTRIST
      COMMON/ROT/WNEW,WALPCZ,ACFPFT,ITFP1,ITFP2,LROT,LINSTB,LWNEW
C MHP 10/24 ADDED STOP CRITERIA FOR CENTRAL H,D,AND HE4
      COMMON/SETT/ENDAGE(50),SETDT(50),LENDAG(50),LSETDT(50),
     * END_DCEN(50),END_XCEN(50),END_YCEN(50)
C      COMMON/SETT/ENDAGE(50),SETDT(50),LENDAG(50),LSETDT(50)
C       DIMENSION HD(JSON),HL(JSON),HS1(JSON),HT(JSON),LC(JSON),  ! KC 2025-05-31
      DIMENSION HD(JSON),HL(JSON),HS1(JSON),HT(JSON),
     *     HCOMP(15,JSON),EG(6),TLUMX(8),HP(JSON),HR(JSON),OMEGA(JSON)
      DIMENSION HR1(JSON),HR2(JSON),HR3(JSON),HR4(JSON),HR5(JSON),
     *     HR6(JSON),HR7(JSON),HR8(JSON),HR9(JSON),HR10(JSON),HS2(JSON),
     *     HR11(JSON),HR12(JSON),HR13(JSON),HF1(JSON),HF2(JSON)
      SAVE

C  HTIMER DETERMINES THE TIMESTEP FOR THE NEXT MODEL.
C  THERE ARE SEVERAL POSSIBLE MEANS OF DETERMINING THE TIMESTEP; IT CHOOSES
C  THE MOST STRINGENT ONE.
C  FIRST OF ALL, THE TIMESTEP MAY NOT BE MORE THAN ATIME(13) TIMES THE
C       PREVIOUS ONE.
C  IN SR PTIME, THE TIMESTEP BASED ON STRUCTURE CHANGES FROM ONE
C       MODEL TO THE NEXT IS COMPUTED IF DESIRED(PDELT).
C  IN SR WTIME, THE TIMESTEP BASED ON CHANGES IN THE ANGULAR VELOCITY
C       FROM ONE MODEL TO THE NEXT IS COMPUTED(ROTATING MODELS ONLY)(DELTSW).
C  IN SR XTIME, THE TIMESTEP BASED ON HYDROGEN BURNING IS COMPUTED.(DELTSH).
C  IN SR YTIME, THE TIMESTEP BASED ON HELIUM BURNING IS COMPUTED(DELTSY).
C  DETAILS OF THE COMPUTATION OF THESE TIMESTEPS CAN BE FOUND IN THE
C       APPROPRIATE SUBROUTINES.
C  THE FINAL TIMESTEP (DELTS) IS THE MINIMUM OF THE ABOVE TIMESTEPS.
C  **NOTE : IF A CRITERION IS INAPPLICABLE OR TURNED OFF BY USER PARAMETERS
C       THEN THE 'TIMESTEP' FOR THAT CRITERION IS ARBITRARILY SET TO A
C       LARGE NUMBER.
C  IF A FIXED TIMESTEP IS BEING ENFORCED (LSETDT=T), THE ABOVE CRITERIA ARE
C  SHORT-CIRCUITED AND A TIMESTEP(SETDT) IS ENFORCED. CAVEAT USER.
C  IF THE MODEL IS BEING EVOLVED TO A FIXED AGE(LENDAG=T), THE TIMESTEP WILL BE
C  REDUCED IF THE ONE USED WOULD PUT THE MODEL PAST THE DESIRED AGE(ENDAGE).
C
C  A NEGATIVE TIMESTEP INDICATES THAT A MODEL IS BEING RELAXED
C  AND NOT EVOLVED.  CHECK FOR NEGATIVE DT AND EXIT IF FOUND.


      IF(DELTS.LT.0.0D0) GOTO 310
C IF USER IS FIXING TSTEP, SET DT TO GIVEN VALUE AND EXIT
      IF(LSETDT(NK)) THEN
       DELTSH = SETDT(NK)*CSECYR
       DELTS = DELTSH
       GOTO 310
      ENDIF
C MHP 9/01  TURN OFF STRUCTURE-BASED TIMESTEP SETTING ABOVE A CRITICAL
C           TEMPERATURE; THIS IS DONE WHEN
      IF(LPTIME) THEN
         IF(HT(1).GT.7.1D0 .AND. TLUMX(7).LT. 0.0D0) THEN
            WRITE(*, 100) HT(1), TLUMX(7)
 100        FORMAT('LPTIME SET FALSE - TC ',F7.4,' EGRAV ',E10.2)
            LPTIME = .FALSE.
         ENDIF
      ENDIF
C  FIND TIMESTEP BASED ON CHANGES IN STRUCTURE VARIABLES FROM
C       ONE MODEL TO THE NEXT.
C  NOTE THAT THIS RETURNS THE TIMESTEP STORED IN THE MODEL ON THE
C       FIRST CALL TO HTIMER FOR EACH KIND CARD.
      IF(LPTIME) THEN
       CALL PTIME(DELTS,HL,HP,HR,HT,M,PDELT)
      ELSE
       PDELT = 1.0D20
      ENDIF
C  MHP 6/90 A ZERO TIMESTEP IN THE MODEL WILL FRITZ OUT THE CODE, SO
C  AVOID THIS BY SETTING THE STRUCTURE TIMESTEP ARBITRARILY LARGE.
      IF(PDELT.LT.1.0D0)PDELT=1.0D20
C  FIND TIMESTEP FOR ROTATING MODELS BASED ON CHANGES IN ANGULAR VELOCITY
C       FROM ONE MODEL TO THE NEXT.
C  THIS SR ALSO RETURNS THE MAXIMUM CHANGE IN OMEGA FROM THE PREVIOUS
C       MODEL TO THE CURRENT ONE, WHICH IS USED IN THE DIFFUSION ROUTINE
C       TO DETERMINE THE NUMBER OF DIFFUSION TIMESTEPS REQUIRED.
C  NOTE THAT THIS RETURNS THE TIMESTEP STORED IN THE MODEL ON THE
C       FIRST CALL TO HTIMER FOR EACH KIND CARD.
      IF(LROT) THEN
       CALL WTIME(DELTS,M,OMEGA,DELTSW,DWMAX)
      ELSE
       DELTSW = 1.0D20
      ENDIF
C  MHP 6/90 A ZERO TIMESTEP IN THE MODEL WILL FRITZ OUT THE CODE, SO
C  AVOID THIS BY SETTING THE ROTATION TIMESTEP ARBITRARILY LARGE.
      IF(DELTSW.LT.1.0D0)DELTSW=1.0D20
C  FIND THE TIMESTEP BASED ON HYDROGEN BURNING.
C  DETERMINE THE TOTAL LUMINOSITY DUE TO HYDROGEN BURNING (IN SOLAR UNITS).
      HYDLUM = TLUMX(1) + TLUMX(2) + TLUMX(3) + TLUMX(4)
C  SKIP H-BURNING TIMESTEP CRITERIA FOR STARS WITHOUT H BURNING.
      IF(HYDLUM.GT.1.0D-34)THEN
       CALL XTIME(HD,HCOMP,HL,HS1,HS2,HT,HYDLUM,JCORE,JXMID,M,DELTSH,
     *   HR1,HR2,HR3,HR4,HR5,HR6,HR7,HR8,HR9,HR10,HR11,HR12,HR13,
C      *   HF1,HF2,JXBEG)  ! KC 2025-05-31
     *   HF1,HF2)
      ELSE
       DELTSH = 1.0D20
      ENDIF
C  FIND THE TIMESTEP BASED ON HELIUM BURNING.
C  SKIP FOR STARS WITHOUT HE BURNING.
      IF(TLUMX(5).GT.1.0D-34)THEN
       CALL YTIME(EG,HCOMP,HD,HL,HS1,HT,JCORE,M,DELTSY,
     *   HR1,HR2,HR3,HR4,HR5,HR6,HR7,HR8,HR9,HR10,HR11,HR12,HR13,
C      *   HF1,HF2,QDP,QDT,JXBEG)  ! KC 2025-05-31
     *   HF1,HF2,JXBEG)
      ELSE
       DELTSY = 1.0D20
      ENDIF

C  04/14 JVS ADDED TIMESTEP GOVERNOR BASED ON THE SIZE OF THE ENVELOPE
C  TRIANGLE. TIMESTEPS ARE RESTRICTED SUCH THAT THE MODEL DOES NOT MOVE
C  MORE THAN TRIDL OR TRIDT
      IF(LTRIST .AND. .NOT. LPTIME) THEN
       CALL ENTIME(DELTS,HL,TEFFL,M,EDELT)
      ELSE
       EDELT = 1.0D20
      ENDIF

C     LIMIT INCREASE IN TIME STEP FROM ONE MODEL TO THE NEXT FOR MODELS
C     WITH A NON-ZERO TIMESTEP.
C      write(*,299)PDELT,DELTSW,DELTSH,DELTSY,DELTS
C 299  format('P ',E10.5,' W ',E10.5,' H ',E10.5,' He ',E10.5,' prev. ',
C     * E10.5)
C         WRITE(*,*)'H time ',DELTSH/CSECYR,'   He Time ',DELTSY/CSECYR

      IF(DELTS.GT.1.0D0)THEN
         DELTS = ATIME(13)*DELTS
C  NOW SET THE TIMESTEP TO BE THE MINIMUM OF THE ENTROPY BASED TIMESTEP,
C  THE NUCLEAR BURNING TIMESTEPS, AND THE PREVIOUS TIMESTEP*ATIME(13).
C  04/14 JVS ADDED EDELT
         DELTS = MIN(DELTS,DELTSH,DELTSY,DELTSW,PDELT,EDELT)
      ELSE
C  SET THE TIMESTEP TO BE THE MINIMUM OF THE THE ABOVE TIMESTEPS,
C  BUT NEGLECT THE PREVIOUS TIMESTEP.
C  04/14 JVS ADDED EDELT
         DELTS = MIN(DELTSH,DELTSY,DELTSW,PDELT,EDELT)
      ENDIF

c ******** Grant added ********
c      IF(TLUMX(5).GT.1.0D-2) THEN
c         ATIME(6)=0.000025
c         ATIME(5)=0.01
c       IF(TLUMX(5).GT.5.0D-1) THEN
c         ATIME(6)=0.0000075
c         ATIME(5)=0.003
c       IF(TLUMX(5).GT.5.0D0) THEN
c         ATIME(6)=0.00000075
c         ATIME(5)=0.0003
c       IF(TLUMX(5).GT.5.0D01) THEN
c         ATIME(6)=0.000000025
c         ATIME(5)=0.00001
c       IF(TLUMX(5).GT.5.0D02) THEN
c         ATIME(6)=0.000000005
c         ATIME(5)=0.000005
c       IF(TLUMX(5).GT.2.0D03) THEN
c         ATIME(6)=0.0000000005
c         ATIME(5)=0.0000005
c         ENDIF
c         ENDIF
c         ENDIF
c         ENDIF
c         ENDIF
c      ENDIF
c *****************************

      DELTSH = DELTS
  310 CONTINUE
      DELTS = ABS(DELTS)
      DDAGE = DELTS/CSECYR
C     mhp 10/24 flag includes other stop conditions,
C  so only do this if there is a true stop age.
C     IF EVOLVING TO A GIVEN AGE, ENSURE AGE NOT EXCEEDED
C      IF(LENDAG(NK)) THEN
      IF(LENDAG(NK) .AND. ENDAGE(NK).GT.0.0D0) THEN
       TLEFT = ENDAGE(NK) - DAGE*1.0D9
       IF(TLEFT.LT. DDAGE) THEN
         DDAGE = TLEFT
         DELTSH = DDAGE*CSECYR
         DELTS = DELTSH
       ENDIF
      ENDIF


      RETURN
      END
