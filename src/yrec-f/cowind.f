C$$$$$$
      SUBROUTINE COWIND(BL,DT,HICZ,IT,OMEGAS,SMASS,
     *                  TEFFL,WOLD,WIND1,WIND2)

      PARAMETER (JSON=5000)
      IMPLICIT REAL*8(A-H,O-Z)
      IMPLICIT LOGICAL*4(L)
      COMMON/CONST/CLSUN,CLSUNL,CLNSUN,CMSUN,CMSUNL,CRSUN,CRSUNL,CMBOL
      COMMON/CONST1/ CLN,CLNI,C4PI,C4PIL,C4PI3L,CC13,CC23,CPI
      COMMON/CONST2/CGAS,CA3,CA3L,CSIG,CSIGL,CGL,CMKH,CMKHN
C MHP 8/17 ADDED EXCEN, C_2 TO COMMON BLOCK FOR MATT ET AL. 2012 CENT. TERM
      COMMON/CWIND/WMAX,EXMD,EXW,EXTAU,EXR,EXM,EXL,EXPR,CONSTFACTOR,
     *             STRUCTFACTOR,EXCEN,C_2,LJDOT0
C      COMMON/CWIND/WMAX,EXMD,EXW,EXTAU,EXR,EXM,CONSTFACTOR,STRUCTFACTOR,LJDOT0
C MHP 3/09 ADDED OPTION TO SCALE THE SATURATION RATE BY THE OVERTURN TIMESCALE
      COMMON/DEUTER/DRATE(JSON),DRATE0(JSON),FMASSACC,JCZ
      COMMON/OVRTRN/LNEWTCZ,LCALCENV,TAUCZ,TAUCZ0,PPHOT,PPHOT0,FRACSTEP
      SAVE
C  COWIND RETURNS THE CHANGE IN THE ANGULAR VELOCITY OF THE SURFACE
C  CONVECTION ZONE OF A STAR WHICH IS EXPERIENCING ANGULAR MOMENTUM LOSS.
C
C  INPUT VARIABLES:
C
C       DECLARED
C  BL : TOTAL LUMINOSITY
C  DT : DIFFUSION TIMESTEP.
C  HICZ : TOTAL MOMENT OF INERTIA OF THE SURFACE C.Z.
C  IT : ITERATION NUMBER (IN THE DIFFUSION ROUTINES).
C  OMEGAS : SURFACE ANGULAR VELOCITY AT THE END OF THE PREVIOUS ITERATION.
C  SMASS : STELLAR MASS (SOLAR UNITS)
C  TEFFL : STELLAR EFFECTIVE TEMPERATURE.
C  WOLD : SURFACE ANGULAR VELOCITY AT THE BEGINNING OF THE TIMESTEP.
C       IN COMMON BLOCKS
C
C  EXMD,EXW,EXR,EXM,CONSTFACTOR : USER PARAMETERS WHICH VARY THE STRENGTH
C  OF ANGULAR MOMENTUM LOSS AND ITS DEPENDENCE ON SURFACE ROTATION RATE
C  AND OTHER STELLAR PARAMETERS.
C
C  OUTPUT VARIABLES
C
C  WIND1,WIND2:CHANGE IN ANGULAR VELOCITY OF THE SURFACE C.Z. FOR THE TIMESTEP
C  BASED ON THE ANGULAR VELOCITY AT THE BEGINNING AND END OF THE TIMESTEP
C  RESPECTIVELY.
C
C  WIND1 AND WIND2 ARE INCORPORATED INTO THE DIFFUSION EQUATIONS;
C  THEY ARE AVERAGED TO FIND THE TOTAL ANGULAR MOMENTUM LOSS FOR THE
C  TIMESTEP.
C FIND TOTAL RADIUS OF STAR.
C mhp 10/02 cgrav not used; omit
C      CGRAV = EXP(CLN*CGL)
C MHP 3/09 IF WMAX > 1 THEN ASSUME THAT THE PARAMETER WMAX IS DEFINED BY
C WMAX = WMAX(SUN)*TAUCZ(SUN) AND THE SATURATION THRESHOLD WSAT = WMAX/TAUCZ(STAR)
      IF(WMAX.GT.1.0D0)THEN
         IF(TAUCZ.GT.1.0D0)THEN
            WSAT = WMAX/TAUCZ
c            WRITE(*,912)WSAT,TAUCZ
c 912        FORMAT('Omega sat, Tau',1p2e12.3)
         ELSE
            WRITE(*,911)WMAX,TAUCZ
 911        FORMAT('ERROR IN WIND - TAUCZ NOT DEFINED ',1P2E12.3,'STOPPED')
            STOP
         ENDIF
      ELSE
         WSAT = WMAX
      ENDIF
      RL=0.5D0*(BL+CLSUNL-C4PIL-CSIGL-4.D0*TEFFL)
      RTOT = EXP(CLN*RL)
C DMDOT IS THE MASS LOSS RATE IN SOLAR MASSES PER YEAR.
      DMDOT = 2.0D-14
C DJ/DT = DT*CONSTFACTOR*(DMDOT/1.0D-14)**EXMD*OMEGA**EXW*(M/MSUN)**EXM
C         *(R/RSUN)**EXR
C  THE CONSTANT AND EXPONENTS ARE SET IN PARMIN BASED ON THE INPUT
C  INDEX ALFA;SEE PARMIN FOR DETAILS ON THE DEPENDENCE OF EACH ON ALFA.
      C = DT/HICZ*CONSTFACTOR*(DMDOT/1.0D-14)**EXMD
     *    *(RTOT/CRSUN)**EXR*SMASS**EXM
      WP = MIN(WOLD,WSAT)
      WN = MIN(OMEGAS,WSAT)
      WIND1 = C*WP**(EXW-1.0D0)*WOLD
      TEMP = C*WN**(EXW-1.0D0)*OMEGAS
      IF(IT.EQ.1) THEN
         WIND2 = TEMP
      ELSE
         WIND2 = 0.5D0*(WIND2+TEMP)
      ENDIF

      RETURN
      END
