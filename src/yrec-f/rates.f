C$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C RATES
C$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C       SUBROUTINE RATES(DL,TL,X,Y,Z,XHE3,XC12,XC13,XN14,XN15,XO16,XO17,
C      *XO18,XH2,XLI6,XLI7,XBE9,IU,HR1,HR2,HR3,HR4,HR5,HR6,HR7,  ! KC 2025-05-31
      SUBROUTINE RATES(DL,TL,X,Y,XHE3,XC12,XC13,XN14,XO16,
     *XO18,IU,HR1,HR2,HR3,HR4,HR5,HR6,HR7,
     *HR8,HR9,HR10,HR11,HR12,HR13,HF1,HF2)

C JULY 3, 1991 (MHP)
C THIS SUBROUTINE COMPUTES THE NUCLEAR BURNING RATES FOR USE IN KEMCOM.
C IT IS A STRIPPED-DOWN VERSION OF ENGEB; SEE ENGEB FOR DETAILED NOTES
C ON THE REACTIONS.
C ALL PHYSICAL QUANTITIES ARE CGS. THE REACTION RATES ARE IN
C  GM^{-1}S^{-1}.
C  MHP 10/97
C
c   On 10/13/97, JNB converted the nuclear masses from neutral nuclear
c   masses to bare nuclear masses by subtracting Z(I)*(m_e)*c^2 from
c   the neutral nuclear masses. This caused changes in a number of
c   places: in ANUC(I), in Q1(I)-Q7(I), in the calculation of the Be7electron
c   Be7proton rates, and in the calculation of the N15p branching ratio.
c
c   JNB made some purely cosmetic changes on 1/20/96. Revised all of the
c   input Q's on 9/23-25/97 to agree with submitted version of Solar
c   Fusion Workshop paper. The SStandard are fixed to agree with
c   the Workshop paper. JNB recalculated all of the EG(I) to determine
c   the best values for the energy generation for all the reactions,
c   taking account of my improved calculations of neutrino energy loss.
c   The calculations are documented in Vol. 19, 132-141, 1997 of my notes.
c   The neutral atom mass differences are taken from Table of Isotopes,
c   8th Ed, 1996 and the neutrino energy losses from Bahcall, Gallium
c   solar neutrino experiments, Phys Rev C, in press, 1997.
c
C ALL NUMBERS IN THIS SUBROUTINE HAVE BEEN CHECKED AND REVISED, WHERE
C  NECESSARY, BY JOHN BAHCALL SO THAT THEY AGREE WITH THE MODERN NUMBERS
C  IN NEUTRINO ASTROPHYSICS (1989).
C *****************************************
C CHANGING NUCLEAR REACTION CROSS SECTIONS.
C *****************************************
C NUCLEAR CROSS SECTIONS CAN BE CHANGED SIMPLY BY INSERTING NEW NUMBERS
C  FOR THE DATA VALUES OF SSTANDARD(I), WHICH ARE THE RATIOS OF THE
C  DESIRED CROSS SECTION FACTORS TO THE VALUES GIVEN IN TABLES 3.2 AND
C  3.4 OF NEUTRINO ASTROPHYSICS (1989). IF THE VALUE GIVEN IN NEUTRINO
C  ASTROPHYSICS IS USED, THEN SSTANDARD(I) = 1.0 .  TO INCREASE THE CROSS
C  SECTION FOR REACTION K BY A FACTOR OF TWO COMPARED TO THE STANDARD
C  VALUE, SET SSTANDARD(K) = 2.0 .  TO DETERMINE WHICH VALUE OF I GOES
C  WITH WHICH REACTION, SEE THE SECTION JUST BELOW.
C THE ENERGY DERIVATIVES ENTER IN A FORM IN WHICH THEY ARE DIVIDED BY
C  THE ABSOLUTE VALUES OF THE CROSS SECTIONS AT ZERO ENERGY.  THUS IF
C  THE SHAPE OF THE CROSS SECTION EXTRAPOLATION IS UNCHANGED AND ONLY
C  THE INTERCEPT OF S(E) AT ZERO ENERGY IS CHANGED, THEN NO CORRECTION
C  NEED BE MADE FOR THE DERIVATIVES.  THEY ARE AUTOMATICALLY SCALED
C  CORRECTLY.  THE EXACT WAY THAT THE DERIVATIVES ENETER THE
C  CALCUALTIONS IS DESCRIBED IN THE SECTION LABELED ``DEFINING THE
C  Q(I)' THAT IS PRESENTED BELOW.
C ************************************
C IDENTIFYING THE REACTIONS.
C ************************************
C  THE VALUE OF J DENOTES WHICH OF THE REACTIONS THE COEFFICIENTS
C  REFER TO:
C  J = 1, PP; J = 2, HE3+HE3; J = 3, HE3+ HE4; J =4, P + C12;  J = 5, P+C13;
C  J = 6. P + N14; J = 7, P + O16 ; J = 8, HE4+C13; J = 10, HE4+C12;
C  J = 11, HE4 + N14; J = 12, TRIPLE ALPHA.
C  REACTION 14 IS PEP; REACTION 15 IS BE7 ELECTRON CAPTURE; REACTION 16 IS
C   BE7 PROTON CAPTURE; REACTION 17 IS THE HEP REACTION.
C  do not change sstandard(14) unless you want to change the ratio of
C  pep to pp.
C   REACTIONS 14-17 WERE NOT EXPLICITLY INCLUDED IN THE YALE
C    PREVIOUS VERSION OF THE CODE, BUT THEY ARE MOST OF THE STORY FOR
C    THE SOLAR NEUTRINO PROBLEM.
C   THE BRANCHING OF THE N15 + P REACTIONS IS TREATED IN A SERIES OF
C    SEPARATE STATEMENTS FOLLOWING THE CALCULATION OF THE BE7 + P
C    REACTION. SEE THE DEFINITIONS OF F3 AND F4.  IF THE CROSS-SECTION
C    FACTORS OF THE N15 + P REACTIONS ARE REVISED, THEN THE NUMERICAL
C    COEFFICIENTS MUST BE CHANGED IN THE DEFINITION OF C12ALPHA AND
C    O16GAMMA.
C FOR Q1(I), ...,Q(5(I), I = 8 CORRESPONDS TO THE BE7 +  P REACTION.
C  THIS ASSIGNMENT FOR I = 8 IS ONLY VALID FOR THE LISTED Q'S AND NOT
C  FOR OTHER ARRAYS IN THE PROGRAM.
C IU IS THE SHELL NUMBER.

      PARAMETER(JSON=5000)
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT LOGICAL*4 (L)
      COMMON/CTLIM/ATIME(14),TCUT(5),TSCUT,TENV0,TENV1,TENV,TGCUT
      COMMON/CONST1/ CLN,CLNI,C4PI,C4PIL,C4PI3L,CC13,CC23,CPI
C MHP 8/96 CROSS SECTIONS PUT IN COMMON BLOCK.
C      COMMON/CROSS/SSTANDARD(17)
C MHP 6/14 DERIVATIVES ADDED
      COMMON/CROSS/SSTANDARD(17),QS0E(8),QQS0EE(8),FO16,FC12,LNEWNUC
      DIMENSION HR1(JSON),HR2(JSON),HR3(JSON),HR4(JSON),HR5(JSON),
     1 HR6(JSON),HR7(JSON),HR8(JSON),HR9(JSON),HR10(JSON),HR11(JSON),
     2 HR12(JSON),HR13(JSON),HF1(JSON),HF2(JSON)
      DIMENSION XFRAC(13),RATE(13),UTOT(13),ZPRD(13),Z53(13),Z43(13),
     1 Z23(13),Z86(13),Q1(8),Q2(8),Q3(8),Q4(8),Q5(8),Q6(7),Q7(7),Q8(7)
      DIMENSION ANUC(13), ZNUC(13)
      COMMON/WEAK/WEAKSCREENING

C ***************************
C ANUC ARE ATOMIC MASS UNITS.
C ***************************
c  On 10/13/97, JNB converted ANUC(I) from neutral atomic masses to
c  bare nuclear masses.
c
c  The scale is the mass of C12 divided by 12 or 931.49432 MeV,
c  which is 1.6605402 times 10^{-24} gm. The values are obtained
c  by dividing the mass excess (expressed in MeV) by 931.49432 MeV and
c  adding to this the atomic mass number, A.  The value for Be7, which
c  is used implicitly in this subroutine was, 7.016930, until 13/13/97.
c  We now use the bare nuclear mass of 7.014735 .
c
      DATA ANUC/1.008665,1.007276,2.013553,3.015501,3.014933,4.001506,
     1     11.996709,13.000064,13.999233,15.990526,17.994772,19.986954,
     2     23.978458/,ZNUC/0.,1.,1.,1.,2.,2.,6.,6.,7.,8.,8.,10.,12./,
     3     NELEM/13/
C THE ISOTOPES ARE NEUTRON, H1, D, H3, HE3, HE4, C12, C13, N14, O16, O18,
C  NE20, MG24, RESPECTIVELY. ALL OF THESE NUMBERS WERE CHECKED.
C NELEM IS THE NUMBER OF ISOTOPES INCLUDED.
C **************************************************************************
C THE QUANTITIES Q1(J), Q2(J), ...,Q5(J) ARE THE TERMS IN EQUATION 3.14 OF
C  NEUTRINO ASTROPHYSICS AND IN EQUATION 53 OF FOWLER, CAUGHLAN, AND
C  ZIMMERMAN (1967) EQ. 53, IN BOTH CASES MULTIPLIED BY T SUB 9 ^(-2/3).
C  THE REACTIONS CORRESPONDING TO EACH J ARE LISTED ABOVE, UNDER:
C   IDENTIFYING THE RECTIONS.
C  FOR THIS SET OF PARAMETERS, AND ONLY FOR THIS SET OF PARAMETERS,
C  J = 8 CORRESPONDS TO THE BE7 + P REACTION.
C **************************************************************************
C GENERAL EXPRESSION FOR Q'S:
C T_9^(-2/3)[S_EFF/S(0)] =
C   [T_9^(-2/3) + Q1(I)T_9^(-1/3) + Q2(I) + Q3(I)T_9^(1/3) +
C    Q4(I)T_9^(2/3) + Q(5)T_9 ]
C
C BY COMPARISON WITH EQUATION 3.14 OF NEUTRINO ASTROPHYSICS, WE SEE THAT
C
C  Q1 = (5/(12*TAU))*T_9^(-1/3) .
C  Q2 = (S'/S)(E_0))*T_9^(-2/3) .
C  Q3 = (S'/S)(35/36)(K*10^9 K)
C  Q4 = (S''/2S)(E_0^2)(T_9^(-4/3)
C  Q5 = (89/72)(S''/S)(E_0)(KT)(T_9^(-5/3)
C
C EACH OF THE Q'S IS INDEPENDENT OF TEMPERATURE (T), AS CAN BE SEEN FROM
C  EQUATIONS 3.10 AND 3.11 .
C
C ALL OF THE VALUES OF THE Q1, ...., Q5 HAVE BEEN RECALCULATED, USING
C  WHERE NEEDED NUCLEAR CROSS SECTIONS GIVEN IN TABLES 3.2 AND 3.4 OF NEUTRINO
C  ASTROPHYSICS.
C ******************************************************************
C Q6 IS THE COEFFICIENT OF THE TEMPERATURE TERM IN THE DEFINITION OF
C  TAU, EQUATION 3.10 OF NEUTRINO ASTROPHYSICS.
C  TAU = Q6*(T SUB 9 TO THE (-1/3) POWER ).
C ******************************************************************
C SLIGHT CHANGES HAVE BEEN MADE IN THE PREVIOUS VALUES OF Q6 TO MAKE
C  THE DATA MORE ACCURATE.
C NOTE Q6 IS NEGATIVE.
C ********************************************************************
C Q7 IS THE CONSTANT IN FRONT OF THE REACTION RATE. THE LOGARITHMS ARE
C  RELATED TO S FACTORS BY A LOGARITHM AND VARIOUS NUMERICAL FACTORS.
C ********************************************************************
C THE GENERAL RELATION IS:
C  Q7 = 70.62860 + ((LN(Z0*Z1/A))/3) -LN(A0*A1) - LN(S SUB 0)
C       -LN(1 + DELTA_01)
C  HERE S SUB 0 IS THE CROSS SECTION FACTOR IN UNITS OF KEV-BARNS.
C  THE NUMERICAL VALUES USED HERE ARE TAKEN FROM TABLES 3.4 AND 3.2
C   OF NUCLEAR ASTROPHYSICS.
C  THE QUANTITY DELTA_01 IS NON-ZERO (EQUAL TO UNITY) ONLY WHEN THE
C   TWO REACTING NUCLEI, 0 AND 1, ARE IDENTICAL.
C Q8 REFLECTS A TERM IN THE EXPONENTIAL THAT OCCURS IN THE RATES, THE
C  TERM BEING PROPORTIONAL TO E^( CONSTANT*T_9^2). SEE HARRIS, ET AL.
C  1983, ANNUAL REV. ASTRON. ASTROPHYS.21, 165 (1983) FOR THE MEANING
C  OF THIS OBSCURE TERM.
C THE VALUES OF Q1(I),...Q7(I) GIVEN BELOW WERE OBTAINED USING THE DATA
C  IN NEUTRINO ASTROPHYSICS WITH THE AID OF AUXILARY COMPUTER CODES
C  THAT GENERATED ACCURATE EVALUATIONS. THE NUMBERS GIVEN HERE ARE
C  IN MANY CASES COMPLETELY DIFFERENT FROM THE VALUES IN THE ORIGINAL
C  YALE CODE.
C NRXNS IS THE NUMBER OF REACTIONS BEING TRACKED.
C DBG 8/94 APPLIED MHP UPDATE TO NUCLEAR REACTIONS
c 10/13/97. Changed Q(I) so that are now calculated for the bare nuclear
c masses. The calculates were made cues.f . Last date on the calculations
c in my notes is 10/10/97.  Note Q6 is -tau(T_9 = 1); in cues.f, I calculate
c tau(T_6 = 1). The connection is:  Q6 = 0.1*tau(cues.f).
c
c
C BP00 values
C      DATA Q1/0.12317,.03392,.0325,.0304,.03035,.0273,.02494,.040572/,
C     1 Q2/1.08749,-.273,-.2085,.7630,-0.4044,-1.60,-1.224, -0.2095/,
C     2 Q3/.93833,-.0648,-.0474,.1626,-.08598,-.3064,-.2139,-0.0595/,
C     3 Q4/0.,0.,0.,4.79,7.456,0.0,.69703,.16762/,
C     4 Q5/0.,0.,0.,2.595,4.032,0.0,0.3097,.12114/,
C     5 Q6/-3.3804,-12.2757,-12.826,-13.6899,-13.7173,-15.2281,
C     5    -16.6925/,
C     6 Q7/20.8964,76.6003,67.8036,69.130,70.3809,69.8517,70.8012/,
C     7 Q8/0.,0.,0.,0.0,0.0,0.0,0.0/,
C     8NRXNS/13/
C Revised values 7/21/03
C      DATA Q1/0.12317,.03392,.0325,.0304,.03035,.0273,.02494,.040572/,
C     1Q2/1.08749,-.273,-.2085,.7630,-0.4044,-1.60,-1.224, 0.0/,
C     2Q3/.93833,-.0648,-.0474,.1626,-.08598,-.3064,-.2139,0.0/,
C     3Q4/0.,0.,0.,4.79,7.456,0.0,.69703,0.0/,
C     4Q5/0.,0.,0.,2.595,4.032,0.0,0.3097,0.0/,
C     5Q6/-3.3804,-12.2757,-12.826,-13.6899,-13.7173,-15.2281,-16.6925/,
C     6Q7/20.8964,76.6003,67.8036,69.130,70.3809,69.8517,70.8012/,
C     7Q8/0.,0.,0.,0.0,0.0,0.0,0./,
C     8NRXNS/13/
C MHP 9/14 RESTORED BE7+P DERIVATIVES THAT WERE ZEROED OUT
      DATA Q1/0.12317,.03392,.0325,.0304,.03035,.0273,.02494,.040572/,
     1Q2/1.08749,-.273,-.2085,.7630,-0.4044,-1.60,-1.224,-0.2095/,
     2Q3/.93833,-.0648,-.0474,.1626,-.08598,-.3064,-.2139,-0.0595/,
     3Q4/0.,0.,0.,4.79,7.456,0.0,.69703,0.16762/,
     4Q5/0.,0.,0.,2.595,4.032,0.0,0.3097,0.12114/,
     5Q6/-3.3804,-12.2757,-12.826,-13.6899,-13.7173,-15.2281,-16.6925/,
     6Q7/20.8964,76.6003,67.8036,69.130,70.3809,69.8517,70.8012/,
     7Q8/0.,0.,0.,0.0,0.0,0.0,0./,
     8NRXNS/13/
C For different values of SSTANDARD, check the comments in ENGEB
C *********************************************************************
C THE VALUES OF SSTANDARD(I) ARE TO BE CHANGED FROM UNITY IF THE CROSS
C  SECTION FACTORS ARE NOT THE ONES GIVEN IN NEUTRINO ASTROPHYSICS,
C  TABLE 3.2 AND TABLE 3.4 .
C THE CURRENT VALUES ARE CHANGED TO BE THE PREFERRED VALUES LISTED IN
C  THE LAST COLUMN OF TABLE 1 OF BAHCALL AND PINSONNEAULT (1991),
C   6/14/91.
C  THE VALUE OF SSTANDARD(17) FOR HEP IS TAKEN FROM WOLFS ET AL. , PHYS.
C   REV. LETTERS, 63, 2721 (1989).  IT CORRESPONDS TO AN S SUB 0 =
C   15.3E-20 KEV-BARNS, A FACTOR OF 1.913 LARGER THAN FOR THE OLDER
C   MEASUREMENTS USED IN NEUTRINO ASTROPHYSICS.
C *********************************************************************
C ZPRD IS USED IN SCREENING CALCULATIONS. IT IS THE PRODUCT OF THE
C  CHARGES OF THE INTERACTING IONS. ZPRD WAS CHECKED. Z86 IS USED
C  IN CALCULATING INTERMEDIATE SCREENING AND IS DEFINED BY GRABOSKE ET
C  AL, AP. J. 181, PAGE 465 (1973), IN TABLE 4.  Z86 WAS CHECKED AND
C  SOME NUMERICAL VALUES WERE MADE SLIGHTLY MORE ACCURATE.  Z53, Z43,
C  AND Z23 ARE ALSO DEFINED IN TABLE 4 (SEE ABOVE).  SINCE THEY ARE
C  ONLY USED IN STRONG SCREENING, THE VALUES OF Z53, Z43, AND Z23
C  WERE NOT CHECKED.
      DATA ZPRD/1.,4.,4.,6.,6.,7.,8.,12.,16.,12.,14.,12.,36./,Z53/
     1   1.175,3.73,3.73,4.804,4.804,5.385,5.941,9.014,11.24,9.014,
     2   10.15,9.104,23.28/,Z43/0.52,1.31,1.31,1.488,1.488,1.61,1.721,
     3   2.577,3.025,2.577,2.81,2.577,5.668/,Z23/-0.413,-0.655,-0.655,
     4   -0.643,-0.643,-0.659,-0.673,-0.889,-0.946,-0.889,-0.92,-0.889,
     5   -1.36/,Z86/1.630,5.917,5.917,8.302,8.302,9.520,10.716,16.192,
     6   20.978,16.192,18.606,16.192,45.6635/,
     7   C21/5.240358E-8/
      SAVE
C DEFINE NEXT THE FRACTIONAL ABUNDANCES BY MASS OF THE IMPORTANT
C  ISOTOPES.
C X, Y, Z, XHE3,..., XBE9 ARE THE MASS FRACTIONS OF THE ISOTOPES.
C  THE ABUNDANCES OF NEUTRONS, H2, H3, NE20,AND MG24, WHICH ARE,
C  RESPECTIVELY, XFRAC(I) FOR I = 1,3,4,12,13, ARE NO LONGER USED.
      XFRAC(1) = 0.0
      XFRAC(2) = X
      XFRAC(3) = 0.0
      XFRAC(4) = 0.0
      XFRAC(5) = XHE3
      XFRAC(6) = Y
      XFRAC(7) = XC12
      XFRAC(8) = XC13
      XFRAC(9) = XN14
      XFRAC(10) = XO16
      XFRAC(11) = XO18
      XFRAC(12) = 0.0
      XFRAC(13) = 0.0
C *******************************************************************
C BEGIN CALCULATION OF SCREENING CORRECTION.
C *******************************************************************
C  THE BASIC REFERENCES ARE SALPETER, AUSTRALIAN JOURNAL OF PHYSICS,
C  VOL. 7, 373 (1954). THE FORMULA FOR WEAK SCREENING THAT IS BEING
C  PROGRAMMED IS EQUATION (25) OF THIS PAPER. THE OTHER IMPORTANT
C  REFERENCES ARE: DEWITT, GRABOSKE, AND COOPER, AP. J. 181, 439 (1973)
C  AND GRABOSKE ET AL., AP. J. 181, 457 (1973). THE VALUES OF EMU AND
C  ZET ARE ESSENTIAL FOR COMPUTING WEAK SCREENING; THE VALUE OF AMU IS
C  USED IN AN NON-ESSENTIAL WAY IN THIS COMPUTATION. XTR IS USED IN
C  COMPUTING INTERMEDIATE SCREENING.
C AMU IS ONE OVER THE MEAN MOLECULAR WEIGHT OF THE IONS, MU SUB I .
C EMU IS ONE OVER THE ELECTRON MEAN MOLECULAR WEIGHT, MU SUB E.
C  EMU IS USED HERE AS THE NAME FOR THE SECOND PART OF THE ZETA FUNCTION
C  IN THE SALPETER EXPRESSION FOR WEAK SCREENING.
C XTR IS USED LATER IN THE INTERMEDIATE SCREENING CALCULATION.  THE AVERAGE
C  OF THE QUANTITY Z**(3B -1) IS EQUAL TO XTR/AMU.
C ZET IS THE FIRST PART OF THE SALPETER SCREENING ZETA VARIABLE.
C MU = SUM OVER I OF [X(I)/A(I)].
C MU SUB E = SUM OVER I [ Z(I)*X(I)/A(I)].
      AMU = 0.
      EMU = 0.
      XTR = 0.
      ZET = 0.
      DO 10 I = 1,NELEM
         TRM = XFRAC(I)/ANUC(I)
         AMU = AMU+TRM
         EMU = EMU+TRM*ZNUC(I)
         XTR = XTR+TRM*ZNUC(I)**1.58
         ZET = ZET+TRM*ZNUC(I)**2
   10 CONTINUE
C DL AND DT ARE THE THE LOG10 OF THE DENSITY AND TEMPERATURE.
C  THE UNIT OF TEMPERATURE IS 10^9 K AND THE UNIT OF DENSITY IS
C  GM PER CM^3 .
C DD = LOG RHO TO THE BASE 10.
C CLN = LN10.  CLN IS CONVERSION BETWEEN LOG10 AND LN.
C CONVERT DENSITY TO UNLOGGED FORM.
C RWE = RHO/(MU SUB E), I. E., THE NUMBER OF ELECTRONS DIVIDED BY
C  AVOGADRO'S NUMBER.
      RWE = ( EXP(CLN*DL) )*EMU
C THE EXPRESSION FOR RWE WAS INCORRECT IN THE ORIGINAL YALE SUBROUTINE.
C  THE ORIGINAL VERSION HAD ( EXP(CLN*DL) ) DIVIDED BY EMU INSTEAD OF
C  MULTIPLIED BY EMU.  RWE IS USED LATER IN COMPUTING THE SCREENING
C  CORRECTION.
      DD = DL
C SET RATES EQUAL TO ZERO FOR THE LOG_10(T) < TCUT(1)
      IF(TL.LE.TCUT(1)) THEN
         DO 20 I = 1,NRXNS
            RATE(I) = 0.
   20    CONTINUE
         GO TO 200
      ENDIF
C T9P13 IS THE TEMPERATURE IN UNITS OF 10^9 DEGREES K TO THE PLUS 1/3
C  POWER.  MINUS IS DENOTED BY M.  HERE T9 IS THE TEMPERATURE IN UNITS
C  OF 10^9 K, CONVERTED FROM THE LOG_10 (T) AND RHO IS THE DENSITY IN
C  CGS UNITS.
      RHO=EXP(CLN*DD)
      T9 = EXP(CLN*(TL - 9.0D0))
      T9P13 = T9**CC13
      T9P23 = T9P13**2
      T9M13=1./T9P13
      T9M23=T9M13**2
      T9M1=1./T9
      T9M2=T9M1**2
      T9M12=1./DSQRT(T9)
      T9M32=T9M1*T9M12
C ***********************************
C F PRIME/F
C ***********************************
C THE NEXT PIECE OF CODE COMPUTES FIRST THE FERMI ENERGY DIVIDED BY KT, WHERE
C  PFMC2 IS THE FERMI MOMENTUM DIVIDED BY MASS OF ELECTRON TIMES C, ALL
C  SQUARED AND EFMKT IS THE FERMI ENERGY DIVIDED BY KT.  THE QUANTITY
C  FPRF IS THE RATIO OF F PRIME TO F IN SALPETER'S SCREENING CORRECTION.
C  THE VALUE OF FPRF IS DETERMINED IN THE INTERMEDIATE CASE BY AN
C  INTERPOLATION FORMULA DEPENDING UPON THE DEGREE OF DEGENERACY, AS
C  MEASURED BY DEGD = LOG10(E_F/KT). THE NUMERICAL VALUES FOR THE FIT
C  WERE TAKEN FROM SALPETER'S ORIGINAL PAPER, FIGURE 1. THE ONLY CHANGES
C  IN THIS PART OF THE SUBROUTINE WERE THE CORRECTION OF THE ERROR IN
C  THE DEFINITION OF RWE (SEE ABOVE) AND REFINEMENTS OF THE COEFFICIENTS
C  IN THE EXPRESSIONS FOR PFMC2 AND EFMKT.
      PFMC2=1.017677E-4*RWE**0.6666667
      EFMKT=5.92986*T9M1*(DSQRT(1.+PFMC2)-1.)
      IF(EFMKT.LE.1.E-2) THEN
         FPRF=1.0
      ELSE
         DEGD=DLOG10(EFMKT)
         IF(DEGD.GE.1.5) THEN
            FPRF=0.0
         ELSE
            FPRF=0.75793-0.54621*DEGD-0.30964*DEGD**2+0.12535*DEGD**3+
     *      0.1203*DEGD**4-0.012857*DEGD**5-0.014768*DEGD**6
         ENDIF
      ENDIF
C END OF CALCULATION OF FERMI ENERGY DIVIDED BY KT AND THE INTERPOLATION
C  FORMULA FOR (F PRIME/F), WHICH APPEARS IN SALPETER'S SCREENING
C  FORMULA.
C NOW WE GET TO THE COMPUTATION OF WEAK SCREENING (SEE ALSO PAGE 61 OF
C  NEUTRINO ASTROPHYSICS, WHICH GIVES ONLY A SIMPLIFIED FORMULA) AND
C  ALSO THE MORE COMPLICATED INTERMEDIATE AND STRONG SCREENING CASES
C  (SEE REFERENCES TO AP. J. 181 ABOVE FOR INTERMEDIATE
C  AND STRONG SCREENING), ESPECIALLY PAGE 465.
C XXL IS USED IN ALL THE SCREENING FORMULAE.  THIS QUANTITY IS THE
C  IS THE FUNCTION CALLED LAMBDA SUB ZERO BY GRABOSKE ET AL. THE
C  QUANTITY XXL**0.86 = XXL8 IS USED IN CALCULATING INTERMEDIATE
C  SCREENING.
C ZCURL IS THE QUANTITY DEFINED BY EQUATION (4) OF DEWITT ET AL; IT
C  IS THEIR Z WITH A CURLY SYMBOL ON ITS TOP. ZCURL IS USED IN WEAK
C  AND IN INTERMEDIATE SCREENING AND WAS FIRST DEFINED BY SALPETER.
C  ZCURL IS THE SAME AS SALPETER'S ZETA EXCEPT FOR THE FACTOR OF 1/AMU.
C (Z SUB 1 TIMES Z SUB 2)*XXL*ZCURL GIVES THE WEAK SCREENING FACTOR,
C  THE SAME AS SALPETER OR AS EQUATION (19) OF GRABOSKE ET AL.
C Z BAR IS THE SAME AS Z BAR OF DE WITT ET AL AND IS THE AVERAGE CHARGE
C  OF THE IONS.  IT IS EQUAL TO EMU/AMU.  THE QUANTITY (Z BAR)**0.28 =
C  Z28 OCCURS IN THE COMPUTATION OF INTERMEDIATE SCREENING.
C XXL6 IS USED FOR COMPUTING STRONG STRONG SCREENING.
C THE NOTATION USED HERE IS EXPLAINED IN LARGE PART BY
C  EQUATION (4) OF DEWITT ET AL., AP. J. 181, PAGE 439.
C THE FINAL EXPRESSION FOR WEAK SCREENING IS EXACTLY EQUAL TO SALPETER'S
C  FORMULA, WHICH INCLUDES A DEGENERACY CORRECTION. THE MORE GENERAL
C  EXPRESSIONS ARE GIVEN IN TABLE 4 AND EQUATION (19) OF GRABOSKE ET AL.
      XXL=5.9426E-6*T9M32*DSQRT(RHO*AMU)
      XXL6=XXL**0.666667
      XXL8=XXL**0.86
      ZCURL=DSQRT((ZET+FPRF*EMU)/AMU)
      ZBAR=EMU/AMU
      Z58=ZCURL**0.58
      Z28=ZBAR**0.28
      Z33=ZBAR**CC13
      TM1=XXL*ZCURL
C COMPUTE SCREENING FOR EACH OF THE REACTIONS.
      DO 30 I=1,NRXNS
         UWK=TM1*ZPRD(I)
         IF(UWK.LE.WEAKSCREENING) THEN
C WEAKSCREENING IS A NUMERICAL PARAMETER PASSED IN THE FLUX COMMON
C  BLOCK. TO OBTAIN THE GRABOSKE ET AL. AND SALPETER STANDARD RESULTS,
C  USE: WEAKSCREENING = 0.03.  FOR THE STANDARD SOLAR MODEL, THIS IS THE
C  VALUE THAT SHOULD BE ADOPTED. TO INVESTIGATE THE EFFECT OF ALWAYS USING
C  WEAK SCREENING, USE A LARGE VALUE FOR WEAKSCREENING, E. G., 30.  AS
C  LONG AS WEAKSCREENING IS ASSUMED TO BE BIGGER THAN ONE, THE PROGRAM
C  WILL ALWAYS CALCULATE FOR THE SUN WITH THE WEAK SCREENING
C  APPROXIMATION.
C UTOT IS THE FINAL SCREENING CORRECTION WHICH APPEARS IN THE
C  RATE EXPRESSION AS: EXP(UTOT) .
            UTOT(I)=UWK
         ELSE
            UINT=0.38*XXL8*XTR*Z86(I)/(AMU*Z58*Z28)
            IF(UWK.LE.2.) THEN
               UTOT(I)=UINT
            ELSE
               USTR=0.624*Z33*XXL6*(Z53(I)+0.316*Z33*Z43(I)+0.737*
     *         Z23(I)/(ZBAR*XXL6))
               IF(USTR.LT.UINT.OR.UWK.GE.5.) THEN
                  UTOT(I)=USTR
               ELSE
                  UTOT(I)=UINT
               ENDIF
            ENDIF
         ENDIF
   30 CONTINUE
C ****************************************************************
C END OF SCREENING CALCULATION. WEAK AND INTERMEDIATE SCREENING FORMS
C  ARE GIVEN CORRECTLY.  STRONG SCREENING WAS NOT CHECKED BECAUSE IT IS
C  NOT RELEVANT FOR THE SUN.
C ****************************************************************
      NZ=1
      IF(X.EQ.0.0) THEN
         F1=0.
         F3=0.
         GOTO 50
      ENDIF
      NZ=8
C **************************************************************
C  CALCULATE REACTION RATES FOR THE THREE PRINCIPAL REACTIONS OF
C   THE PP CHAIN: PP, HE3+HE3, HE3 +HE4, AND THE FOUR PROTON
C   BURNING REACTIONS ON C12: C13, N14, AND O16.
C **************************************************************
C R1 IS (T SUB 9)^(-3/2) TIMES (S SUB EFF)/(S SUB 0). THE CORRECT
C  EXPRESSION FOR (S SUB EFF)/(S SUB 0) IS GIVEN IN EQUATION 3.14 OF
C  NEUTRINO ASTROPHYSICS.  THE NUMERICAL FORM THAT IS USED IS EQUATION
C  52 OF FOWLER, CAUHLAN, AND ZIMMERMAN, VOL. 5, 1967.
C RATE(I) IS THE RATE OF THE DIFFERENT REACTIONS PER SECOND PER GRAM,
C  EXCEPT THAT THE MASS FRACTIONS ARE OMITTED.
C THE PREVIOUS YALE VERSION HAD THE RATE FOR THE O16 + P REACTION
C  MULTIPLIED BY T9**(-1/7). THIS FACTOR IS INCORRECT AND HAS BEEN
C  REMOVED; IT APPEARED BEFORE AS AN IF STATEMENT REFERRING ONLY TO
C  RATE(7).
      DO 40 I=1,7
C         R1=T9M23+Q1(I)*T9M13+Q2(I)+Q3(I)*T9P13+Q4(I)*T9P23+Q5(I)*T9
C MHP 8/14 RATES CORRECTED TO PERMIT USER MODIFICATION OF REACTION
C RATE DERIVATIVES
         R1=T9M23+Q1(I)*T9M13+QS0E(I)*(Q2(I)+Q3(I)*T9P13)+
     *      QQS0EE(I)*(Q4(I)*T9P23+Q5(I)*T9)
         RATE(I)=RHO*R1*EXP(Q6(I)*T9M13+Q7(I)+(Q8(I)*T9)**2+UTOT(I))
         RATE(I) = RATE(I)*SSTANDARD(I)
         IF(RATE(I).LT.1.E-30) RATE(I)=0.0D0
   40 CONTINUE
C ***************************************************************
C END OF CALCULATION OF REACTION RATES FOR FIRST 7 REACTIONS.
C ***************************************************************
C ***********************************************
C BE7 BURNING
C ***********************************************
C CALCULATE THE BURNING OF BE7 BY PROTONS (WHICH PRODUCES THE MOST
C  EXPERIMENTALLY ACCESSIBLE SOLAR NEUTRINOS) AND THE BURNING OF BE7
C  BY ELECTRON CAPTURE (THE DOMINANT PROCESS). THE PREVIOUSLY USED
C  EXPRESSION CONTAINED A TERM THAT WAS PHYSICALLY INCORRECT
C  AND ALSO USED CROSS SECTION FACTORS THAT WERE OUT OF DATE.
C THE ELECTRON CAPTURE RATE IN SEC^(-1) IS GIVEN BY EQUATION (3.18) OF
C  NEUTRINO ASTROPHYSICS.  CAN OMIT THE FACTOR OF RHO (IN CGS UNITS)
C  WHICH ALSO APPEARS IN THE BE7PROTON RATE. THE BE7 PROTON CAPTURE
C  RATE IS GIVEN BY TABLE 3.2 AND EQUATION (3.12).  NOTE THAT 1 OVER MU
C  SUB E IS EQUAL TO EMU.
C HERE WE USE THE NOTATION BE7 + PROTON = BE7PROTON AND
C  BE7 + E = BE7ELECTRON.
C F1 IS THE FRACTION OF THE BE7 THAT IS BURNED BY ELECTRON CAPTURE.
C 1-F1 IS THE FRACTION OF THE BE7 THAT IS BURNED BY PROTON CAPTURE.
C F3 IS THE FRACTION OF THE N14 THAT IS BURNED BY P, ALPHA REACTION.
C 1-F3 IS THE FRACTION OF THE N15 THAT IS BURNED BY P, GAMMA REACTION.
C SEE TABLE 21 OF BAHCALL AND ULRICH (1988), REV. MOD. PHYS. 60.
c 10/13/97. I changed Temp3 (i.e., tau) by 5/10^5 as a result of
c using bare nuclear masses. Previously coefficient was -10.26202.
c I did not change Be7electron for the slightly different S0 for
c electron capture, since that is done in SStandard. Previous coefficient
c in Be7electron expression was (3.126571E+5). 10/14/97.
c
         BE7ELECTRON = (1.752E-10)*T9M12*(1.0 + 0.004*(1000.*T9-16.))
         BE7ELECTRON = BE7ELECTRON*EMU*SSTANDARD(15)
         TEMP3 = (-10.2625*T9M13)
         BE7PROTON = (3.128813E+5)*X*SSTANDARD(16)*EXP(TEMP3)

C INCLUDE FOR BE7PROTON THE T9M23 FACTOR AND ALL CORRECTIONS PROPORTIONAL TO
C  Q1,...,Q5 FROM EQUATION 3.14 OF NEUTRINO ASTROPHYSICS. THESE
C  CORRECTIONS ARE DEFINED EARLIER IN THIS SUBROUTINE.
C         QRBE7 = T9M23 + Q1(8)*T9M13 + Q2(8)+ Q3(8)*T9P13
C     $          + Q4(8)*T9P23 + Q5(8)*T9
C MHP 9/14 ADDED THE ABILITY TO ALTER DERIVATIVES INDEPENDENTLY
         QRBE7 = T9M23 + Q1(8)*T9M13 + QS0E(8)*(Q2(8)+ Q3(8)*T9P13)
     $          + QQS0EE(8)*(Q4(8)*T9P23 + Q5(8)*T9)
         BE7PROTON = BE7PROTON*QRBE7
C CALCULATE THE SCREENING CORRECTION FOR BE7 + P REACTION.  USE WEAK AND
C  INTERMEDIATE SCREENING FORMULAE.
         ZPRDBE7P = 4.0
         Z86BE7P = 5.7790
         UWK = TM1*ZPRDBE7P
         IF(UWK.LE.WEAKSCREENING) THEN
            UTOTBE7P = UWK
         ELSE
            UINT = 0.38*XXL8*XTR*Z86BE7P/(AMU*Z58*Z28)
            UTOTBE7P = UINT
          END IF
         BE7PROTON = BE7PROTON*EXP(UTOTBE7P)
C END OF CALCULATION OF SCREENING CORRECTION FOR BE7 + P REACTION.
C MULTIPLY RATES BY FACTOR OF RHO/[(ATOMIC MASS UNIT)*A(BE7)] TO GET
C  IN UNITS OF GM^{-1}.  CALL FACTOR CAMUBE7. Corrected for Be7 bare
c  mass number. Previously used 8.582295E+22 in the expression below.
c  These corrections were made on 10/14/97.
c
         CAMUBE7 = RHO*8.584981E+22
         BE7PROTON = CAMUBE7*BE7PROTON
         BE7ELECTRON = CAMUBE7*BE7ELECTRON
C END OF MULTIPLICATION INSERTED NOVEMBER 6, 1990.
         F1 = BE7ELECTRON/(BE7ELECTRON + BE7PROTON)
C *********************************************************************
C END OF CALCULATION OF CRUCIAL BE7 ELECTRON CAPTURE AND PROTON CAPTURE
C  RATES AND THEIR RATIO.
C *********************************************************************
C ***************************
C N15 + P BRANCHING.
C ***************************
C THE FOLLOWING STATEMENTS COMPUTE THE BRANCHING OF N15(P,ALPHA)C12
C  AND N15(P,GAMMA)O16. THESE STATEMENTS REPLACE OUTDATED STATEMENTS
C  IN THE YALE CODE. THE CNO CROSS-SECTION FACTORS ARE FROM TABLE 3.4
C  OF NEUTRINO ASTROPHYSICS.  THE RATIO OF THE REACTIONS DEPENDS ONLY
C  UPON THE EFFECTIVE ZERO ENERGY S-FACTOR, WHICH IS S0(ZERO ENERGY)
C  TIMES THE COMBINATION OF TEMPERATURE AND S-FACTOR DERIVATIVES/S0
C  THAT WAS USED PREVIOUSLY AS R1 IN THE RATE CALCULATIONS. THE
C  NUMERICAL COEFFICIENTS THAT APPEAR IN THE RATE WERE REPRESENTED
C  BY THE Q1(J),...Q5(J) FOR THE OTHER REACTIONS.
C  THE QVALUES FOR THE N15 REACTIONS HAVE BEEN COMPUTED SEPARATELY.
C DBG 8/94 APPLIED MHP UPDATE TO NUCLEAR REACTIONS
c  To agree with Solar Fusion Workshop paper, the value of S0 in keV-b
c  has been changed from 78000. to 675000. on 9/25/97. On 10/14/97,
c  JNB changed cues.f so as to compute the Q-coefficients for the N15 + p
c  reactions.  Also, checked that the coefficients are the same as the
c  ones given earlier in energy.f when we use the older CNO data.
c
      O16GAMMA = T9M23 + 0.0273016*T9M13 + 0.14374 + 0.027490*T9P13
     $          + 6.14685*T9P23 + 2.98940*T9
C MULTIPLY BY THE VALUE OF S0 IN KEV-B.
C      O16GAMMA = O16GAMMA*64.
C MHP 9/14 ADDED THE OPTION TO MODIFY THE RELATIVE CROSS SECTIONS
C FOR N15+P -> C12+ALPHA AND O16+GAMMA
      O16GAMMA = O16GAMMA*64*FO16
C
      C12ALPHA = T9M23 + 0.0273016*T9M13 + 2.01186 + 0.384763*T9P13
     $          + 17.0579*T9P23 + 8.29580*T9
C      C12ALPHA = C12ALPHA*67500.
      C12ALPHA = C12ALPHA*67500*FC12
      F3 = C12ALPHA/(C12ALPHA + O16GAMMA)
      F4 = 1.0D0 - F3
C END OF NEW ROUTINE FOR THE BRANCHING OF N15 + P .
   50 DO 60 I=NZ,NRXNS
         RATE(I)=0.
   60 CONTINUE
C***MHP 3/91 ALPHA CAPTURE REACTIONS UPDATED TO CAUGHLAN AND FOWLER(1988)
C   RATES.  THE RATES ARE EXPRESSED IN THE SAME TERMS USED BY CZ, WITH
C   THE CONVERSION FACTOR IN THE FRONT OBTAINED FROM VANDENBERG'S
C   NOTES ON THE REACTION RATES.
C  RATE(8)  HE4+C13
C  RATE(10) HE4+C12=>O16
C  RATE(11) HE4+N14=>O18
C  RATE(12) TRIPLE ALPHA
      IF(TL.LT.TCUT(4)) GO TO 100
C C13(ALPHA,N) O16
      R1=T9M23+0.0129D0*T9M13+2.04D0+0.184D0*T9P13
      A1 = 6.77D15*EXP(-32.329D0*T9M13-(T9/1.284D0)**2)
      A2 = 3.82D5*EXP(-9.373*T9M1)
      A3 = 1.41D6*EXP(-11.873*T9M1)
      A4 = 2.00D9*EXP(-20.409*T9M1)
      A5 = 2.92D9*EXP(-29.283*T9M1)
      RATE(8) = 1.157126D22*RHO*EXP(UTOT(8))*(A1*R1+T9M32*
     *     (A2+A3+A4+A5))
C C12(ALPHA,GAMMA)O16
      R1 = 1.0D0/(1.0D0+0.0489D0*T9M23)
      R2 = 1.0D0/(1.0D0+0.2654D0*T9M23)
      A1 = T9M2*EXP(-32.120*T9M13)
      A2 = 1.04D8*R1**2*EXP(-(T9/3.496)**2)
      A3 = 1.76D8*R2**2
      A4 = 1.25D3*T9M32*EXP(-27.499*T9M1)
      A5 = 1.43D-2*T9**5*EXP(-15.541*T9M1)
      RATE(10) = 1.25388D22*RHO*EXP(UTOT(10))*(A1*(A2+A3)+A4+A5)
C N14(ALPHA,GAMMA)F18 + F18=>O18+EPLUS+NU
      R1 = T9M23+0.012D0*T9M13+1.45D0+0.177D0*T9P13+1.97D0*T9P23
     *     +0.406D0*T9
      A1 = 7.78D9*EXP(-36.031D0*T9M13-(T9/0.881D0)**2)
      A2 = T9M32*2.36D-10*EXP(-2.798D0*T9M1)
      A3 = T9M32*2.03D0*EXP(-5.054D0*T9M1)
      A4 = T9M23*1.15D4*EXP(-12.310*T9M1)
      RATE(11)= 1.07452D22*RHO*EXP(UTOT(11))*(A1*R1+A2+A3+A4)
C TRIPLE ALPHA
      RATE(12) = 1.565315D21*RHO**2*T9M1*T9M2*2.79E-8*
     *           EXP(-4.4027*T9M1+UTOT(12))
  100 CONTINUE
      RATE(9) = 0.0D0
      RATE(13) = 0.0D0
C END OF XEROING OUT OF REACTIONS 9 AND 13.
      DO 130 I=1,NRXNS
         IF(RATE(I).LE.1.E-5) RATE(I) = 0.0
  130 CONTINUE
C ******************************************************
C RATES PER 10^9 YEARS PER ATOMIC MASS UNIT: HRK(IU)
C ******************************************************
C HR1, ..., HR13 ARE THE RATES OF THE INDIVIDUAL REACTIONS.
C  THE INTERPRETATION OF WHICH REACTION GOES WITH WHICH SYMBOL CAN BE
C  MADE EASILY BY LOOKING AT THE DEFINITIONS OF THE EG(I)'S.
C  THE ABUNDANCES ARE UPDATED IN SUBROUTINE KEMCOM USING THESE MATRICES.
C C21 IS THE PRODUCT OF (10^9 YEARS/1 SECOND)*(1 ATOMIC MASS UNIT/1
C  GRAM). I HAVE USED HERE SIDEREAL YEAR IN CONVERTING TO SECONDS.
  200 HR1(IU)=RATE(1)*C21
      HR2(IU)=RATE(2)*C21
      HR3(IU)=RATE(3)*C21
      HR4(IU)=RATE(4)*C21
      HR5(IU)=RATE(5)*C21
      HR6(IU)=RATE(6)*C21
      HR7(IU)=RATE(7)*C21
      HR8(IU)=RATE(8)*C21
      HR9(IU)=RATE(9)*C21
      HR10(IU)=RATE(10)*C21
      HR11(IU)=RATE(11)*C21
      HR12(IU)=RATE(12)*C21
      HR13(IU)=RATE(13)*C21
      HF1(IU)=F3
      HF2(IU)=F1
C ****************************************
C END OF COMPUTATION OF HRK(IU).
C ****************************************
      RETURN
      END
