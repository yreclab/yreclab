C
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C     OP953D
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      SUBROUTINE OP953D(O,OL,QOD,QOT)
C MHP 7/98 GET OPAL95 OPACITY FROM TABLE AT FIXED Z
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT LOGICAL*4(L)
      PARAMETER (NUMT=70)
      PARAMETER (NUMD=19)
      PARAMETER (NUMX=10)
      PARAMETER (NUMZ=13)
      PARAMETER (NUMXZ=126)
C AS ABOVE FOR THE MODEL Z.
      COMMON /LLOT95/CAPPA(NUMX,NUMT,NUMD),ZTAB
C INDICES FOR INTERPOLATION IN Z,X,T, AND R
      COMMON/OP95INDX/JZ,JX(4,4),JT,JD(4)
C INTERPOLATION FACTORS FOR Z,X,T, AND R, AS WELL AS DERIVATIVE
C FACTORS FOR T AND RHO.
      COMMON/OP95FACT/BZ(4),BX(4,4),BT(4),BTT(4),BD(4,4),BDD(4,4)
C DATA FOR LINEAR EXTRAPOLATION WHEN OUTSIDE TABLE IN RHO
      COMMON/OP95EXT/RL,RLL,RLH(4),LEXTL,LEXTH,LEXH(4)
      DIMENSION C(4),QC(4),CC(4),CCT(4),CCD(4)
      SAVE
C FIND OPACITY AT EACH OF THE 4 NEARBY VALUES OF X
      DO J = 1,4
         IX = JX(1,J)
C FOR EACH X, GET CAPPA FOR 4 VALUES OF T
         DO I = 1,4
            IT = JT + I - 1
C FOR EACH T, GET CAPPA FOR 4 VALUES OF R
            ID = JD(I)
C LOG CAPPA AT DESIRED RHO FOR EACH OF THE 4 DESIRED T.
            C(I) = BD(I,1)*CAPPA(IX,IT,ID)+ BD(I,2)*CAPPA(IX,IT,ID+1)
     *         + BD(I,3)*CAPPA(IX,IT,ID+2) + BD(I,4)*CAPPA(IX,IT,ID+3)
C D LOG CAPPA/D LOG R FOR EACH OF THE 4 DESIRED T.
            QC(I)=BDD(I,1)*CAPPA(IX,IT,ID)+BDD(I,2)*CAPPA(IX,IT,ID+1)
     *          +BDD(I,3)*CAPPA(IX,IT,ID+2)+BDD(I,4)*CAPPA(IX,IT,ID+3)
         END DO
C CHECK ON WHETHER RHO IS OUTSIDE OF THE TABLE AND NEEDS EXTRAPOLATION
         IF(LEXTL)THEN
C FACTOR IN R
            DELTAR = RL-RLL
C CORRECT CAPPA BY USING THE DERIVATIVE AT THE BOUNDARY.
            DO I = 1,4
               C(I) = C(I)+DELTAR*QC(I)
            END DO
         ELSE IF(LEXTH)THEN
            DO I = 1,4
               IF(LEXH(I))THEN
                  DELTAR = RL - RLH(I)
                  C(I) = C(I)+DELTAR*QC(I)
               ENDIF
            END DO
         ENDIF
C INTERPOLATE FOR LOG CAPPA IN T.
         CC(J) = BT(1)*C(1) + BT(2)*C(2) + BT(3)*C(3) + BT(4)*C(4)
C D LOG CAPPA/D LOG T
         CCT(J) = BTT(1)*C(1) + BTT(2)*C(2) + BTT(3)*C(3) + BTT(4)*C(4)
C D LOG CAPPA/D LOG R
         CCD(J) = BT(1)*QC(1) + BT(2)*QC(2) + BT(3)*QC(3) + BT(4)*QC(4)
      END DO
C INTERPOLATE FOR LOG CAPPA IN X.
      OL = BX(1,1)*CC(1) + BX(1,2)*CC(2) + BX(1,3)*CC(3) +
     *     BX(1,4)*CC(4)
C INTERPOLATE FOR QOT IN X.
      QOT = BX(1,1)*CCT(1) + BX(1,2)*CCT(2) + BX(1,3)*CCT(3) +
     *      BX(1,4)*CCT(4)
C INTERPOLATE FOR QOD IN X.
      QOD = BX(1,1)*CCD(1) + BX(1,2)*CCD(2) + BX(1,3)*CCD(3) +
     *      BX(1,4)*CCD(4)
C CORRECT FROM DERIVATE AT FIXED R TO DERIVATIVE AT FIXED RHO.
      QOT = QOT - 3.0D0*QOD
      O = 1.0D1**OL
      RETURN
      END
