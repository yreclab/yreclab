C
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C     OP954D
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      SUBROUTINE OP954D(O,OL,QOD,QOT)
C MHP 7/98 GET OPAL95 OPACITY FROM FULL SET OF OPACITY TABLES.
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT LOGICAL*4(L)
      PARAMETER (NUMT=70)
      PARAMETER (NUMD=19)
      PARAMETER (NUMX=10)
      PARAMETER (NUMZ=13)
      PARAMETER (NUMXZ=126)
C FULL SET OF TABLES: OPACITY AS A FUNCTION OF Z AND X, T, RHO/T6**3
C TABLES ARE INCREMENTED IN SETS OF NZ*NX.  SO THE TABLES FOR THE
C THIRD METAL ABUNDANCE (3 X 10**-4)BEGIN AT TABLE 21 AND END AT TABLE 30.
C FOR THE HIGH VALUES OF Z, THE NUMBER OF X TABLES IS NOT THE SAME (I.E.
C X<0.9 FOR Z=0.1).
C FOR EACH COMPOSITION A FULL GRID IN (T,RHO/T6**3) IS RETAINED.
      COMMON /LLOT95A/TGR(NUMT),XXG(NUMX),R0GR(NUMD),ZZG(NUMZ),
     *                CAPPA2(NUMXZ,NUMT,NUMD),NUMXM(NUMZ),NZ(NUMZ)
C INDICES FOR INTERPOLATION IN Z,X,T, AND R
      COMMON/OP95INDX/JZ,JX(4,4),JT,JD(4)
C INTERPOLATION FACTORS FOR Z,X,T, AND R, AS WELL AS DERIVATIVE
C FACTORS FOR T AND RHO.
      COMMON/OP95FACT/BZ(4),BX(4,4),BT(4),BTT(4),BD(4,4),BDD(4,4)
C DATA FOR LINEAR EXTRAPOLATION WHEN OUTSIDE TABLE IN RHO
      COMMON/OP95EXT/RL,RLL,RLH(4),LEXTL,LEXTH,LEXH(4)
      DIMENSION C(4),QC(4),CC(4),CCT(4),CCD(4),CZ(4),CZT(4),CZD(4)
      SAVE
C FIND OPACITY AT EACH OF THE 4 NEARBY VALUES OF Z
      DO K = 1,4
         IZ = NZ(JZ+K-1)
C FIND OPACITY AT EACH OF THE 4 NEARBY VALUES OF X
         DO J = 1,4
            IXZ = JX(K,J)+IZ
C FOR EACH X, GET CAPPA FOR 4 VALUES OF T
            DO I = 1,4
               IT = JT + I - 1
C FOR EACH T, GET CAPPA FOR 4 VALUES OF R
               ID = JD(I)
C LOG CAPPA AT DESIRED RHO FOR EACH OF THE 4 DESIRED T.
               C(I) = BD(I,1)*CAPPA2(IXZ,IT,ID) +
     *                BD(I,2)*CAPPA2(IXZ,IT,ID+1)
     *               + BD(I,3)*CAPPA2(IXZ,IT,ID+2)
     *              + BD(I,4)*CAPPA2(IXZ,IT,ID+3)
C D LOG CAPPA/D LOG R FOR EACH OF THE 4 DESIRED T.
               QC(I) = BDD(I,1)*CAPPA2(IXZ,IT,ID)
     *               + BDD(I,2)*CAPPA2(IXZ,IT,ID+1)
     *                + BDD(I,3)*CAPPA2(IXZ,IT,ID+2)
     *               + BDD(I,4)*CAPPA2(IXZ,IT,ID+3)
            END DO
C CHECK ON WHETHER RHO IS OUTSIDE OF THE TABLE AND NEEDS EXTRAPOLATION
            IF(LEXTL)THEN
C FACTOR IN R
               DELTAR = RL-RLL
C CORRECT CAPPA BY USING THE DERIVATIVE AT THE BOUNDARY.
               DO I = 1,4
                  C(I) = C(I)+DELTAR*QC(I)
               END DO
            ELSE IF(LEXTH)THEN
               DO I = 1,4
                  IF(LEXH(I))THEN
                     DELTAR = RL - RLH(I)
                     C(I) = C(I)+DELTAR*QC(I)
                  ENDIF
               END DO
            ENDIF
C INTERPOLATE FOR LOG CAPPA IN T.
            CC(J) = BT(1)*C(1) + BT(2)*C(2) + BT(3)*C(3)
     *            + BT(4)*C(4)
C D LOG CAPPA/D LOG T
            CCT(J) = BTT(1)*C(1) + BTT(2)*C(2) + BTT(3)*C(3)
     *             + BTT(4)*C(4)
C D LOG CAPPA/D LOG R
            CCD(J) = BT(1)*QC(1) + BT(2)*QC(2) + BT(3)*QC(3)
     *             + BT(4)*QC(4)
         END DO
C INTERPOLATE FOR LOG CAPPA IN X.
         CZ(K) = BX(K,1)*CC(1) + BX(K,2)*CC(2) + BX(K,3)*CC(3) +
     *           BX(K,4)*CC(4)
C INTERPOLATE FOR QOT IN X.
         CZT(K) = BX(K,1)*CCT(1) + BX(K,2)*CCT(2) + BX(K,3)*CCT(3) +
     *            BX(K,4)*CCT(4)
C INTERPOLATE FOR QOD IN X.
         CZD(K) = BX(K,1)*CCD(1) + BX(K,2)*CCD(2) + BX(K,3)*CCD(3) +
     *            BX(K,4)*CCD(4)
      END DO
C INTERPOLATE FOR LOG CAPPA IN Z.
      OL = BZ(1)*CZ(1) + BZ(2)*CZ(2) + BZ(3)*CZ(3) +
     *     BZ(4)*CZ(4)
C INTERPOLATE FOR QOT IN Z.
      QOT = BZ(1)*CZT(1) + BZ(2)*CZT(2) + BZ(3)*CZT(3) +
     *     BZ(4)*CZT(4)
C INTERPOLATE FOR QOD IN Z.
      QOD = BZ(1)*CZD(1) + BZ(2)*CZD(2) + BZ(3)*CZD(3) +
     *     BZ(4)*CZD(4)
C CORRECT FROM DERIVATE AT FIXED R TO DERIVATIVE AT FIXED RHO.
      QOT = QOT - 3.0D0*QOD
      O = 1.0D1**OL
      RETURN
      END
